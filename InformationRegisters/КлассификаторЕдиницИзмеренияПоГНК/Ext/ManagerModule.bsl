
Процедура ОбновитьКлассификатор() Экспорт
	
	Макет = РегистрыСведений.КлассификаторЕдиницИзмеренияПоГНК.ПолучитьМакет("Классификатор");
	
	НаборЗаписей = РегистрыСведений.КлассификаторЕдиницИзмеренияПоГНК.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();

	ОбластьКлассификатор = Макет.ПолучитьОбласть("Классификатор");
	
	Для Стр = 2 По ОбластьКлассификатор.ВысотаТаблицы Цикл
		
		Набор = НаборЗаписей.Добавить();
		
		// Код
		ЯчейкаКод = ОбластьКлассификатор.Область(Стр, 1, Стр, 1); 
		Если ЯчейкаКод.СодержитЗначение Тогда
			Набор.Код = ЯчейкаКод.Значение;
		Иначе
			Набор.Код = ЯчейкаКод.Текст;
		КонецЕсли;
		
		// Наименование
		ЯчейкаНаименование = ОбластьКлассификатор.Область(Стр, 2, Стр, 2); 
		Если ЯчейкаНаименование.СодержитЗначение Тогда
			Набор.Наименование = НРег(ЯчейкаНаименование.Значение);
		Иначе
			Набор.Наименование = НРег(ЯчейкаНаименование.Текст);
		КонецЕсли;
		
		// КраткоеНаименование
		ЯчейкаКраткоеНаименование = ОбластьКлассификатор.Область(Стр, 3, Стр, 3); 
		Если ЯчейкаКраткоеНаименование.СодержитЗначение Тогда
			Набор.КраткоеНаименование = НРег(ЯчейкаКраткоеНаименование.Значение);
		Иначе
			Набор.КраткоеНаименование = НРег(ЯчейкаКраткоеНаименование.Текст);
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();	
	
КонецПроцедуры

Функция НайтиКодЕдиницыИзмеренияПоКлассификаторуГНК(ПолноеНаименованиеЕдИзм) Экспорт
	
	Код = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КлассификаторЕдиницИзмеренияПоГНК.Код
		|ИЗ
		|	РегистрСведений.КлассификаторЕдиницИзмеренияПоГНК КАК КлассификаторЕдиницИзмеренияПоГНК
		|ГДЕ
		|	КлассификаторЕдиницИзмеренияПоГНК.Наименование ПОДОБНО &ПолноеНаименованиеЕдИзм";
	
	Запрос.УстановитьПараметр("ПолноеНаименованиеЕдИзм", НРег(ПолноеНаименованиеЕдИзм));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Код = ВыборкаДетальныеЗаписи.Код;
		
	КонецЕсли;
	
	Возврат Код;
	
КонецФункции