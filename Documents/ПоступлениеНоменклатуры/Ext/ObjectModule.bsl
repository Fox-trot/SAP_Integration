
Перем ТаблицаЗачетаНДС;
Перем ПорядокУплатыНДС;

Функция ПолучитьСтруктуруПечатныхФорм() экспорт
	Возврат Новый Структура("ПриемныйАкт,РасчетСтоимостиНоменклатуры","Приходный акт","Расчет стоимости номенклатуры");
КонецФункции

Процедура Печать(ИмяМакета, КоличествоЭкземпляров=1, НаПринтер=Ложь, НепосредственнаяПечать=Ложь) Экспорт
	Если ИмяМакета="ПриемныйАкт" Тогда
		ТабДокумент = ПечатьПриемныйАкт();
	ИначеЕсли ИмяМакета="РасчетСтоимостиНоменклатуры" Тогда
		ТабДокумент = ПечатьРасчетаСтоимостиНоменклатуры();
	КонецЕсли; 
	НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Расчет стоимости");
КонецПроцедуры

Функция ПечатьРасчетаСтоимостиНоменклатуры() Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("РасчетСтоимости");
	
	Руководители = ОтветственныеЛица(Организация, Дата);	
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Организация = Организация;
	Область.Параметры.Склад = Склад;
	Область.Параметры.Номер = ПолучитьНомерНаПечать(ЭтотОбъект);
	Область.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	Область.Параметры.Поставщик = Поставщик;
	Область.Параметры.ДоговорПоставщика	= ДоговорПоставщика;
	Область.Параметры.ПриходныйДокумент = ПриходныйДокумент;
	ТабДок.Вывести(Область);
	
	
	ОбластьНачало = Макет.ПолучитьОбласть("ШапкаТаблицы|Начало");
	ОбластьДопРасход = Макет.ПолучитьОбласть("ШапкаТаблицы|ДопРасход");
	ОбластьИтог= Макет.ПолучитьОбласть("ШапкаТаблицы|ВертИтог");
	
	ТабДок.Вывести(ОбластьНачало);
	ИмяСекции="Доп. расходы";
	Для Каждого Строка из РасходыВключаемыеВСтоимость Цикл
		ОбластьДопРасход.Параметры.ИмяСекции=ИмяСекции;
		ОбластьДопРасход.Параметры.Описание=Строка.Описание;
		ТабДок.Присоединить(ОбластьДопРасход);
		ИмяСекции="";
	КонецЦикла;	
	ТабДок.Присоединить(ОбластьИтог);
	
	НомерСтроки=0;
	ОбластьНачало = Макет.ПолучитьОбласть("Строка|Начало");
	ОбластьДопРасход = Макет.ПолучитьОбласть("Строка|ДопРасход");
	ОбластьИтог= Макет.ПолучитьОбласть("Строка|ВертИтог");
	
	ТаблицаНоменклатура=СформироватьТаблицуНоменклатуры();

	Для каждого СтрокаНоменклатуры Из ТаблицаНоменклатура  Цикл
		НомерСтроки=НомерСтроки+1;
		ОбластьНачало.Параметры.НомерСтроки = НомерСтроки;
		ОбластьНачало.Параметры.ЕдиницаИзмерения = СтрокаНоменклатуры.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьНачало.Параметры.Заполнить(СтрокаНоменклатуры);
		ТабДок.Вывести(ОбластьНачало);
		
		СуммаДопРасходов=0;
		Для Каждого Строка из РасходыВключаемыеВСтоимость Цикл
			ОбластьДопРасход.Параметры.Сумма=СтрокаНоменклатуры["РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки)];
			СуммаДопРасходов=СуммаДопРасходов+СтрокаНоменклатуры["РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки)];
			ТабДок.Присоединить(ОбластьДопРасход);
		КонецЦикла;	
		ОбластьИтог.Параметры.СуммаДопРасходов=СуммаДопРасходов;
		ОбластьИтог.Параметры.ОбщаяСумма=СтрокаНоменклатуры.Сумма+СуммаДопРасходов;
		Цена=0;
		Если СтрокаНоменклатуры.Количество<>0 Тогда
			Цена=Окр((СтрокаНоменклатуры.Сумма+СуммаДопРасходов)/СтрокаНоменклатуры.Количество,2);
		Иначе
			Цена=СтрокаНоменклатуры.Сумма+СуммаДопРасходов;
		КонецЕсли;	
		ОбластьИтог.Параметры.ЦенаЕдиницы=Цена;
		ТабДок.Присоединить(ОбластьИтог);
	КонецЦикла; 
	
	//Итоги ===========================
	
	ОбластьНачало = Макет.ПолучитьОбласть("Итог|Начало");
	ОбластьДопРасход = Макет.ПолучитьОбласть("Итог|ДопРасход");
	ОбластьИтог= Макет.ПолучитьОбласть("Итог|ВертИтог");
	
	ОбластьНачало.Параметры.Сумма=Номенклатура.Итог("Сумма");
	ОбластьНачало.Параметры.Ответственный=Ответственный;
	
	ОбластьНачало.Параметры.СуммаНДС=Номенклатура.Итог("СуммаНДС")+РасходыВключаемыеВСтоимость.Итог("СуммаНДС");
	ОбластьНачало.Параметры.Всего=Номенклатура.Итог("Сумма")+РасходыВключаемыеВСтоимость.Итог("Сумма")+Номенклатура.Итог("СуммаНДС")+РасходыВключаемыеВСтоимость.Итог("СуммаНДС");
	
	ТабДок.Вывести(ОбластьНачало);
	
	СуммаДопРасходов=0;
	Для Каждого Строка из РасходыВключаемыеВСтоимость Цикл
		ОбластьДопРасход.Параметры.Сумма=ТаблицаНоменклатура.Итог("РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки));
		СуммаДопРасходов=СуммаДопРасходов+ТаблицаНоменклатура.Итог("РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки));
		ТабДок.Присоединить(ОбластьДопРасход);
	КонецЦикла;	
	ОбластьИтог.Параметры.СуммаДопРасходов=СуммаДопРасходов;
	ОбластьИтог.Параметры.ОбщаяСумма=Номенклатура.Итог("Сумма")+СуммаДопРасходов;
	
	ТабДок.Присоединить(ОбластьИтог);
	
	
	ТабДок.ФиксацияСверху=9;
	ТабДок.ФиксацияСлева=0;
	ТабДок.ЭкземпляровНаСтранице=1;
	ТабДок.ТолькоПросмотр = Истина;
	
	Возврат ТабДок;
КонецФункции

Функция ПечатьПриемныйАкт() Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("ПриемныйАкт");
	
	Руководители = ОтветственныеЛица(Организация, Дата);	
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Организация = Организация;
	Область.Параметры.Номер = ПолучитьНомерНаПечать(ЭтотОбъект);
	Область.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	Область.Параметры.ПриходныйДокумент = ПриходныйДокумент;
	Область.Параметры.Доверенность = "№ "+ПолучитьНомерНаПечать(Доверенность.Ссылка)+" от "+формат(Доверенность.Дата,"ДФ=dd.MM.yyyy");
	Область.Параметры.Через = ФИОСотрудника(Доверенность.Сотрудник,Дата);
	Область.Параметры.Поставщик = Поставщик;
	Область.Параметры.ДоговорПоставщика = ДоговорПоставщика;
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого = Макет.ПолучитьОбласть("Итог");
	
	Запрос=новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПоступлениеНоменклатурыНоменклатура.Номенклатура,
	|	ПоступлениеНоменклатурыНоменклатура.Количество,
	|	ПоступлениеНоменклатурыНоменклатура.Сумма КАК Сумма,
	|	ПоступлениеНоменклатурыНоменклатура.СтавкаНДС,
	|	ПоступлениеНоменклатурыНоменклатура.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеНоменклатурыНоменклатура.СчетУчетаБУ,
	|	ПоступлениеНоменклатурыНоменклатура.Цена,
	|	ПоступлениеНоменклатурыНоменклатура.Сумма + ПоступлениеНоменклатурыНоменклатура.СуммаНДС КАК Всего,
	|	ПоступлениеНоменклатурыНоменклатура.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеНоменклатурыНоменклатура.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Документ.ПоступлениеНоменклатуры.Номенклатура КАК ПоступлениеНоменклатурыНоменклатура
	|ГДЕ
	|	ПоступлениеНоменклатурыНоменклатура.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС),
	|	СУММА(Всего)
	|ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	ВыборкаОбщие=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщие.Следующий();
	ОбластьИтого.Параметры.Заполнить(ВыборкаОбщие);	
	ОбластьИтого.Параметры.Завскладом=ДанныеФизЛица(Организация,Склад.МОЛ.ФизическоеЛицо,Дата).Представление;
	Выборка=ВыборкаОбщие.Выбрать();
	Пока Выборка.Следующий() Цикл
		Область.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Область);
	КонецЦикла; 
	
	ТабДок.Вывести(ОбластьИтого);
	
	ТабДок.ФиксацияСверху=0;
	ТабДок.ФиксацияСлева=0;
	ТабДок.ЭкземпляровНаСтранице=0;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ТабДок.КлючПараметровПечати="КПП_ПоступлениеНоменклатуры";
	
	Возврат ТабДок;
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьЗаполнениеДокумента(Отказ, Заголовок, СтруктураШапкиДокумента)
	СтруктураОбязательныхПолей = Новый Структура("Организация");
	СтруктураОбязательныхПолей.Вставить("Склад","Не выбран склад.");
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика Тогда
		СтруктураОбязательныхПолей.Вставить("Поставщик","Не выбран поставщик.");
		СтруктураОбязательныхПолей.Вставить("ДоговорПоставщика","Не указан договор с поставщиком.");
		СтруктураОбязательныхПолей.Вставить("СчетУчетаРасчетовСКонтрагентом","Не указан счет учета расчетов с поставщиком.");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.Внутренний Тогда
		СтруктураОбязательныхПолей.Вставить("СкладСдатчик","Не выбран склад - сдатчик.");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.СПереработки Тогда
		СтруктураОбязательныхПолей.Вставить("ПодразделениеСдатчик","Не выбрано подразделение - сдатчик.");
	КонецЕсли; 
	ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	//
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Номенклатура","Не выбрана номенклатура");
	СтруктураПолей.Вставить("СчетУчетаБУ","Не указан счет учета номенклатуры (таблица ""Номенклатура"")");
	ПроверитьЗаполнениеТабличнойЧастиПлатежногоДокумента(ЭтотОбъект, "Номенклатура", СтруктураПолей, Отказ, Заголовок);
	//
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("СчетКредит","Не указан счет кредита (таблица ""Дополнительные расходы"")");
	ПроверитьЗаполнениеТабличнойЧастиПлатежногоДокумента(ЭтотОбъект, "РасходыВключаемыеВСтоимость", СтруктураПолей, Отказ, Заголовок);
	//
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("СчетДебет","Не указан счет дебет (таблица ""Прочее"")");
	СтруктураПолей.Вставить("СчетКредит","Не указан счет кредит (таблица ""Прочее"")");
	ПроверитьЗаполнениеТабличнойЧастиПлатежногоДокумента(ЭтотОбъект, "Прочее", СтруктураПолей, Отказ, Заголовок);
КонецПроцедуры

Функция СформироватьТаблицуНоменклатуры()
	ТаблицаНоменклатуры=Номенклатура.Выгрузить();
	ТаблицаНоменклатуры.Колонки.Добавить("РасходыВключаемыеВСтоимость",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(18,2)));
	Для Каждого Строка из РасходыВключаемыеВСтоимость Цикл
		ТаблицаНоменклатуры.Колонки.Добавить("РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки),Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(18,2)));
	КонецЦикла;	
	Если ТаблицаНоменклатуры.Итог("Сумма")<>0 Тогда
		Для Каждого Строка из РасходыВключаемыеВСтоимость Цикл
			СуммаКРаспределению=Строка.Сумма;
			Остаток=СуммаКРаспределению;
			Для Каждого СтрокаТаблицаНоменклатуры из ТаблицаНоменклатуры Цикл
				Сумма=Окр(СуммаКРаспределению*СтрокаТаблицаНоменклатуры.Сумма/ТаблицаНоменклатуры.Итог("Сумма"),2);
				СтрокаТаблицаНоменклатуры["РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки)]=СтрокаТаблицаНоменклатуры["РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки)]+Сумма;
				СтрокаТаблицаНоменклатуры["РасходыВключаемыеВСтоимость"]=СтрокаТаблицаНоменклатуры["РасходыВключаемыеВСтоимость"]+Сумма;
				Остаток=Остаток-Сумма;
			КонецЦикла;	
			Если Остаток<>0 Тогда
				ТаблицаНоменклатуры[0]["РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки)]=ТаблицаНоменклатуры[0]["РасходыВключаемыеВСтоимость"+Строка(Строка.НомерСтроки)]+Остаток;	
				ТаблицаНоменклатуры[0]["РасходыВключаемыеВСтоимость"]=ТаблицаНоменклатуры[0]["РасходыВключаемыеВСтоимость"]+Остаток;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	Возврат ТаблицаНоменклатуры;
КонецФункции	

Процедура ЗаписатьЦеныВСправочник()
	Если ЗаписыватьЦеныВСправочник Тогда
		ТаблицаНоменклатуры=СформироватьТаблицуНоменклатуры();	
		Для Каждого Строка из ТаблицаНоменклатуры Цикл
			НоменклатураОбъект=Строка.Номенклатура.ПолучитьОбъект();
			Цена=0;
			Если Строка.Количество<>0 Тогда
				Цена=(Строка.Сумма+Строка.РасходыВключаемыеВСтоимость)/Строка.Количество;
			КонецЕсли;	
			НоменклатураОбъект.Цена=Цена;
			НоменклатураОбъект.Записать();
		КонецЦикла;	
	КонецЕсли;
КонецПроцедуры

Процедура ПрверитьЗачетНДС(Отказ)
	Для каждого СтрокаТЧ Из Номенклатура Цикл
		Если СтрокаТЧ.СуммаНДС<>0 И НЕ ЗначениеЗаполнено(СтрокаТЧ.ЗачетНДС) Тогда
			Отказ=Истина;
			Сообщить("В таблице НОМЕНКЛАТУРА в строке № "+Строка(СтрокаТЧ.НомерСтроки)+" не заполнен зачет НДС");
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТЧ Из Прочее Цикл
		Если СтрокаТЧ.СуммаНДС<>0 И НЕ ЗначениеЗаполнено(СтрокаТЧ.ЗачетНДС) Тогда
			Отказ=Истина;
			Сообщить("В таблице ПРОЧЕЕ в строке № "+Строка(СтрокаТЧ.НомерСтроки)+" не заполнен зачет НДС");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаписатьПлатежиПоНДС()
	Для Каждого СтрокаТЧ Из Номенклатура Цикл
		Если СтрокаТЧ.СуммаНДС=0 Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ СтрокаТЧ.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.А0800) Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаТЧ.ЗачетНДС<>Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС И
			 СтрокаТЧ.ЗачетНДС<>Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
			Продолжить;
		КонецЕсли; 
		//
		Если ЗначениеЗаполнено(СтрокаТЧ.ПлатежПоНДС) Тогда
			ОбъектПлатеж = СтрокаТЧ.ПлатежПоНДС.ПолучитьОбъект();
		Иначе
			ПлатежПоНДС = ПолучитьПлатежПоНДС(Ссылка,СтрокаТЧ.Номенклатура);
			Если ЗначениеЗаполнено(ПлатежПоНДС) Тогда
				ОбъектПлатеж = ПлатежПоНДС.ПолучитьОбъект();
			Иначе
				ОбъектПлатеж = Справочники.ПлатежиПоНДС.СоздатьЭлемент();
				ОбъектПлатеж.Наименование = СтрокаТЧ.Номенклатура.Наименование;
				ОбъектПлатеж.Номенклатура = СтрокаТЧ.Номенклатура;
				ОбъектПлатеж.ДокументПоступления = Ссылка;
				ОбъектПлатеж.УстановитьНовыйКод();
			КонецЕсли;
		КонецЕсли;
		ОбъектПлатеж.ДатаНачала = Дата;
		ОбъектПлатеж.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		Если СтрокаТЧ.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
			ОбъектПлатеж.КоличествоМесяцев = 36;
		ИначеЕсли СтрокаТЧ.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС Тогда
			ОбъектПлатеж.КоличествоМесяцев = 12;
		КонецЕсли;
		ОбъектПлатеж.ОбменДанными.Загрузка=Истина;
		ОбъектПлатеж.Записать();
		СтрокаТЧ.ПлатежПоНДС=ОбъектПлатеж.Ссылка;
	КонецЦикла;
	Для Каждого СтрокаТЧ Из Прочее Цикл
		Если СтрокаТЧ.СуммаНДС=0 Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ СтрокаТЧ.СчетДебет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.А0800) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.СубконтоД1) Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаТЧ.ЗачетНДС<>Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС И
			 СтрокаТЧ.ЗачетНДС<>Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
			Продолжить;
		КонецЕсли; 
		//
		Если ЗначениеЗаполнено(СтрокаТЧ.ПлатежПоНДС) Тогда
			ОбъектПлатеж = СтрокаТЧ.ПлатежПоНДС.ПолучитьОбъект();
		Иначе
			ПлатежПоНДС = ПолучитьПлатежПоНДС(Ссылка,СтрокаТЧ.СубконтоД1);
			Если ЗначениеЗаполнено(ПлатежПоНДС) Тогда
				ОбъектПлатеж = ПлатежПоНДС.ПолучитьОбъект();
			Иначе
				ОбъектПлатеж = Справочники.ПлатежиПоНДС.СоздатьЭлемент();
				ОбъектПлатеж.Наименование = СтрокаТЧ.СубконтоД1.Наименование;
				ОбъектПлатеж.Номенклатура = СтрокаТЧ.СубконтоД1;
				ОбъектПлатеж.ДокументПоступления = Ссылка;
				ОбъектПлатеж.УстановитьНовыйКод();
			КонецЕсли;
		КонецЕсли;
		ОбъектПлатеж.ДатаНачала = Дата;
		ОбъектПлатеж.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		Если СтрокаТЧ.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
			ОбъектПлатеж.КоличествоМесяцев = 36;
		ИначеЕсли СтрокаТЧ.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС Тогда
			ОбъектПлатеж.КоличествоМесяцев = 12;
		КонецЕсли;
		ОбъектПлатеж.ОбменДанными.Загрузка=Истина;
		ОбъектПлатеж.Записать();
		СтрокаТЧ.ПлатежПоНДС=ОбъектПлатеж.Ссылка;
	КонецЦикла;
	Записать(РежимЗаписиДокумента.Запись);
КонецПроцедуры

Функция СформироватьОписаниеПервичногоДокумента(ПервичныйДокумент)
	ПервичныйДокументСтр=сокрлп(ПервичныйДокумент);	
    Если лев(ПервичныйДокументСтр,1)="№" или ЭтоЦифра(лев(ПервичныйДокументСтр,1)) Тогда
		Возврат "Сч.фак. "+ПервичныйДокументСтр;	
	Иначе
		Возврат ПервичныйДокументСтр;
	КонецЕсли; 
КонецФункции

Процедура ДвиженияПоБУ(Режим, Отказ, Заголовок ,СтруктураШапкиДокумента)
	ПорядокУплатыНДС = ПолучитьПорядокУплатыНДС(Дата,Организация);
	ТаблицаЗачетаНДС = СоздатьТаблицуДляУчетаНДС();
	ПроводкиБУ = Движения.Хозрасчетный;
	Для Каждого СтрокаНоменклатуры Из Номенклатура Цикл
		Проводка = ПроводкиБУ.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание	 = "Поступление номенклатуры на склад";     
		Проводка.ПервичныйДокумент=СформироватьОписаниеПервичногоДокумента(ПриходныйДокумент);     
		//
		Проводка.СчетДт = СтрокаНоменклатуры.СчетУчетаБУ;
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
		Проводка.КоличествоДт = СтрокаНоменклатуры.Количество;
		//
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика Тогда
			Проводка.СчетКт = СчетУчетаРасчетовСКонтрагентом;
			Если ЭтоСчетСАналитикойПоКонтрагентамИДоговорам(СчетУчетаРасчетовСКонтрагентом) Тогда
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Поставщик);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,ДоговорПоставщика);
			Иначе
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Субконто1);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,Субконто2);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,Субконто3);
			КонецЕсли;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.Внутренний Тогда
			Проводка.СчетКт = СтрокаНоменклатуры.СчетУчетаБУ;
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Номенклатура",СтрокаНоменклатуры.Номенклатура);
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Склады",СкладСдатчик);
			Проводка.КоличествоКт = СтрокаНоменклатуры.Количество;
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.СПереработки Тогда
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("2310");
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"Подразделения",ПодразделениеСдатчик);
		КонецЕсли; 
		//
		Проводка.Сумма = СтрокаНоменклатуры.Сумма;
		////
		Если ВидОперации=Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика И СтрокаНоменклатуры.СуммаНДС<>0 Тогда
			Проводка = ПроводкиБУ.Добавить();
			Проводка.Период      = Дата;
			Проводка.Организация = Организация;
			Проводка.Содержание	 = "НДС";     
			Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(ПриходныйДокумент);     
			//
			Если Дата<Дата('20190101') И 
				(СтрокаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеОсновныеСредства		ИЛИ
				 СтрокаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеНематериальныеАктивы	ИЛИ
				 СтрокаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Оборудование) Тогда
				Проводка.СчетДт = СтрокаНоменклатуры.СчетУчетаБУ;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
			ИначеЕсли Дата<Дата('20190101') Тогда
				Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаНоменклатуры.СтавкаНДС);
			ИначеЕсли ПорядокУплатыНДС = Перечисления.ПорядокУплатыНДС.Общеустановленный Тогда
				Если СтрокаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачет Тогда
					Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаНоменклатуры.СтавкаНДС);
				ИначеЕсли СтрокаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС ИЛИ
						  СтрокаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
					Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А4401;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ПлатежиПоНДС",СтрокаНоменклатуры.ПлатежПоНДС);
					Проводка.КоличествоДт = СтрокаНоменклатуры.Количество;
				Иначе
					Проводка.СчетДт = СтрокаНоменклатуры.СчетУчетаБУ;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
				КонецЕсли;
			Иначе
				Проводка.СчетДт = СтрокаНоменклатуры.СчетУчетаБУ;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
			КонецЕсли; 
			//
			Проводка.СчетКт = СчетУчетаРасчетовСКонтрагентом;
			Если ЭтоСчетСАналитикойПоКонтрагентамИДоговорам(СчетУчетаРасчетовСКонтрагентом) Тогда
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Поставщик);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,ДоговорПоставщика);
			Иначе
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Субконто1);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,Субконто2);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,Субконто3);
			КонецЕсли;
			//
			Проводка.Сумма = СтрокаНоменклатуры.СуммаНДС;
		КонецЕсли; 
		////
		Если ВидОперации=Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика Тогда
			Движение = ТаблицаЗачетаНДС.Добавить();
			Движение.Период				= ?(ЗначениеЗаполнено(ПриходныйДокументДата),ПриходныйДокументДата,Дата);
			Движение.Организация		= Организация;
			Движение.СчетДт				= СтрокаНоменклатуры.СчетУчетаБУ;
			Движение.Получатель			= Склад;
			Движение.СчетКт				= СчетУчетаРасчетовСКонтрагентом;
			Движение.Сумма				= СтрокаНоменклатуры.Сумма;
			Движение.СуммаНДС			= СтрокаНоменклатуры.СуммаНДС;
			Движение.Поставщик			= Поставщик;
			Движение.Договор			= ДоговорПоставщика;
			Движение.ПриходныйДокумент	= ПриходныйДокумент;
			Движение.ЗачетНДС			= СтрокаНоменклатуры.ЗачетНДС;
			Движение.СтавкаНДС			= СтрокаНоменклатуры.СтавкаНДС;
			Движение.ПорядокУплатыНДС	= ПорядокУплатыНДС;
		КонецЕсли; 
	КонецЦикла; 
	//
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика Тогда
		Для Каждого СтрокаРасходыВключаемыеВСтоимость Из РасходыВключаемыеВСтоимость Цикл
			СуммаКРаспределению=СтрокаРасходыВключаемыеВСтоимость.Сумма;
			Остаток=СуммаКРаспределению;
			СуммаНДСКРаспределению=СтрокаРасходыВключаемыеВСтоимость.СуммаНДС;
			ОстатокНДС=СуммаНДСКРаспределению;
			//
			ТаблицаНоменклатуры=Номенклатура.Выгрузить();
			Для Каждого СтрокаТаблицаНоменклатуры из ТаблицаНоменклатуры Цикл
				СуммаВПроводку=Окр(СуммаКРаспределению*СтрокаТаблицаНоменклатуры.Сумма/ТаблицаНоменклатуры.Итог("Сумма"),2);
				Остаток=Остаток-СуммаВПроводку;
				СуммаНДСВПроводку=Окр(СуммаНДСКРаспределению*СтрокаТаблицаНоменклатуры.Сумма/ТаблицаНоменклатуры.Итог("Сумма"),2);
				ОстатокНДС=ОстатокНДС-СуммаНДСВПроводку;
				////
				Проводка = ПроводкиБУ.Добавить();
				Проводка.Период      = Дата;
				Проводка.Организация = Организация;
				Проводка.Содержание	 = СтрокаРасходыВключаемыеВСтоимость.Описание;     
				Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокумент);     
				//
				Проводка.СчетДт = СтрокаТаблицаНоменклатуры.СчетУчетаБУ;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаТаблицаНоменклатуры.Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
				//
				Проводка.СчетКт = СтрокаРасходыВключаемыеВСтоимость.СчетКредит;
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,СтрокаРасходыВключаемыеВСтоимость.Субконто1);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,СтрокаРасходыВключаемыеВСтоимость.Субконто2);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,СтрокаРасходыВключаемыеВСтоимость.Субконто3);
				//
				Проводка.Сумма = СуммаВПроводку; 
				////
				Если СуммаНДСВПроводку <> 0 Тогда
					Проводка = ПроводкиБУ.Добавить();
					Проводка.Период      = Дата;
					Проводка.Организация = Организация;
					Проводка.Содержание	 = СтрокаРасходыВключаемыеВСтоимость.Описание;     
					Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокумент);     
					//
					Если Дата<Дата('20190101') И 
						(СтрокаТаблицаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеОсновныеСредства		ИЛИ
						 СтрокаТаблицаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеНематериальныеАктивы	ИЛИ
						 СтрокаТаблицаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Оборудование) Тогда
						Проводка.СчетДт = СтрокаТаблицаНоменклатуры.СчетУчетаБУ;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаТаблицаНоменклатуры.Номенклатура);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
					ИначеЕсли Дата<Дата('20190101') Тогда
						Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаРасходыВключаемыеВСтоимость.СтавкаНДС);
					ИначеЕсли ПорядокУплатыНДС = Перечисления.ПорядокУплатыНДС.Общеустановленный Тогда
						Если СтрокаТаблицаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачет Тогда
							Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
							УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
							УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаРасходыВключаемыеВСтоимость.СтавкаНДС);
						ИначеЕсли СтрокаТаблицаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС ИЛИ
								  СтрокаТаблицаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
							Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А4401;
							УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ПлатежиПоНДС",СтрокаТаблицаНоменклатуры.ПлатежПоНДС);
						Иначе
							Проводка.СчетДт = СтрокаТаблицаНоменклатуры.СчетУчетаБУ;
							УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаТаблицаНоменклатуры.Номенклатура);
							УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
							УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
						КонецЕсли;
					Иначе
						Проводка.СчетДт = СтрокаТаблицаНоменклатуры.СчетУчетаБУ;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаТаблицаНоменклатуры.Номенклатура);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
					КонецЕсли; 
					//
					Проводка.СчетКт = СтрокаРасходыВключаемыеВСтоимость.СчетКредит;
					УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,СтрокаРасходыВключаемыеВСтоимость.Субконто1);
					УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,СтрокаРасходыВключаемыеВСтоимость.Субконто2);
					УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,СтрокаРасходыВключаемыеВСтоимость.Субконто3);
					//
					Проводка.Сумма = СуммаНДСВПроводку; 
				КонецЕсли; 
				////
				Движение = ТаблицаЗачетаНДС.Добавить();
				Движение.Период				= ?(ЗначениеЗаполнено(СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокументДата),СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокументДата,Дата);
				Движение.Организация		= Организация;
				Движение.СчетДт				= СтрокаТаблицаНоменклатуры.СчетУчетаБУ;
				Движение.Получатель			= Склад;
				Движение.СчетКт				= СтрокаРасходыВключаемыеВСтоимость.СчетКредит;
				Движение.Сумма				= СуммаВПроводку;
				Движение.СуммаНДС			= СуммаНДСВПроводку;
				Движение.Поставщик			= СтрокаРасходыВключаемыеВСтоимость.Субконто1;
				Движение.Договор			= СтрокаРасходыВключаемыеВСтоимость.Субконто2;
				Движение.ПриходныйДокумент	= СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокумент;
				Движение.ЗачетНДС			= СтрокаТаблицаНоменклатуры.ЗачетНДС;
				Движение.СтавкаНДС			= СтрокаРасходыВключаемыеВСтоимость.СтавкаНДС;
				Движение.ПорядокУплатыНДС	= ПорядокУплатыНДС;
			КонецЦикла;
			Если Остаток<>0 Тогда
				Проводка = ПроводкиБУ.Добавить();
				Проводка.Период      = Дата;
				Проводка.Организация = Организация;
				Проводка.Содержание	 = СтрокаРасходыВключаемыеВСтоимость.Описание;     
				Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокумент);     
				//
				Проводка.СчетДт = ТаблицаНоменклатуры[0].СчетУчетаБУ;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,ТаблицаНоменклатуры[0].Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
				//
				Проводка.СчетКт = СтрокаРасходыВключаемыеВСтоимость.СчетКредит;
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,СтрокаРасходыВключаемыеВСтоимость.Субконто1);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,СтрокаРасходыВключаемыеВСтоимость.Субконто2);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,СтрокаРасходыВключаемыеВСтоимость.Субконто3);
				//
				Проводка.Сумма = Остаток; 
			КонецЕсли;
			Если ОстатокНДС<>0 Тогда
				Проводка = ПроводкиБУ.Добавить();
				Проводка.Период      = Дата;
				Проводка.Организация = Организация;
				Проводка.Содержание  = СтрокаРасходыВключаемыеВСтоимость.Описание;     
				Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокумент);     
				//
				Если Дата<Дата('20190101') И 
					(ТаблицаНоменклатуры[0].Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеОсновныеСредства		ИЛИ
					 ТаблицаНоменклатуры[0].Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеНематериальныеАктивы	ИЛИ
					 ТаблицаНоменклатуры[0].Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Оборудование) Тогда
					Проводка.СчетДт = ТаблицаНоменклатуры[0].СчетУчетаБУ;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,ТаблицаНоменклатуры[0].Номенклатура);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
				ИначеЕсли Дата<Дата('20190101') Тогда
					Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаРасходыВключаемыеВСтоимость.СтавкаНДС);
				ИначеЕсли ПорядокУплатыНДС = Перечисления.ПорядокУплатыНДС.Общеустановленный Тогда
					Если ТаблицаНоменклатуры[0].ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачет Тогда
						Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаРасходыВключаемыеВСтоимость.СтавкаНДС);
					ИначеЕсли ТаблицаНоменклатуры[0].ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС ИЛИ
							  ТаблицаНоменклатуры[0].ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
						Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А4401;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ПлатежиПоНДС",ТаблицаНоменклатуры[0].ПлатежПоНДС);
					Иначе
						Проводка.СчетДт = ТаблицаНоменклатуры[0].СчетУчетаБУ;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,ТаблицаНоменклатуры[0].Номенклатура);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
					КонецЕсли;
				Иначе
					Проводка.СчетДт = ТаблицаНоменклатуры[0].СчетУчетаБУ;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,ТаблицаНоменклатуры[0].Номенклатура);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
				КонецЕсли; 
				//
				Проводка.СчетКт = СтрокаРасходыВключаемыеВСтоимость.СчетКредит;
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,СтрокаРасходыВключаемыеВСтоимость.Субконто1);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,СтрокаРасходыВключаемыеВСтоимость.Субконто2);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,СтрокаРасходыВключаемыеВСтоимость.Субконто3);
				//
				Проводка.Сумма = ОстатокНДС; 
			КонецЕсли; 
			Если Остаток<>0 ИЛИ ОстатокНДС<>0 Тогда
				Движение = ТаблицаЗачетаНДС.Добавить();
				Движение.Период				= ?(ЗначениеЗаполнено(СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокументДата),СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокументДата,Дата);
				Движение.Организация		= Организация;
				Движение.СчетДт				= ТаблицаНоменклатуры[0].СчетУчетаБУ;
				Движение.Получатель			= Склад;
				Движение.СчетКт				= СтрокаРасходыВключаемыеВСтоимость.СчетКредит;
				Движение.Сумма				= Остаток;
				Движение.СуммаНДС			= ОстатокНДС;
				Движение.Поставщик			= СтрокаРасходыВключаемыеВСтоимость.Субконто1;
				Движение.Договор			= СтрокаРасходыВключаемыеВСтоимость.Субконто2;
				Движение.ПриходныйДокумент	= СтрокаРасходыВключаемыеВСтоимость.ПриходныйДокумент;
				Движение.ЗачетНДС			= ТаблицаНоменклатуры[0].ЗачетНДС;
				Движение.СтавкаНДС			= СтрокаРасходыВключаемыеВСтоимость.СтавкаНДС;
				Движение.ПорядокУплатыНДС	= ПорядокУплатыНДС;
			КонецЕсли;
		КонецЦикла; 
		//
		Для Каждого СтрокаПрочее Из Прочее Цикл
			Проводка = ПроводкиБУ.Добавить();
			Проводка.Период      = Дата;
			Проводка.Организация = Организация;
			Проводка.Содержание	 = СтрокаПрочее.Описание;     
			Проводка.ПервичныйДокумент = "";     
			//
			Проводка.СчетДт = СтрокаПрочее.СчетДебет;
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаПрочее.СубконтоД1);
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,СтрокаПрочее.СубконтоД2);
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,СтрокаПрочее.СубконтоД3);
			//
			Проводка.СчетКт = СтрокаПрочее.СчетКредит;
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,СтрокаПрочее.СубконтоК1);
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,СтрокаПрочее.СубконтоК2);
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,СтрокаПрочее.СубконтоК3);
			//
			Проводка.Сумма = СтрокаПрочее.Сумма; 
			////
			Если СтрокаПрочее.СуммаНДС<>0 Тогда
				Проводка = ПроводкиБУ.Добавить();
				Проводка.Период      = Дата;
				Проводка.Организация = Организация;
				Проводка.Содержание	 = СтрокаПрочее.Описание;     
				Проводка.ПервичныйДокумент = "";     
				//
				Если Дата<Дата('20190101') И (СтрокаПрочее.СчетДебет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.А0700) ИЛИ
											  СтрокаПрочее.СчетДебет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.А0800)) Тогда
					Проводка.СчетДт = СтрокаПрочее.СчетДебет;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаПрочее.СубконтоД1);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,СтрокаПрочее.СубконтоД2);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,СтрокаПрочее.СубконтоД3);
				ИначеЕсли Дата<Дата('20190101') Тогда
					Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаПрочее.СтавкаНДС);
				ИначеЕсли ПорядокУплатыНДС = Перечисления.ПорядокУплатыНДС.Общеустановленный Тогда
					Если СтрокаПрочее.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачет Тогда
						Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаПрочее.СтавкаНДС);
					ИначеЕсли СтрокаПрочее.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС ИЛИ
							  СтрокаПрочее.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
						Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А4401;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ПлатежиПоНДС",СтрокаПрочее.ПлатежПоНДС);
					ИначеЕсли СтрокаПрочее.ЗачетНДС=Перечисления.ЗачетНДС.НДСУслугамВСтоимость_ОС_НМА_НС Тогда
						Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А4402;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаПрочее.СубконтоД1);
					Иначе
						Проводка.СчетДт = СтрокаПрочее.СчетДебет;
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаПрочее.СубконтоД1);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,СтрокаПрочее.СубконтоД2);
						УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,СтрокаПрочее.СубконтоД3);
					КонецЕсли;
				Иначе
					Проводка.СчетДт = СтрокаПрочее.СчетДебет;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаПрочее.СубконтоД1);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,СтрокаПрочее.СубконтоД2);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,СтрокаПрочее.СубконтоД3);
				КонецЕсли;	
				//
				Проводка.СчетКт = СтрокаПрочее.СчетКредит;
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,СтрокаПрочее.СубконтоК1);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,СтрокаПрочее.СубконтоК2);
				УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,3,СтрокаПрочее.СубконтоК3);
				//
				Проводка.Сумма = СтрокаПрочее.СуммаНДС; 
			КонецЕсли; 
			////
			Движение = ТаблицаЗачетаНДС.Добавить();
			Движение.Период				= ?(ЗначениеЗаполнено(ПриходныйДокументДата),ПриходныйДокументДата,Дата);
			Движение.Организация		= Организация;
			Движение.СчетДт				= СтрокаПрочее.СчетДебет;
			Движение.Получатель			= СтрокаПрочее.СубконтоД1;
			Движение.СчетКт				= СтрокаПрочее.СчетКредит;
			Движение.Сумма				= СтрокаПрочее.Сумма;
			Движение.СуммаНДС			= СтрокаПрочее.СуммаНДС;
			Движение.Поставщик			= СтрокаПрочее.СубконтоК1;
			Движение.Договор			= СтрокаПрочее.СубконтоК2;
			Движение.ПриходныйДокумент	= ПриходныйДокумент;
			Движение.ЗачетНДС			= СтрокаПрочее.ЗачетНДС;
			Движение.СтавкаНДС			= СтрокаПрочее.СтавкаНДС;
			Движение.ПорядокУплатыНДС	= ПорядокУплатыНДС;
		КонецЦикла; 
	КонецЕсли; 
	ТаблицаЗачетаНДС.Свернуть("Период,Организация,СчетДт,Получатель,СчетКт,Поставщик,Договор,ПриходныйДокумент,ЗачетНДС,СтавкаНДС,ПорядокУплатыНДС","Сумма,СуммаНДС,СуммаНДСПоУслугам,СуммаНДСПоИмпорту");	
	Движения.УчетЗачетаНДС.Загрузить(ТаблицаЗачетаНДС);
	Если ЗачестьАванс Тогда
		Движения.Хозрасчетный.Записать();
		ЗачетАвансов.ВыполнитьЗакрытиеАвансов(ЭтотОбъект, СчетУчетаРасчетовСКонтрагентом, Поставщик,ДоговорПоставщика);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Префикс=Организация.Префикс;
	ДобавитьПрефиксУзла(Префикс);
КонецПроцедуры
 
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	СуммаПоДокументу = Номенклатура.Итог("Сумма")+Номенклатура.Итог("СуммаНДС");
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика Тогда
		СуммаПоДокументу = СуммаПоДокументу+РасходыВключаемыеВСтоимость.Итог("Сумма")+РасходыВключаемыеВСтоимость.Итог("СуммаНДС");
		СуммаПоДокументу = СуммаПоДокументу+Прочее.Итог("Сумма")+Прочее.Итог("СуммаНДС");
	КонецЕсли; 
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Для Каждого СтрокаТЧ Из Номенклатура Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ПлатежПоНДС) Тогда
			СтрокаТЧ.ПлатежПоНДС="";
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаТЧ Из Прочее Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ПлатежПоНДС) Тогда
			СтрокаТЧ.ПлатежПоНДС="";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	//ПроверитьЗаполнениеДокумента(Отказ, Заголовок, СтруктураШапкиДокумента);
	ЗаписатьЦеныВСправочник();
	ПрверитьЗачетНДС(Отказ);
	Если НЕ Отказ Тогда
		ЗаписатьПлатежиПоНДС();
		ДвиженияПоБУ(Режим, Отказ, Заголовок ,СтруктураШапкиДокумента);
		//
		СписокНоменклатуры=Номенклатура.Выгрузить();
		СписокНоменклатуры.Свернуть("Номенклатура","");
		СформироватьДвиженияПоРегиструДатыПоступленияОборудованияИПриобретенныхОС(ЭтотОбъект,СписокНоменклатуры);
	КонецЕсли;
КонецПроцедуры
