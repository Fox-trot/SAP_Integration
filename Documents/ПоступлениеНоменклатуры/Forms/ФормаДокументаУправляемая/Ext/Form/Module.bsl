
&НаКлиенте
Перем ВидыОпераций;


&НаКлиенте
Процедура ВидОперацииОтПоставщика(Команда)
	
	Объект.ВидОперации=ВидыОпераций.ОтПоставщика;
	ПриВыбореВидаОперации();
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореВидаОперации()
	
	ВидОперации=Объект.ВидОперации;
	
	Если ВидОперации=ВидыОпераций.ОтПоставщика Тогда
		Элементы.СтраницыПоставщики.ТекущаяСтраница=Элементы.СтраницыПоставщики.ПодчиненныеЭлементы.ОтПоставщика;
		Заголовок="От поставщика";
	ИначеЕсли ВидОперации=ВидыОпераций.Внутренний Тогда
		Элементы.СтраницыПоставщики.ТекущаяСтраница=Элементы.СтраницыПоставщики.ПодчиненныеЭлементы.Внутренний;
		Заголовок="Внутреннее";
	ИначеЕсли ВидОперации=ВидыОпераций.СПереработки Тогда
		Элементы.СтраницыПоставщики.ТекущаяСтраница=Элементы.СтраницыПоставщики.ПодчиненныеЭлементы.СПереработки;
		Заголовок="С переработки";
	КонецЕсли;		
	
КонецПроцедуры         

&НаКлиенте
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("ПриемныйАкт,РасчетСтоимостиНоменклатуры","Приходый акт","Расчет стоимости номенклатуры");
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Организация.ТолькоПросмотр = не УчетПоВсемОрганизациям;
	Элементы.Ответственный.ТолькоПросмотр = не ПроцедурыСервера.РольДоступна_Сервер("ПолныеПрава");
	
	ЗаполнитьВидыОпераций(ВидыОпераций);
	
	ПриВыбореВидаОперации();
	РассчитатьИтоги();
	УстановитьВидимость();
	
КонецПроцедуры         

&НаКлиенте
Процедура УстановитьВидимость()

	Элементы.ГруппаПрочее.Видимость=Объект.ВидОперации=ВидыОпераций.ОтПоставщика;
	Элементы.ГруппаРасходыВключаемыеВСтоимость.Видимость=Объект.ВидОперации=ВидыОпераций.ОтПоставщика;
	

КонецПроцедуры // УстановитьВидимость()()


&НаСервереБезКонтекста
Процедура ЗаполнитьВидыОпераций(ВидыОпераций)

	ВидыОпераций=Новый Структура;
	ВидыОпераций.Вставить("ОтПоставщика",Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика);
	ВидыОпераций.Вставить("Внутренний",Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.Внутренний);
	ВидыОпераций.Вставить("СПереработки",Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.СПереработки);

КонецПроцедуры // ЗаполнитьВидыОпераций()


&НаКлиенте
Процедура ВидОперацииВнутренний(Команда)
	
	Объект.ВидОперации=ВидыОпераций.Внутренний;
	ПриВыбореВидаОперации();
	УстановитьВидимость();
	
КонецПроцедуры


&НаКлиенте
Процедура ВидОперацииСПереработки(Команда)
	
	Объект.ВидОперации=ВидыОпераций.СПереработки;
	ПриВыбореВидаОперации();
	УстановитьВидимость();
	
КонецПроцедуры


&НаКлиенте
Процедура НоменклатураКоличествоПриИзменении(ТекущиеДанные=Неопределено)
	
	РассчитатьСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСумму(ТекущиеДанные=Неопределено)
	
	Если ТекущиеДанные=Неопределено Тогда
		ТекущиеДанные=Элементы.Номенклатура.ТекущиеДанные;
	КонецЕсли;

	Если ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Сумма=ТекущиеДанные.Количество*ТекущиеДанные.Цена;
	Если Строка(ТекущиеДанные.СтавкаНДС)="20%" Тогда
		ТекущиеДанные.СуммаНДС=ТекущиеДанные.Сумма*0.2;
	Иначе
		ТекущиеДанные.СуммаНДС=0;
	КонецЕсли;	
	ТекущиеДанные.Всего=ТекущиеДанные.Сумма+ТекущиеДанные.СуммаНДС;
	
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные=Элементы.Номенклатура.ТекущиеДанные;
	ТекущиеДанные.Цена=ПолучитьЦену(ТекущиеДанные.Номенклатура);
	НоменклатураКоличествоПриИзменении();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦену(Номенклатура)

	Возврат Номенклатура.Цена;

КонецФункции // ПолучитьЦенуНоменклатуры()


&НаКлиенте
Процедура ПечатьПриходныйАкт(Команда)
	
	 ТабДок=ВыполнитьПечать("ПриемныйАкт");
	 ПроцедурыКлиента.НапечататьДокумент_УП(ТабДок);
	 
КонецПроцедуры

&НаСервере
Функция ВыполнитьПечать(ИмяМакета)
	
	Документ = РеквизитФормыВЗначение("Объект");
	
	Если ИмяМакета="ПриемныйАкт" Тогда
		Возврат Документ.ПечатьПриемныйАкт();
	ИначеЕсли ИмяМакета="РасчетСтоимостиНоменклатуры" Тогда
		 Возврат Документ.ПечатьРасчетаСтоимостиНоменклатуры();
	КонецЕсли; 
	
КонецФункции

&НаКлиенте
Процедура ПечатьРасчетаСтоимости(Команда)
	
	 ТабДок=ВыполнитьПечать("РасчетСтоимостиНоменклатуры");
	 ПроцедурыКлиента.НапечататьДокумент_УП(ТабДок);
	 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ПроцедурыСервера.ЗаполнитьШапкуДокумента_Сервер(Объект, Объект.Ссылка);
		Объект.ВидОперации=Перечисления.ВидыОперацийПоступлениеНоменклатурыНаСклад.ОтПоставщика;
		
	КонецЕсли;	
	
	Для Каждого Строка Из Объект.Номенклатура Цикл
		Строка.Всего=Строка.Сумма+Строка.СуммаНДС;
	КонецЦикла;	
	Для Каждого Строка Из Объект.РасходыВключаемыеВСтоимость Цикл
		Строка.Всего=Строка.Сумма+Строка.СуммаНДС;
	КонецЦикла;	
	Для Каждого Строка Из Объект.Прочее Цикл
		Строка.Всего=Строка.Сумма+Строка.СуммаНДС;
	КонецЦикла;	
	
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	НоменклатураКоличествоПриИзменении();
	РассчитатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьИтоги()
	
	Всего=Объект.Номенклатура.Итог("Сумма")+Объект.Номенклатура.Итог("СуммаНДС");
	СуммаНДС=Объект.Номенклатура.Итог("СуммаНДС");
	
	Если Объект.ВидОперации=ВидыОпераций.ОтПоставщика Тогда
		Всего=Всего+Объект.РасходыВключаемыеВСтоимость.Итог("Сумма")+Объект.РасходыВключаемыеВСтоимость.Итог("СуммаНДС");
		Всего=Всего+Объект.Прочее.Итог("Сумма")+Объект.Прочее.Итог("СуммаНДС");              
		СуммаНДС=СуммаНДС+Объект.РасходыВключаемыеВСтоимость.Итог("СуммаНДС");
		СуммаНДС=СуммаНДС+Объект.Прочее.Итог("СуммаНДС");              
	КонецЕсли;	
	
КонецПроцедуры


&НаКлиенте
Процедура РасходыВключаемыеВСтоимостьПриИзменении(Элемент)
	
	РассчитатьСуммуПоСтроке();
	РассчитатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуПоСтроке(ТекущиеДанные=Неопределено,ИмяТабличнойЧасти="РасходыВключаемыеВСтоимость")
	
	Если ТекущиеДанные=Неопределено Тогда
		ТекущиеДанные=Элементы[ИмяТабличнойЧасти].ТекущиеДанные;
	КонецЕсли;

	Если ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Строка(ТекущиеДанные.СтавкаНДС)="20%" Тогда
		ТекущиеДанные.СуммаНДС=ТекущиеДанные.Сумма*0.2;
	Иначе
		ТекущиеДанные.СуммаНДС=0;
	КонецЕсли;	
	ТекущиеДанные.Всего=ТекущиеДанные.Сумма+ТекущиеДанные.СуммаНДС;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПриИзменении(Элемент)
	
	РассчитатьСуммуПоСтроке(,"Прочее");
	РассчитатьИтоги();
	
КонецПроцедуры

 

