
Функция ПечатьДоверенности(НазваниеМакета) экспорт

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доверенность.Номер КАК НомерДокумента,
	|	Доверенность.Дата КАК ДатаДокумента,
	|	Доверенность.Организация КАК Руководители,
	|	Доверенность.Организация,
	|	Доверенность.Сотрудник.ФизическоеЛицо,
	|	Доверенность.Сотрудник.ФизическоеЛицо.Наименование КАК ФамилияИмяОчествоДоверенного,
	|	Доверенность.Контрагент КАК Поставщик,
	|	Доверенность.НаПолучениеОт КАК ПоставщикПредставление,
	|	Доверенность.ДатаДействия КАК СрокДействия,
	|	Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
	|	Доверенность.Товары.(
	|		НомерСтроки КАК Номер,
	|		НаименованиеТовара КАК Ценнности,
	|		НаименованиеТовара КАК ЦеннностиПредставление,
	|		ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|		ЕдиницаПоКлассификатору.Представление КАК ЕдиницаИзмеренияПредставление,
	|		Количество
	|	),
	|	Доверенность.БанковскийСчет,
	|	Доверенность.Сотрудник.ФизическоеЛицо.ФИОНаЛатинице КАК ФамилияИмяОчествоДоверенногоНаЛатинице
	|ИЗ
	|	Документ.Доверенность КАК Доверенность
	|ГДЕ
	|	Доверенность.Ссылка = &ТекущийДокумент";
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСтрокТовары = Шапка.Товары.Выбрать();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2";

	Макет = ПолучитьОбщийМакет("М2");

	ДанныеОФизЛице = ДанныеФизЛица(Организация, Сотрудник.ФизическоеЛицо, Дата);
	
	ФамилияИмяОчествоДоверенного = СокрЛП(ДанныеОФизЛице.Фамилия) + " " + СокрЛП(ДанныеОФизЛице.Имя) + " " + СокрЛП(ДанныеОФизЛице.Отчество);
	Должность                    = ДолжностьСотрудника(Сотрудник,Дата).наименование;
	
	Если ЗначениеЗаполнено(Шапка.ФамилияИмяОчествоДоверенногоНаЛатинице) Тогда
		ФамилияИмяОчествоДоверенного = СокрЛП(Шапка.ФамилияИмяОчествоДоверенногоНаЛатинице);
	КонецЕсли;	

	Если НазваниеМакета = "М2" тогда
		
		ОбластьМакета = Макет.ПолучитьОбласть("Отрез");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.ФИОДоверенного = "" + ?(ПустаяСтрока(Должность), "", Должность + ", ") + (ФамилияИмяОчествоДоверенного);
		ТабДокумент.Вывести(ОбластьМакета);
		
		НазваниеФормы = "Типовая межотраслевая форма № М-2";
		
	Иначе
		НазваниеФормы = "Типовая межотраслевая форма № М-2а";
	КонецЕсли;


	СведенияОбОрганизации = СведенияОЮрФизЛице(Шапка.Организация,Шапка.БанковскийСчет ,Шапка.ДатаДокумента);

	СпособПечати=Константы.СпособПечатиДоверенности.Получить();
	
	Если не ЗначениеЗаполнено(СпособПечати) Тогда
		СпособПечати=Перечисления.СпособыПечатиДоверенностей.ПечатьНа1Стороне;
	КонецЕсли;	
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.ДатаДокумента=Формат(Дата,"ДФ='dd MMMM yyyy ""г.""'");
	ОбластьМакета.Параметры.СрокДействия=Формат(ДатаДействия,"ДФ='dd MMMM yyyy ""г.""'");
	ОбластьМакета.Параметры.НомерДокумента           = ПолучитьНомерНаПечать(ЭтотОбъект);
	//	ОбластьМакета.Параметры.НазваниеФормы            = НазваниеФормы;
	ОбластьМакета.Параметры.ОрганизацияПредставление = Организация.Наименование;
	ОбластьМакета.Параметры.РеквизитыСчета           = ОписаниеОрганизации(СведенияОбОрганизации, "ОрганизацияНомерСчета,ОрганизацияБанк,ОрганизацияМФО,");
	Если Дата>=Дата(2017,01,01) Тогда
		ОбластьМакета.Параметры.РеквизитыПотребителя     = ОписаниеОрганизации(СведенияОбОрганизации, "ОрганизацияНаименование,ОрганизацияИНН,ОрганизацияОКЭД,ОрганизацияЮридическийАдрес,Телефоны,");
	Иначе 
		ОбластьМакета.Параметры.РеквизитыПотребителя     = ОписаниеОрганизации(СведенияОбОрганизации, "ОрганизацияНаименование,ОрганизацияИНН,ОрганизацияОКОНХ,ОрганизацияЮридическийАдрес,Телефоны,");
	КонецЕсли;
	ОбластьМакета.Параметры.ПаспортСерия             = ДанныеОФизЛице.ДокументСерия;
	ОбластьМакета.Параметры.ПаспортНомер             = ДанныеОФизЛице.ДокументНомер;
	ОбластьМакета.Параметры.ПаспортВыдан             = ДанныеОФизЛице.ДокументКемВыдан;
	ОбластьМакета.Параметры.ПаспортДатаВыдачи        = ДанныеОФизЛице.ДокументДатаВыдачи;
	ОбластьМакета.Параметры.ФамилияИмяОчествоДоверенного = ФамилияИмяОчествоДоверенного;
	ОбластьМакета.Параметры.ДолжностьДоверенного     = Должность;
	ТабДокумент.Вывести(ОбластьМакета);

	Если СпособПечати=Перечисления.СпособыПечатиДоверенностей.ПечатьНа2Сторонах Тогда
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Если НазваниеМакета="М2" Тогда
			ТабДокумент.Вывести(Макет.ПолучитьОбласть("ПустойОтрез"));
		КонецЕсли;
	КонецЕсли;	
	
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Строка");

	Пока ВыборкаСтрокТовары.Следующий() Цикл

		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
		ОбластьМакета.Параметры.КоличествоПрописью = ?(ВыборкаСтрокТовары.Количество = 0,
		                                               "",
		                                               Строка(ВыборкаСтрокТовары.Количество) + " (" + 
		                                               КоличествоПрописью(ВыборкаСтрокТовары.Количество) + ")");
		ТабДокумент.Вывести(ОбластьМакета);

	КонецЦикла;

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	//Руководители = ОтветственныеЛица(Организация, Дата);	
	//ОбластьМакета.Параметры.Заполнить(Руководители);

	ОбластьМакета.Параметры.РуководительПредставление=ПолучитьФИОФизЛица(Руководитель.ФизическоеЛицо,Дата);
	ОбластьМакета.Параметры.ГлавныйБухгалтерПредставление=ПолучитьФИОФизЛица(ГлавныйБухгалтер.ФизическоеЛицо,Дата);

	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьДоверенности()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт

	Если ИмяМакета = "М2" или ИмяМакета = "М2а" Тогда
		
		ТабДокумент = ПечатьДоверенности(ИмяМакета);
		
	КонецЕсли;

	НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Доверенность", НепосредственнаяПечать);

КонецПроцедуры // Печать()

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("М2,М2а","Доверенность (М-2)","Доверенность (М-2а)");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Процедура ОбработкаЗаполнения(Основание)

КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс=Организация.Префикс;
	ДобавитьПрефиксУзла(Префикс);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);

	ПроверитьЗаполнениеДокумента(Отказ, Заголовок, СтруктураШапкиДокумента);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДокумента(Отказ, Заголовок, СтруктураШапкиДокумента)
	
	СтруктураОбязательныхПолей = Новый Структура("Организация");
	СтруктураОбязательныхПолей.Вставить("Сотрудник","Не выбран сотрудник.");
	//СтруктураОбязательныхПолей.Вставить("Контрагент","Не выбран контрагент.");
	//СтруктураОбязательныхПолей.Вставить("ДоговорКонтрагента","Не выбран договор контрагента.");
	
	ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
КонецПроцедуры

