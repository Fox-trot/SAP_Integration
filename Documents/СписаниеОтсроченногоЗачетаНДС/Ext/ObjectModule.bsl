
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.Хозрасчетный.Очистить();
	//
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ПлатежНДС,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК Сумма,
	|	ХозрасчетныйОстатки.Субконто1.КоличествоМесяцев - РАЗНОСТЬДАТ(ХозрасчетныйОстатки.Субконто1.ДатаНачала, &Дата, МЕСЯЦ) КАК КоличествоМесяцев
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А4401),
	|			,
	|			Организация = &Организация
	|				И Субконто1.КоличествоМесяцев > 0
	|				И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ) >= КОНЕЦПЕРИОДА(Субконто1.ДатаНачала, МЕСЯЦ)) КАК ХозрасчетныйОстатки";
	
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("Организация",Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СуммаСписания = ?(Выборка.КоличествоМесяцев<=0,Выборка.Сумма,Окр(Выборка.Сумма/Выборка.КоличествоМесяцев,2));
		Если СуммаСписания=0 Тогда
			Продолжить;
		КонецЕсли;
		//
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание	 = "Списание отсроченного зачета по НДС";     
		//
		Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",Выборка.ПлатежНДС.СтавкаНДС);
		//
		Проводка.СчетКт = ПланыСчетов.Хозрасчетный.А4401;
		УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,"ПлатежиПоНДС",Выборка.ПлатежНДС);
		//
		Проводка.Сумма = СуммаСписания; 
	КонецЦикла;
КонецПроцедуры
