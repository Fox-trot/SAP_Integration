
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "Т5_от_5_1_2004" Тогда
		ТабДокумент = ПечатьТ5(ИмяМакета);
		
	ИначеЕсли ИмяМакета = "Т5а_от_5_1_2004" тогда
		ТабДокумент = ПечатьТ5а(ИмяМакета);
		
	КонецЕсли;
		
	НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, СформироватьЗаголовокДокумента(ЭтотОбъект,"Кадровое перемещение "));
	
КонецПроцедуры // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураПечатныхФорм = Новый Структура;
	СтруктураПечатныхФорм.Вставить("Т5а_от_5_1_2004",	"Форма Т-5а");
	СтруктураПечатныхФорм.Вставить("Т5_от_5_1_2004",	"Форма Т-5");
	
	Возврат СтруктураПечатныхФорм;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Формирует запрос по документу
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросДляПечати(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	Запрос.УстановитьПараметр("Руководитель"  ,	Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("ДатаДокумента" ,	Дата);
	Запрос.УстановитьПараметр("ДатаДоИзменений" ,	ДатаИзменения-1);

	Если Режим = "ПоРеквизитамДокумента" Тогда

		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьРуководителя,
		|	КадровоеПеремещениеОрганизаций.Номер КАК НомерДок,
		|	КадровоеПеремещениеОрганизаций.Дата КАК ДатаДок,
		|	ВЫРАЗИТЬ(КадровоеПеремещениеОрганизаций.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК НазваниеОрганизации,
		|	КадровоеПеремещениеОрганизаций.Организация.КодПоОКПО КАК КодПоОКПО,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
		|	КадровоеПеремещениеОрганизаций.Организация.Префикс
		|ИЗ
		|	Документ.КадровыеИзменения КАК КадровоеПеремещениеОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследних
		|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|		ПО (ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо = &Руководитель)
		|			И КадровоеПеремещениеОрганизаций.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
		|ГДЕ
		|	КадровоеПеремещениеОрганизаций.Ссылка = &ДокументСсылка";
		
	ИначеЕсли Режим = "ПоШапкеДокумента" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	1 КАК НомерСтроки,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, КадровыеИзменения.Сотрудник.ФизическоеЛицо.Наименование) КАК Работник,
		|	КадровыеИзменения.ТабельныйНомер,
		|	Работники.Подразделение КАК ПрежнееПодразделение,
		|	Работники.Должность КАК ПрежняяДолжность,
		|	КадровыеИзменения.Ссылка.ДатаИзменения КАК ДатаНачала,
		|	КадровыеИзменения.Ссылка.ДатаОтменыИзменений КАК ДатаОкончания,
		|	ЕСТЬNULL(КадровыеИзменения.Должность.Наименование, """") + "" "" + ЕСТЬNULL(КадровыеИзменения.Разряд.Наименование, """") КАК НоваяДолжность,
		|	ВЫБОР
		|		КОГДА КадровыеИзменения.Оклад = 0
		|			ТОГДА КадровыеИзменения.Тариф
		|		ИНАЧЕ КадровыеИзменения.Оклад
		|	КОНЕЦ КАК ОкладТарифнаяСтавка,
		|	КадровыеИзменения.Подразделение КАК НовоеПодразделение,
		|	ВЫБОР
		|		КОГДА КадровыеИзменения.ВВалюте
		|			ТОГДА КадровыеИзменения.Валюта
		|		ИНАЧЕ ""сум.""
		|	КОНЕЦ КАК Валюта
		|ИЗ
		|	Документ.КадровыеИзменения КАК КадровыеИзменения
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СписокДат.Сотрудник КАК Сотрудник,
		|			РаботникиОрганизаций.ПодразделениеОрганизации КАК Подразделение,
		|			РаботникиОрганизаций.Должность КАК Должность
		|		ИЗ
		|			(ВЫБРАТЬ
		|				РаботникиВнутри.Сотрудник КАК Сотрудник,
		|				МАКСИМУМ(РаботникиВнутри.Период) КАК ДатаПоследнегоИзменения
		|			ИЗ
		|				РегистрСведений.РаботникиОрганизаций КАК РаботникиВнутри
		|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КадровыеИзменения КАК Док
		|					ПО РаботникиВнутри.Сотрудник = Док.Сотрудник
		|						И РаботникиВнутри.Период < Док.ДатаИзменения
		|			ГДЕ
		|				Док.Ссылка = &ДокументСсылка
		|			
		|			СГРУППИРОВАТЬ ПО
		|				РаботникиВнутри.Сотрудник) КАК СписокДат
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
		|				ПО СписокДат.ДатаПоследнегоИзменения = РаботникиОрганизаций.Период
		|					И СписокДат.Сотрудник = РаботникиОрганизаций.Сотрудник) КАК Работники
		|		ПО КадровыеИзменения.Сотрудник = Работники.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				ФизЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						КадровыеИзмененияСотрудники.Сотрудник.ФизическоеЛицо
		|					ИЗ
		|						Документ.КадровыеИзменения.Сотрудники КАК КадровыеИзмененияСотрудники
		|					ГДЕ
		|						КадровыеИзмененияСотрудники.Ссылка = &ДокументСсылка)) КАК ФИОФизЛицСрезПоследних
		|		ПО КадровыеИзменения.Сотрудник.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|ГДЕ
		|	КадровыеИзменения.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	ИначеЕсли Режим = "ПоТабличнойЧастиДокумента" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровыеИзмененияСотрудники.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, КадровыеИзмененияСотрудники.Сотрудник.ФизическоеЛицо.Наименование) КАК Работник,
		|	КадровыеИзмененияСотрудники.ТабельныйНомер,
		|	Работники.Подразделение КАК ПрежнееПодразделение,
		|	Работники.Должность КАК ПрежняяДолжность,
		|	КадровыеИзмененияСотрудники.Ссылка.ДатаИзменения КАК ДатаНачала,
		|	КадровыеИзмененияСотрудники.Ссылка.ДатаОтменыИзменений КАК ДатаОкончания,
		|	ЕСТЬNULL(КадровыеИзмененияСотрудники.Должность.Наименование, """") + "" "" + ЕСТЬNULL(КадровыеИзмененияСотрудники.Разряд.Наименование, """") КАК НоваяДолжность,
		|	ВЫБОР
		|		КОГДА КадровыеИзмененияСотрудники.Оклад = 0
		|			ТОГДА КадровыеИзмененияСотрудники.Тариф
		|		ИНАЧЕ КадровыеИзмененияСотрудники.Оклад
		|	КОНЕЦ КАК ОкладТарифнаяСтавка,
		|	КадровыеИзмененияСотрудники.Подразделение КАК НовоеПодразделение,
		|	ВЫБОР
		|		КОГДА КадровыеИзмененияСотрудники.ВВалюте
		|			ТОГДА КадровыеИзмененияСотрудники.Валюта
		|		ИНАЧЕ ""сум.""
		|	КОНЕЦ КАК Валюта
		|ИЗ
		|	Документ.КадровыеИзменения.Сотрудники КАК КадровыеИзмененияСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				ФизЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						КадровыеИзмененияСотрудники.Сотрудник.ФизическоеЛицо
		|					ИЗ
		|						Документ.КадровыеИзменения.Сотрудники КАК КадровыеИзмененияСотрудники
		|					ГДЕ
		|						КадровыеИзмененияСотрудники.Ссылка = &ДокументСсылка)) КАК ФИОФизЛицСрезПоследних
		|		ПО КадровыеИзмененияСотрудники.Сотрудник.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации КАК Подразделение,
		|			РаботникиОрганизацийСрезПоследних.Должность КАК Должность,
		|			РаботникиОрганизацийСрезПоследних.Сотрудник КАК Сотрудник
		|		ИЗ
		|			РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаДоИзменений, ) КАК РаботникиОрганизацийСрезПоследних) КАК Работники
		|		ПО КадровыеИзмененияСотрудники.Сотрудник = Работники.Сотрудник
		|ГДЕ
		|	КадровыеИзмененияСотрудники.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросДляПечати()

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой "Т-5а",
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьТ5а(ИмяМакета)

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ПолеСлева = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КадровоеПеремещениеОрганизации_Т5а";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	// получаем данные для печати
	ВыборкаДляШапки = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
	Если Списком Тогда
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();
	Иначе
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоШапкеДокумента").Выбрать();
	КонецЕсли;

	// подсчитываем количество страниц документа - для корректного разбиения на страницы
	ВсегоСтрокДокумента = ВыборкаРаботники.Количество();

	// запоминаем области макета
	Макет = ПолучитьМакет(ИмяМакета);
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
	ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати"); // повторяющаяся шапка страницы
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");// Подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаРаботник"); // строка работника

	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакета);
	
	// выводим данные о руководителях организации
	Если ВыборкаДляШапки.Следующий() Тогда
		ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки); // Шапка документа.
		ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
		ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки);// Для подвала.
	КонецЕсли;

	// Начинаем формировать выходной документ
	ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.

	ВыведеноСтрок = 0;
	// выводим строки по работникам
	Пока ВыборкаРаботники.Следующий() Цикл

		// Данные по работнику.
		ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
		//Если ЗначениеЗаполнено(ВыборкаРаботники.ВалютаТарифнойСтавки) Тогда
		//	ОбластьМакета.Параметры.ОкладТарифнаяСтавка = "" + Формат(ВыборкаРаботники.ОкладТарифнаяСтавка,"ЧЦ=15; ЧДЦ=2; ЧН=' '") + Символы.ПС + "(" + ВыборкаРаботники.ВалютаТарифнойСтавки +")";
		//КонецЕсли;
		
		//уберем из табельного номера префикс
		ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
		
		// разбиение на страницы
		ВыведеноСтрок = ВыведеноСтрок + 1;
		
		// Проверим, уместится ли строка на странице или надо открывать новую страницу
		ВывестиПодвалЛиста = Не ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
		Если Не ВывестиПодвалЛиста и ВыведеноСтрок = ВсегоСтрокДокумента Тогда
			ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
			ВывестиПодвалЛиста = Не ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
		КонецЕсли;
		Если ВывестиПодвалЛиста Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		//Если ЗначениеЗаполнено(ВыборкаРаботники.ОснованиеПеремещения) Тогда
		//	ВысотаДока = ТабДокумент.ВысотаТаблицы;
		//	ОбластьДокумента = ТабДокумент.Область(ВысотаДока,14,ВысотаДока,17);
		//	ОбластьДокумента.Объединить();
		//	ОбластьДокумента.Текст = ВыборкаРаботники.ОснованиеПеремещения;
		//КонецЕсли;

	КонецЦикла;

	// если не было ни одного работника - выводим пустой бланк
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакета);
	ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
	Для Сч = 1 По ОбластьМакета.Параметры.Количество() Цикл
		ОбластьМакета.Параметры.Установить(Сч - 1,"");
	КонецЦикла;
	ОбластьМакета.Параметры.Работник = " " + Символы.ПС + " ";
	Пока ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти, Ложь) Цикл
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	// выводим предварительно подготовленный Подвал документа.
	ТабДокумент.Вывести(ОбластьМакетаПодвал);

	Возврат ТабДокумент;

КонецФункции // ПечатьТ5а()

// Функция формирует табличный документ с печатной формой "Т-5",
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьТ5(ИмяМакета)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КадровоеПеремещениеОрганизации_Т5";
	
	// получаем данные для печати
	ВыборкаДляШапки = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
	Если Списком Тогда
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();
	Иначе
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоШапкеДокумента").Выбрать();
	КонецЕсли;

	// запоминаем области макета
	Макет = ПолучитьМакет(ИмяМакета);
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал"); // Подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Работник"); // строка работника

	// выводим данные о руководителях организации
	Если ВыборкаДляШапки.Следующий() Тогда
		ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки); // Шапка документа.
		ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
		ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки); // Для подвала.
		НомерДокДляПечати	= ВыборкаДляШапки.НомерДок;
	КонецЕсли;
	
	// Начинаем формировать выходной документ
	Пока ВыборкаРаботники.Следующий() Цикл

		// Каждый приказ на отдельной странице.
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Шапка документа.
		Если Сотрудники.Количество() > 1 Тогда
			ОбластьМакетаШапка.Параметры.НомерДок = НомерДокДляПечати + "/" + ВыборкаРаботники.НомерСтроки;
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьМакетаШапка);

		// Данные по работнику.
		ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
		//ОбластьМакета.Параметры.ТрудовойДоговорЧисло    = ?(ЗначениеЗаполнено(ВыборкаРаботники.ТрудовойДоговорДата), Формат(ВыборкаРаботники.ТрудовойДоговорДата, "ДФ=dd"), "        ");
		//ОбластьМакета.Параметры.ТрудовойДоговорМесяцГод = ?(ЗначениеЗаполнено(ВыборкаРаботники.ТрудовойДоговорДата), НРЕГ(Формат(ВыборкаРаботники.ТрудовойДоговорДата, "ДФ='MMMM yyyy ""г.""'")), "                                г.");

		//уберем из табельного номера префикс
		ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
		
		Если ЗначениеЗаполнено(ВыборкаРаботники.ДатаОкончания) Тогда
			ОбластьМакета.Параметры.ВидПеревода = "временно";
						
		Иначе
			ОбластьМакета.Параметры.ВидПеревода = "постоянно";
			
		КонецЕсли;
		
		Если ВыборкаРаботники.Валюта="сум." Тогда
			
			ОбластьМакета.Параметры.КопейкиТарифнойСтавки = "тийин.";
			ОбластьМакета.Параметры.ВалютаТарифнойСтавки = "сум.";
			
		Иначе
			
			СтрокаПараметров = СтрЗаменить(ВыборкаРаботники.Валюта.ПараметрыПрописиНаРусском, ",", Символы.ПС);

			ОбластьМакета.Параметры.КопейкиТарифнойСтавки = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 5));
			ОбластьМакета.Параметры.ВалютаТарифнойСтавки = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 1));
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаРаботники.ОкладТарифнаяСтавка) Тогда
			
			ОбластьМакета.Параметры.ОкладЦелаяЧасть = "" + Цел(ВыборкаРаботники.ОкладТарифнаяСтавка);
			ДробнаяЧасть = ВыборкаРаботники.ОкладТарифнаяСтавка - Цел(ВыборкаРаботники.ОкладТарифнаяСтавка);
			ОбластьМакета.Параметры.ОкладДробнаяЧасть = ?(ДробнаяЧасть = 0, "00", ДробнаяЧасть*100); 
		Иначе
			ОбластьМакета.Параметры.ОкладЦелаяЧасть = ""; 
			ОбластьМакета.Параметры.ОкладДробнаяЧасть = ""; 
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);

		// Подвал документа.
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
		
	КонецЦикла;
	
	// если не было ни одного работника - выводим пустой бланк
	Если ТабДокумент.ВысотаТаблицы = 0 Тогда
		ТабДокумент.Вывести(ОбластьМакетаШапка);
		ТабДокумент.Вывести(ОбластьМакета);
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
	КонецЕсли;
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьТ5()

#КонецЕсли


////////////////////////////////////////////////////////////////////////////////

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ДобавитьПрефиксУзла(Префикс);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Списком Тогда
		
		Сотрудник="";
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);
	
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	//ПроверитьЗаполнениеДокумента(Отказ, Заголовок, СтруктураШапкиДокумента);
	
	ПроверитьСотрудников(Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	
	Если Списком Тогда
		ПроверитьДубли( Отказ, Заголовок,"Сотрудник"," сотрудник ");
		ПроверитьДубли( Отказ, Заголовок,"ТабельныйНомер"," табельный № ");
	КонецЕсли; 
	
	ДвиженияПоРегистрам(Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	
КонецПроцедуры

Процедура ПроверитьДубли( Отказ, Заголовок,ИмяПоля,ОписаниеПоля) Экспорт
	
	ТаблицаДублей = Сотрудники.Выгрузить();
	
	// Подсчитываем количество повторений сотрудников.
	ТаблицаДублей.Колонки.Добавить("КоличествоПовторений");
	ТаблицаДублей.ЗаполнитьЗначения(1, "КоличествоПовторений");
	ТаблицаДублей.Свернуть(ИмяПоля, "КоличествоПовторений");
	
	// Если количество повторений > 1, выдаем сообщение об ошибке.
	Если ?(ТаблицаДублей.Количество() > 0, ТаблицаДублей.Итог("КоличествоПовторений") / ТаблицаДублей.Количество(), 0) > 1 Тогда
		
		ТекстСообщенияОбОшибке = "";
		
		// Цикл по каждому найденному повторению.
		Для Каждого СтрокаТаблицыДублей Из ТаблицаДублей Цикл
			
			Если СтрокаТаблицыДублей.КоличествоПовторений = 1 Тогда
				Продолжить; // повторений нет.
			КонецЕсли;
			
			// Добавим перевод строки, если требуется.
			ТекстСообщенияОбОшибке = ТекстСообщенияОбОшибке 
			                       + ?(ЗначениеНеЗаполнено(ТекстСообщенияОбОшибке), "", "
			                                                                            |");
			
			ТекстСообщенияОбОшибке = ТекстСообщенияОбОшибке
			                       + "В строках №№ "; 
								   
			// Выводим номера строк.
			СписокНомеровСтрок = "";
			МассивСтрок = Сотрудники.НайтиСтроки(Новый Структура(ИмяПоля, СтрокаТаблицыДублей[ИмяПоля]));
			Для Каждого Строка Из МассивСтрок Цикл
				СписокНомеровСтрок = СписокНомеровСтрок + ?(ЗначениеНеЗаполнено(СписокНомеровСтрок), "", ", ") + Строка.НомерСтроки;
			КонецЦикла;
			
			
			ТекстСообщенияОбОшибке = ТекстСообщенияОбОшибке
			                       + СписокНомеровСтрок
			                       + " табличной части указан один и тот же "+ОписаниеПоля+".";
								   
		КонецЦикла;
														  
		ОшибкаПриПроведении(ТекстСообщенияОбОшибке, Отказ, Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПроверитьЗаполнениеДокумента(Отказ, Заголовок, СтруктураШапкиДокумента)
	
	Если не Списком Тогда
		СтруктураОбязательныхПолей = Новый Структура();
		СтруктураОбязательныхПолей.Вставить("Организация");
		СтруктураОбязательныхПолей.Вставить("Сотрудник","Не выбран сотрудник.");
		СтруктураОбязательныхПолей.Вставить("ДатаИзменения","Не заполнена дата изменения.");
		
		СтруктураОбязательныхПолей.Вставить("ТабельныйНомер","Не заполнен таб. №.");
		СтруктураОбязательныхПолей.Вставить("ТипСотрудника","Не выбран тип сотрудника.");
		СтруктураОбязательныхПолей.Вставить("Подразделение","Не выбрано подразделение.");
		СтруктураОбязательныхПолей.Вставить("Должность","Не выбрана должность.");
		СтруктураОбязательныхПолей.Вставить("Категория","Не выбрана категория.");
		СтруктураОбязательныхПолей.Вставить("СпособРасчетаПН","Не выбран способ расчета подоходного налога.");
		СтруктураОбязательныхПолей.Вставить("ГрафикРаботы","Не выбран график работы.");
		СтруктураОбязательныхПолей.Вставить("ФормаТруда","Не выбрана форма труда.");
		СтруктураОбязательныхПолей.Вставить("Ставка","Не заполнена ставка.");
		
		СтруктураОбязательныхПолей.Вставить("СпособОтраженияРасходовБУ","Не указан способ отражения расходов (БУ).");
		
		ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	Иначе
		СтруктураОбязательныхПолей = Новый Структура();
		СтруктураОбязательныхПолей.Вставить("Организация");
		СтруктураОбязательныхПолей.Вставить("ДатаИзменения","Не заполнена дата изменения.");
		ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
		
		СтруктураОбязательныхПолей = Новый Структура();
		СтруктураОбязательныхПолей.Вставить("Сотрудник","Не выбран сотрудник.");
		
		СтруктураОбязательныхПолей.Вставить("ТабельныйНомер","Не заполнен таб. №.");
		СтруктураОбязательныхПолей.Вставить("ТипСотрудника","Не выбран тип сотрудника.");
		СтруктураОбязательныхПолей.Вставить("Подразделение","Не выбрано подразделение.");
		СтруктураОбязательныхПолей.Вставить("Должность","Не выбрана должность.");
		СтруктураОбязательныхПолей.Вставить("Категория","Не выбрана категория.");
		СтруктураОбязательныхПолей.Вставить("СпособРасчетаПН","Не выбран способ расчета подоходного налога.");
		СтруктураОбязательныхПолей.Вставить("ГрафикРаботы","Не выбран график работы.");
		СтруктураОбязательныхПолей.Вставить("ФормаТруда","Не выбрана форма труда.");
		СтруктураОбязательныхПолей.Вставить("Ставка","Не заполнена ставка.");
		
		СтруктураОбязательныхПолей.Вставить("СпособОтраженияРасходовБУ","Не указан способ отражения расходов (БУ).");
		
		ПроверитьЗаполнениеТабличнойЧастиПлатежногоДокумента(ЭтотОбъект, "Сотрудники", СтруктураОбязательныхПолей, Отказ, Заголовок);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДвиженияПоРегистрам(Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	
	Если не Списком Тогда
		СформироватьДвиженияПоСотруднику(СтруктураШапкиДокумента,Режим, Отказ, Заголовок, ДатаИзменения);
		
		если ДатаОтменыИзменений<>дата(1,1,1) тогда
			СведенияОСотруднике=СведенияОСотруднике(СтруктураШапкиДокумента.Сотрудник,Организация,ДатаИзменения-1);
			СформироватьДвиженияПоСотруднику(СведенияОСотруднике,Режим, Отказ, Заголовок, ДатаОтменыИзменений);
		конецесли;	
				
	Иначе
		Для Каждого СтрокаСотрудники из Сотрудники Цикл
			СформироватьДвиженияПоСотруднику(СтрокаСотрудники,Режим, Отказ, Заголовок, ДатаИзменения);
			если ДатаОтменыИзменений<>дата(1,1,1) тогда
				СведенияОСотруднике=СведенияОСотруднике(СтрокаСотрудники.Сотрудник,Организация,ДатаИзменения-1);
				СформироватьДвиженияПоСотруднику(СведенияОСотруднике,Режим, Отказ, Заголовок, ДатаОтменыИзменений);
			конецесли;	
		КонецЦикла; 
	КонецЕсли; 		
	
КонецПроцедуры 

Процедура СформироватьДвиженияПоСотруднику(Реквизиты,Режим, Отказ, Заголовок, ДатаИзменения);
	
	//=======================================
	Движение=Движения.РаботникиОрганизаций.Добавить();		
	Движение.Организация=Организация;
	Движение.Сотрудник=Реквизиты.Сотрудник;
	Движение.Период=ДатаИзменения;
	
	Движение.ТабельныйНомер=Реквизиты.ТабельныйНомер;
	Движение.ПодразделениеОрганизации=Реквизиты.Подразделение;
	Движение.Должность=Реквизиты.Должность;
	Движение.Категория=Реквизиты.Категория;
	Движение.ТипСотрудника=Реквизиты.ТипСотрудника;
	Движение.НеРезидент=Реквизиты.НеРезидент;
	Движение.Надомник=Реквизиты.Надомник;
	Движение.НеУдерживатьПрофвзносы=Реквизиты.НеУдерживатьПрофвзносы;
	Движение.НеУдерживатьВПенсионныйФонд = Реквизиты.НеУдерживатьВПенсионныйФонд;
	
	Движение.ПричинаИзмененияСостояния=Перечисления.ПричиныИзмененияСостояния.Перемещение;
	//=======================================
	Движение=Движения.ДанныеДляРасчетаЗП.Добавить();		
	Движение.Организация=Организация;
	Движение.Сотрудник=Реквизиты.Сотрудник;
	Движение.Период=ДатаИзменения;
	
	Движение.Оклад=Реквизиты.Оклад;
	Движение.Тариф=Реквизиты.Тариф;
	Движение.ФормаТруда=Реквизиты.ФормаТруда;
	Движение.Ставка=Реквизиты.Ставка;
	Движение.Разряд=Реквизиты.Разряд;
	Движение.ГрафикРаботы=Реквизиты.ГрафикРаботы;
	Движение.ВВалюте=Реквизиты.ВВалюте;
	Если Движение.ВВалюте Тогда 
		Движение.Валюта=Реквизиты.Валюта;
	КонецЕсли;
	//=======================================
	Движение=Движения.ДанныеДляРасчетаПН.Добавить();		
	Движение.Организация=Организация;
	Движение.Сотрудник=Реквизиты.Сотрудник;
	Движение.Период=ДатаИзменения;
	
	Движение.СпособРасчетаПН=Реквизиты.СпособРасчетаПН;
	//=======================================
	Движение=Движения.СпособыОтраженияРасходовПоЗПБУ.Добавить();		
	Движение.Организация=Организация;
	Движение.Сотрудник=Реквизиты.Сотрудник;
	Движение.Период=ДатаИзменения;
	
	Движение.СпособОтраженияРасходов=Реквизиты.СпособОтраженияРасходовБУ;
	//=======================================
	Движение=Движения.ПроцентВыплатыНаПластиковуюКарту.Добавить();
	Движение.Организация=Организация;
	Движение.Период=ДатаИзменения;
	Движение.Сотрудник=Реквизиты.Сотрудник;
	Движение.Процент=Реквизиты.ПроцентВыплатыНаПластиковуюКарту;
	
КонецПроцедуры

Процедура ПроверитьСотрудников(Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	
	МассивСотрудников=новый Массив;
	Если не Списком Тогда
		МассивСотрудников.Добавить(СтруктураШапкиДокумента.Сотрудник);
	Иначе	
		МассивСотрудников=Сотрудники.ВыгрузитьКолонку("Сотрудник");
	КонецЕсли;
	
	Запрос=новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ 
	|	РаботникиОрганизаций.Регистратор КАК Документ,
	|	РаботникиОрганизаций.Период КАК ДатаПриема,
	|	РаботникиОрганизаций.Сотрудник
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &Организация
	|	И РаботникиОрганизаций.Сотрудник В(&МассивСотрудников)
	|	И РаботникиОрганизаций.ПричинаИзмененияСостояния = &ПриемНаРаботу";
	Запрос.УстановитьПараметр("ПриемНаРаботу", 	Перечисления.ПричиныИзмененияСостояния.ПриемНаРаботу);
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	Запрос.УстановитьПараметр("Организация",    Организация);
	
	ВыборкаДоков=Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаМассива из МассивСотрудников Цикл
		СтрокаВыборки=ВыборкаДоков.Найти(СтрокаМассива,"Сотрудник");	
		Если СтрокаВыборки=неопределено Тогда
			Сообщить("На сотрудника "+СтрокаМассива+" ещё не оформлялся приказ о приеме!");
		    Отказ=истина;
		ИначеЕсли СтрокаВыборки.ДатаПриема>ДатаИзменения Тогда
			Сообщить("На сотрудника "+СтрокаМассива+" изменения можно вводить только после даты приема, приказа о приеме: "+СтрокаВыборки.Документ+", дата приема: "+строка(СтрокаВыборки.ДатаПриема));
		    Отказ=истина;
		КонецЕсли; 
	КонецЦикла; 
	
	Запрос=новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ 
	|	РаботникиОрганизаций.Регистратор КАК Документ,
	|	РаботникиОрганизаций.Период КАК ДатаУвольнения,
	|	РаботникиОрганизаций.Сотрудник
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &Организация
	|	И РаботникиОрганизаций.Сотрудник В(&МассивСотрудников)
	|	И РаботникиОрганизаций.ПричинаИзмененияСостояния = &Увольнение";
	Запрос.УстановитьПараметр("Увольнение", 	Перечисления.ПричиныИзмененияСостояния.Увольнение);
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	Запрос.УстановитьПараметр("Организация",    Организация);
	
	ВыборкаДоков=Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаМассива из МассивСотрудников Цикл
		СтрокаВыборки=ВыборкаДоков.Найти(СтрокаМассива,"Сотрудник");	
		Если СтрокаВыборки<>неопределено Тогда
			//Если СтрокаВыборки.ДатаУвольнения<ДатаИзменения Тогда
				Сообщить("На сотрудника "+СтрокаМассива+" уже введен приказа об увольнении: "+СтрокаВыборки.Документ+", дата увольнения: "+строка(СтрокаВыборки.ДатаУвольнения));
			    Отказ=истина;
			//КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	
	//Если не Списком Тогда
	//	ПроверитьСотрудника(СтруктураШапкиДокумента,Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	//Иначе
	//	Для Каждого СтрокаСотрудники из Сотрудники Цикл
	//		ПроверитьСотрудника(СтрокаСотрудники,Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	//	КонецЦикла; 
	//КонецЕсли; 		
	
КонецПроцедуры 

Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = Основание.Ссылка;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
КонецПроцедуры


