
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ДобавитьПрефиксУзла(Префикс);
	
КонецПроцедуры

Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("Печать", "Печать");
	
КонецФункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);
	
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	ДвиженияПоРегистрам(Режим, Отказ, Заголовок,СтруктураШапкиДокумента);
	
КонецПроцедуры

Функция ПечатьДокумента(ИмяМакета)
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет(ИмяМакета);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтог = Макет.ПолучитьОбласть("Итог");
	
	ОбластьЗаголовок.Параметры.Заполнить(Ссылка);
	ОбластьЗаголовок.Параметры.Номер = ПолучитьНомерНаПечать(ЭтотОбъект);
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеРасчетыСотрудники.Сотрудник,
	               |	ДополнительныеРасчетыСотрудники.Параметр КАК Сумма,
	               |	ДополнительныеРасчетыСотрудники.Сотрудник.Код,
	               |	ДополнительныеРасчетыСотрудники.Сотрудник.Наименование
	               |ИЗ
	               |	Документ.ДополнительныеРасчеты.Сотрудники КАК ДополнительныеРасчетыСотрудники
	               |ГДЕ
	               |	ДополнительныеРасчетыСотрудники.Ссылка = &Ссылка
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	ОБЩИЕ";
				   
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
				   
	Результат = Запрос.Выполнить();
	ВыборкаИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтог.Следующий();
	
	нпп = 0;
	Выборка = ВыборкаИтог.Выбрать();
	Пока Выборка.Следующий() Цикл   
		нпп = нпп + 1;
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ОбластьСтрока.Параметры.нпп = нпп;
		ОбластьСтрока.Параметры.СотрудникКод = СокрЛП(Выборка.СотрудникКод);
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;               	 
	
	ОбластьИтог.Параметры.Заполнить(ВыборкаИтог);
	ТабДок.Вывести(ОбластьИтог);
	
	ТабДок.ПовторятьПриПечатиСтроки = ТабДок.Область(1,,6);
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ТабДок.НижнийКолонтитул.Выводить = Истина;
	ТабДок.НижнийКолонтитул.НачальнаяСтраница = 1;
	ТабДок.НижнийКолонтитул.ТекстВЦентре = "Лист №[&НомерСтраницы]";
	
	Возврат ТабДок;
КонецФункции

Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	
	ТабДокумент = ПечатьДокумента(ИмяМакета);
		
	НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "");
	
КонецПроцедуры // Печать()

Процедура ДвиженияПоРегистрам(Режим, Отказ, Заголовок,СтруктураШапкиДокумента)
	
	Для каждого Строка Из Сотрудники Цикл
		
		Движение=Движения.ДополнительныеРасчеты.Добавить();
		Движение.Период=Дата;
		Движение.Организация=Организация;
		Движение.Сотрудник=Строка.Сотрудник;
		Движение.ВидРасчета=ВидРасчета;
		Движение.СпособРасчета=СпособРасчета;
		Движение.Параметр=Строка.Параметр;
	
	КонецЦикла; 
	
КонецПроцедуры	
