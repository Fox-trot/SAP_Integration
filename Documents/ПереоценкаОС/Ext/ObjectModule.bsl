
Процедура ДвиженияПоРегистрам(Выборка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ГруппаОС,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство
	|ПОМЕСТИТЬ ГруппыОС
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(&ДатаСреза, ) КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереоценкаОСТаблицаОС.ОС,
	|	ПереоценкаОСТаблицаОС.МОЛБУ,
	|	ПереоценкаОСТаблицаОС.ПодразделениеБУ,
	|	ПереоценкаОСТаблицаОС.ИзменениеПервоначальнаяСтоимости,
	|	ПереоценкаОСТаблицаОС.ИзменениеИзноса,
	|	ГруппыОС.ГруппаОС,
	|	ПереоценкаОСТаблицаОС.СчетУчета,
	|	ПереоценкаОСТаблицаОС.СчетИзноса
	|ИЗ
	|	Документ.ПереоценкаОС.ТаблицаОС КАК ПереоценкаОСТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГруппыОС КАК ГруппыОС
	|		ПО ПереоценкаОСТаблицаОС.ОС = ГруппыОС.ОсновноеСредство
	|ГДЕ
	|	ПереоценкаОСТаблицаОС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ГруппыОС";

	Запрос.УстановитьПараметр("ДатаСреза", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Результат = Запрос.Выполнить();

	СтрокаТаблицыОС = Результат.Выбрать();

	Пока СтрокаТаблицыОС.Следующий() Цикл
		
		Если СтрокаТаблицыОС.ИзменениеПервоначальнаяСтоимости <> 0 Тогда
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.Период = Дата;
			Движение.Организация = Организация;
			
			Движение.СчетДт = СтрокаТаблицыОС.СчетУчета;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства]=СтрокаТаблицыОС.ОС;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.МОЛ]=СтрокаТаблицыОС.МОЛБУ;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Подразделения]=СтрокаТаблицыОС.ПодразделениеБУ;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ГруппыОС]=СтрокаТаблицыОС.ГруппаОС;
			
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("8510");
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства]=СтрокаТаблицыОС.ОС;
			
			Движение.Сумма	= СтрокаТаблицыОС.ИзменениеПервоначальнаяСтоимости;
			
			Движение.Содержание = "Переоценка первоначальной стоимости";
		КонецЕсли;
		
		Если СтрокаТаблицыОС.ИзменениеИзноса <> 0 Тогда
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("8510");
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства]=СтрокаТаблицыОС.ОС;
			
			Движение.СчетКт = СтрокаТаблицыОС.СчетИзноса;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства]=СтрокаТаблицыОС.ОС;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.МОЛ]=СтрокаТаблицыОС.МОЛБУ;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Подразделения]=СтрокаТаблицыОС.ПодразделениеБУ;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ГруппыОС]=СтрокаТаблицыОС.ГруппаОС;
			
			Движение.Сумма	= СтрокаТаблицыОС.ИзменениеИзноса;
			
			Движение.Содержание = "Переоценка стоимости износа ";
		КонецЕсли;
		
	КонецЦикла;
	Движения.Хозрасчетный.Записать();
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ)
	
	// Проверка существования проведенного документа "ПереоценкаОС" в текущем году
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПереоценкаОС.Ссылка
	|ИЗ
	|	Документ.ПереоценкаОС КАК ПереоценкаОС
	|ГДЕ
	|	ПереоценкаОС.Дата МЕЖДУ &НачалоГода И &КонецГода
	|	И ПереоценкаОС.Проведен
	|	И ПереоценкаОС.Ссылка <> &Ссылка
	|	И ПереоценкаОС.Организация = &Организация";
	
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Дата));
	Запрос.УстановитьПараметр("КонецГода", КонецГода(Дата));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	//Если Выборка.Количество() <> 0 Тогда
	//	Предупреждение("Уже существует проведенный документ ""Переоценка Основных средств в текущем году!" + Символы.ПС + "Документ не будет проведен"); 
	//	Отказ = Истина;	
	//КонецЕсли;
	
	ДвиженияПоРегистрам(Выборка);
	
	
	
КонецПроцедуры // ОбработкаПроведения

Процедура Печать() Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = Документы.ПереоценкаОС.ПолучитьМакет("Печать");
	
	// Заголовок
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.Организация=Организация;
	Область.Параметры.Номер=ПолучитьНомерНаПечать(ЭтотОбъект);
	Область.Параметры.Дата=Формат(Дата,"ДФ=dd.MM.yyyy");
	Область.Параметры.период=Формат(ПериодПереоценки,"ДФ='гггг"" г"".'");
	ТабДок.Вывести(Область);
	
	
	// ТаблицаОС
	Область = Макет.ПолучитьОбласть("ТаблицаОСШапка");
	ТабДок.Вывести(Область);
	ОбластьТаблицаОС = Макет.ПолучитьОбласть("ТаблицаОС");
	ОбластьОбщийИтог = Макет.ПолучитьОбласть("ОбщийИтог");
	ОбластьИтогПоГруппировке = Макет.ПолучитьОбласть("ИтогПоГруппировке");
	
	Результат = ПолучитьВыборкуПоТабличнойЧасти();
	
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	ВыборкаОбщие=Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщие.Следующий();
	
	ВыборкаГруппа=ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаГруппа.Следующий() Цикл
		ОбластьИтогПоГруппировке.Параметры.Заполнить(ВыборкаГруппа);
		ТабДок.Вывести(ОбластьИтогПоГруппировке,ВыборкаГруппа.Уровень());
		
		ВыборкаОС=ВыборкаГруппа.Выбрать();
		Пока ВыборкаОС.Следующий() Цикл
			ОбластьТаблицаОС.Параметры.Заполнить(ВыборкаОС);
			ТабДок.Вывести(ОбластьТаблицаОС,ВыборкаОС.Уровень());
		КонецЦикла;
		
	КонецЦикла;
	
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
	ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщие);
	ТабДок.Вывести(ОбластьОбщийИтог);
	
	// Подвал
	Подвал = Макет.ПолучитьОбласть("Подвал");
	Подвал.Параметры.Заполнить(ЭтотОбъект);
	ТабДок.Вывести(Подвал);
	
	ТабДок.ФиксацияСверху = 5;
	ТабДок.ФиксацияСлева = 3;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = ложь;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.АвтоМасштаб  = Истина;
	ТабДок.ПоказатьУровеньГруппировокСтрок(1);
	
	ТабДок.ПовторятьПриПечатиСтроки=ТабДок.Область(5,,5);
	
	ТабДок.Показать();
	
КонецПроцедуры

Функция ПолучитьВыборкуПоТабличнойЧасти()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПереоценкаОСТаблицаОС.НомерСтроки,
	|	ПереоценкаОСТаблицаОС.ОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПереоценкаОСТаблицаОС.ОС,
	|	ПереоценкаОСТаблицаОС.СчетУчета КАК СчетУчета,
	|	ПереоценкаОСТаблицаОС.СчетИзноса,
	|	ПереоценкаОСТаблицаОС.ПервоначальнаяСтоимостьДоПереоценки КАК ПервоначальнаяСтоимостьДоПереоценки,
	|	ПереоценкаОСТаблицаОС.НакопленныйИзносДоПереоценки КАК НакопленныйИзносДоПереоценки,
	|	ПереоценкаОСТаблицаОС.ОстаточнаяСтоимостьДоПереоценки КАК ОстаточнаяСтоимостьДоПереоценки,
	|	ПереоценкаОСТаблицаОС.ДатаВводаВЭксплуатацию,
	|	ПереоценкаОСТаблицаОС.КоэффициентПереоценки,
	|	ПереоценкаОСТаблицаОС.ПервоначальнаяСтоимостьПослеПереоценки КАК ПервоначальнаяСтоимостьПослеПереоценки,
	|	ПереоценкаОСТаблицаОС.НакопленныйИзносПослеПереоценки КАК НакопленныйИзносПослеПереоценки,
	|	ПереоценкаОСТаблицаОС.ОстаточнаяСтоимостьПослеПереоценки КАК ОстаточнаяСтоимостьПослеПереоценки,
	|	ПереоценкаОСТаблицаОС.ИзменениеПервоначальнаяСтоимости КАК ИзменениеПервоначальнаяСтоимости,
	|	ПереоценкаОСТаблицаОС.ИзменениеИзноса КАК ИзменениеИзноса,
	|	ПереоценкаОСТаблицаОС.ИзменениеОстаточнойСтоимости КАК ИзменениеОстаточнойСтоимости,
	|	ПереоценкаОСТаблицаОС.ГруппаОС КАК ГруппаОС,
	|	ПереоценкаОСТаблицаОС.ГруппаОС.Порядок КАК ГруппаОСПорядок
	|ИЗ
	|	Документ.ПереоценкаОС.ТаблицаОС КАК ПереоценкаОСТаблицаОС
	|ГДЕ
	|	ПереоценкаОСТаблицаОС.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппаОСПорядок,
	|	ПереоценкаОСТаблицаОС.ОС.ИнвентарныйНомер
	|ИТОГИ
	|	СУММА(ПервоначальнаяСтоимостьДоПереоценки),
	|	СУММА(НакопленныйИзносДоПереоценки),
	|	СУММА(ОстаточнаяСтоимостьДоПереоценки),
	|	СУММА(ПервоначальнаяСтоимостьПослеПереоценки),
	|	СУММА(НакопленныйИзносПослеПереоценки),
	|	СУММА(ОстаточнаяСтоимостьПослеПереоценки),
	|	СУММА(ИзменениеПервоначальнаяСтоимости),
	|	СУММА(ИзменениеИзноса),
	|	СУММА(ИзменениеОстаточнойСтоимости)
	|ПО
	|	ОБЩИЕ,
	|	ГруппаОС";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции


Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ДобавитьПрефиксУзла(Префикс);
	
КонецПроцедуры
