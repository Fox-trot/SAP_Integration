
Перем ТаблицаЗачетаНДС;
Перем ПорядокУплатыНДС;

////////////////////////////////////////////////////////////////////////////////

Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	Возврат Новый Структура("ПриемныйАкт","Приемный-Акт");
КонецФункции

Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт
	Если ИмяМакета = "ПриемныйАкт" Тогда
		ТабДокумент = ПечатьАкт();
	КонецЕсли;
	НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, СформироватьЗаголовокДокумента(Ссылка), НепосредственнаяПечать);
КонецПроцедуры

Функция ПолучитьЗапросФил()
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Номенклатура,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Количество КАК Количество,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Количество <> 0
	|			ТОГДА ВЫРАЗИТЬ(ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Сумма / ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Количество КАК ЧИСЛО(18, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Цена,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Номенклатура.Код,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.НомерСтроки КАК НомерСтроки,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.НомерКарты
	|ИЗ
	|	Документ.ПриходНоменклатурыПриобретённойСПластиковыхКарт.Материалы КАК ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы
	|ГДЕ
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартМатериалы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Номенклатура,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Количество,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Сумма,
	|	ВЫБОР
	|		КОГДА ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Количество <> 0
	|			ТОГДА ВЫРАЗИТЬ(ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Сумма / ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Количество КАК ЧИСЛО(18, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Номенклатура.Код,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.НомерСтроки,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Номенклатура.БазоваяЕдиницаИзмерения,
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.НомерКарты
	|ИЗ
	|	Документ.ПриходНоменклатурыПриобретённойСПластиковыхКарт.Номенклатура КАК ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура
	|ГДЕ
	|	ПриходНоменклатурыПриобретённойСПластиковыхКартНоменклатура.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПечатьАкт()
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("ПриемныйАкт");
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	Область.Параметры.Отправитель = Сотрудник.Наименование;
	Область.Параметры.должность	= ДолжностьСотрудника(Сотрудник,Дата);
	Область.Параметры.Сдал = ФИОСотрудника(Сотрудник,Дата);	
	Область.Параметры.принял	= ФИОСотрудника(Склад.МОЛ,Дата);
	ТабДок.Вывести(Область);
	
	Результат=ПолучитьЗапросФил();
	
	ВыборкаОбщие=Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщие.Следующий();
	
	
	Область = Макет.ПолучитьОбласть("Строка");
	
	
	ВыборкаДетали=ВыборкаОбщие.Выбрать();
	
	
	Пока ВыборкаДетали.Следующий() Цикл
		Область.Параметры.Заполнить(ВыборкаДетали);
		ТабДок.Вывести(Область);
	КонецЦикла; 
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.Заполнить(ВыборкаОбщие);
	Если  ВыборкаОбщие.количество()<> 0 Тогда 
		Область.Параметры.СуммаПрописью =  СформироватьСуммуПрописью(ВыборкаОбщие.сумма, Константы.ВалютаРегламентированногоУчета.Получить());
	КонецЕсли;
	Область.Параметры.Сдал = ФИОСотрудника(Сотрудник,Дата);	
	Область.Параметры.принял	= ФИОСотрудника(Склад.МОЛ,Дата);
	
	ТабДок.Вывести(Область);
	
	ТабДок.ФиксацияСверху=0;
	ТабДок.ФиксацияСлева=0;
	ТабДок.ЭкземпляровНаСтранице=0;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	
	Возврат ТабДок;
	
КонецФункции // ПечатьМ11()

////////////////////////////////////////////////////////////////////////////////

Функция СформироватьОписаниеПервичногоДокумента(ПервичныйДокумент)
	
	ПервичныйДокументСтр=сокрлп(ПервичныйДокумент);	
	Если лев(ПервичныйДокументСтр,1)="№" или ЭтоЦифра(лев(ПервичныйДокументСтр,1)) Тогда
		Возврат "Сч.фак. "+ПервичныйДокументСтр;	
	Иначе
		Возврат ПервичныйДокументСтр;
	КонецЕсли; 
	
КонецФункции // ()

Процедура ПрверитьЗачетНДС(Отказ)
	Для каждого СтрокаТЧ Из Материалы Цикл
		Если СтрокаТЧ.СуммаНДС<>0 И НЕ ЗначениеЗаполнено(СтрокаТЧ.ЗачетНДС) Тогда
			Отказ=Истина;
			Сообщить("В таблице МАТЕРИАЛЫ в строке № "+Строка(СтрокаТЧ.НомерСтроки)+" не заполнен зачет НДС");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаписатьПлатежиПоНДС()
	Для Каждого СтрокаТЧ Из Материалы Цикл
		Если СтрокаТЧ.СуммаНДС=0 Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ СтрокаТЧ.Счет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.А0800) Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаТЧ.ЗачетНДС<>Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС И
			 СтрокаТЧ.ЗачетНДС<>Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
			Продолжить;
		КонецЕсли; 
		//
		Если ЗначениеЗаполнено(СтрокаТЧ.ПлатежПоНДС) Тогда
			ОбъектПлатеж = СтрокаТЧ.ПлатежПоНДС.ПолучитьОбъект();
		Иначе
			ПлатежПоНДС = ПолучитьПлатежПоНДС(Ссылка,СтрокаТЧ.Номенклатура);
			Если ЗначениеЗаполнено(ПлатежПоНДС) Тогда
				ОбъектПлатеж = ПлатежПоНДС.ПолучитьОбъект();
			Иначе
				ОбъектПлатеж = Справочники.ПлатежиПоНДС.СоздатьЭлемент();
				ОбъектПлатеж.Наименование = СтрокаТЧ.Номенклатура.Наименование;
				ОбъектПлатеж.Номенклатура = СтрокаТЧ.Номенклатура;
				ОбъектПлатеж.ДокументПоступления = Ссылка;
				ОбъектПлатеж.УстановитьНовыйКод();
			КонецЕсли;
		КонецЕсли;
		ОбъектПлатеж.ДатаНачала = Дата;
		ОбъектПлатеж.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		Если СтрокаТЧ.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
			ОбъектПлатеж.КоличествоМесяцев = 36;
		ИначеЕсли СтрокаТЧ.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС Тогда
			ОбъектПлатеж.КоличествоМесяцев = 12;
		КонецЕсли;
		ОбъектПлатеж.ОбменДанными.Загрузка=Истина;
		ОбъектПлатеж.Записать();
		СтрокаТЧ.ПлатежПоНДС=ОбъектПлатеж.Ссылка;
	КонецЦикла;
	Записать(РежимЗаписиДокумента.Запись);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	ДобавитьПрефиксУзла(Префикс);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	СуммаПоДокументу = Материалы.Итог("Сумма");
	КоличествоПоДокументу = Материалы.Итог("Количество");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	ПрверитьЗачетНДС(Отказ);
	Если НЕ Отказ Тогда
		ЗаписатьПлатежиПоНДС();
		ДвиженияПоРегистрамБУ(РежимПроведения, Отказ, Заголовок, СтруктураШапкиДокумента);
	КонецЕсли;
КонецПроцедуры

Процедура ДвиженияПоРегистрамБУ(Режим, Отказ, Заголовок, СтруктураШапкиДокумента)
	ПорядокУплатыНДС = ПолучитьПорядокУплатыНДС(Дата,Организация);
	ТаблицаЗачетаНДС = СоздатьТаблицуДляУчетаНДС();
	ПроводкиБУ = Движения.Хозрасчетный;
	Для Каждого СтрокаНоменклатуры Из Материалы Цикл
		Проводка = ПроводкиБУ.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание	 = "";     
		//
		Проводка.СчетДт = СтрокаНоменклатуры.Счет;
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
		Проводка.КоличествоДт = СтрокаНоменклатуры.Количество;
		//
		Проводка.СчетКт = СчетУчета;
		УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Сотрудник);
		Если СчетКредит.Количественный Тогда
			Проводка.КоличествоКт = СтрокаНоменклатуры.Количество;
		КонецЕсли; 
		//
		Проводка.Сумма = СтрокаНоменклатуры.Сумма;
		////
		Если СтрокаНоменклатуры.СуммаНДС<>0 Тогда
			Проводка = ПроводкиБУ.Добавить();
			Проводка.Период      = Дата;
			Проводка.Организация = Организация;
			Проводка.Содержание	 = "Поступление НДС";     
			Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(ПриходныйДокумент);     
			//
			Если Дата<Дата('20190101') И 
				(СтрокаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеОсновныеСредства		ИЛИ
				 СтрокаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПриобретенныеНематериальныеАктивы	ИЛИ
				 СтрокаНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Оборудование) Тогда
				Проводка.СчетДт = СтрокаНоменклатуры.Счет;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
			ИначеЕсли Дата<Дата('20190101') Тогда
				Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаНоменклатуры.СтавкаНДС);
			ИначеЕсли ПорядокУплатыНДС = Перечисления.ПорядокУплатыНДС.Общеустановленный Тогда
				Если СтрокаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачет Тогда
					Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаНоменклатуры.СтавкаНДС);
				ИначеЕсли СтрокаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСПо_ОС_НМА_НС ИЛИ
						  СтрокаНоменклатуры.ЗачетНДС=Перечисления.ЗачетНДС.НДСВЗачетИмущество Тогда
					Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А4401;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"ПлатежиПоНДС",СтрокаНоменклатуры.ПлатежПоНДС);
					Проводка.КоличествоДт = СтрокаНоменклатуры.Количество;
				Иначе
					Проводка.СчетДт = СтрокаНоменклатуры.Счет;
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
					УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
				КонецЕсли;
			Иначе
				Проводка.СчетДт = СтрокаНоменклатуры.Счет;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,3,Ссылка);
			КонецЕсли; 
			//
			Проводка.СчетКт = СчетУчета;
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Сотрудник);
			//
			Проводка.Сумма = СтрокаНоменклатуры.СуммаНДС; 
		КонецЕсли; 
		////
		Движение = ТаблицаЗачетаНДС.Добавить();
		Движение.Период	= ?(ЗначениеЗаполнено(ПриходныйДокументДата), ПриходныйДокументДата, Дата);
		Движение.Организация		= Организация;
		Движение.СчетДт				= СтрокаНоменклатуры.Счет;
		Движение.Получатель			= Склад;
		Движение.СчетКт				= СчетУчета;
		Движение.Сумма				= СтрокаНоменклатуры.Сумма;
		Движение.СуммаНДС			= СтрокаНоменклатуры.СуммаНДС;
		Движение.Поставщик			= Контрагент;
		Движение.Договор			= "";
		Движение.ПриходныйДокумент	= ПриходныйДокумент;
		Движение.ЗачетНДС			= СтрокаНоменклатуры.ЗачетНДС;
		Движение.СтавкаНДС			= СтрокаНоменклатуры.СтавкаНДС;
		Движение.ПорядокУплатыНДС	= ПорядокУплатыНДС;
		
	КонецЦикла; 
	//
	Для Каждого СтрокаНоменклатуры Из Номенклатура Цикл
		Проводка = ПроводкиБУ.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание	 = "Поступление номенклатуры на склад";     
		Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(ПриходныйДокумент);     
		//
		Проводка.СчетДт = СтрокаНоменклатуры.СчетУчетаБУ;
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
		УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
		Проводка.КоличествоДт = СтрокаНоменклатуры.Количество;
		//
		Проводка.СчетКт = СчетУчета;
		УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Сотрудник);
		//
		Проводка.Сумма = СтрокаНоменклатуры.Сумма; 
		////
		Если СтрокаНоменклатуры.СуммаНДС<>0 Тогда
			Проводка = ПроводкиБУ.Добавить();
			Проводка.Период      = Дата;
			Проводка.Организация = Организация;
			Проводка.Содержание	 = "Поступление НДС";     
			Проводка.ПервичныйДокумент = СформироватьОписаниеПервичногоДокумента(ПриходныйДокумент);     
			//
			Если ПорядокУплатыНДС = Перечисления.ПорядокУплатыНДС.Общеустановленный Тогда
				Проводка.СчетДт = СчетУчетаАвансаПлатежа("НДС");
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"НалогиИОбязательныеПлатежи",Справочники.ПлатежиВБюджет.НДС);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,"СтавкиНДС",СтрокаНоменклатуры.СтавкаНДС);
			Иначе
				Проводка.СчетДт = СтрокаНоменклатуры.СчетУчетаБУ;
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,СтрокаНоменклатуры.Номенклатура);
				УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Склад);
			КонецЕсли; 
			//
			Проводка.СчетКт = СчетУчета;
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Сотрудник);
			//
			Проводка.Сумма = СтрокаНоменклатуры.СуммаНДС; 
		КонецЕсли; 
		////
		Движение = ТаблицаЗачетаНДС.Добавить();
		Движение.Период	= ?(ЗначениеЗаполнено(ПриходныйДокументДата), ПриходныйДокументДата, Дата);
		Движение.Организация		= Организация;
		Движение.СчетДт				= СтрокаНоменклатуры.СчетУчетаБУ;
		Движение.Получатель			= Склад;
		Движение.СчетКт				= СчетУчета;
		Движение.Сумма				= СтрокаНоменклатуры.Сумма;
		Движение.СуммаНДС			= СтрокаНоменклатуры.СуммаНДС;
		Движение.Поставщик			= Контрагент;
		Движение.Договор			= "";
		Движение.ПриходныйДокумент	= ПриходныйДокумент;
		Движение.ЗачетНДС			= ПолучитьТипЗачетаНДС(СтрокаНоменклатуры.СчетУчетаБУ);
		Движение.СтавкаНДС			= СтрокаНоменклатуры.СтавкаНДС;
		Движение.ПорядокУплатыНДС	= ПорядокУплатыНДС;
	КонецЦикла; 
	ТаблицаЗачетаНДС.Свернуть("Период,Организация,СчетДт,Получатель,СчетКт,Поставщик,Договор,ПриходныйДокумент,ЗачетНДС,СтавкаНДС,ПорядокУплатыНДС","Сумма,СуммаНДС,СуммаНДСПоУслугам,СуммаНДСПоИмпорту");	
	Движения.УчетЗачетаНДС.Загрузить(ТаблицаЗачетаНДС);
КонецПроцедуры
