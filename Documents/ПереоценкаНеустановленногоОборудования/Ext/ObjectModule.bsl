
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ДобавитьПрефиксУзла(Префикс);
	
КонецПроцедуры

Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("Печать","Печать");
	
КонецФункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Заголовок = ПредставлениеДокументаПриПроведении(Ссылка);
	
	СтруктураШапкиДокумента = СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	
	Если ЗаписыватьЦеныВСправочник Тогда
		
		ЗаписатьЦеныВСправочник();
		
	КонецЕсли; 
	
	ДвиженияПоБУ(Режим, Отказ, Заголовок,СтруктураШапкиДокумента);
	
КонецПроцедуры

Процедура ЗаписатьЦеныВСправочник()
	
	Для каждого Строка Из Оборудование Цикл
		
		Если Строка.РазницаПоПереоценке=0 Тогда
			
			Продолжить;
			
		КонецЕсли; 
		
		НоваяЦена=0;
		
		Если Строка.Количество<>0 Тогда
			
			НоваяЦена=окр(Строка.СтоимостьНовая/Строка.Количество,2);
			
		КонецЕсли; 
		
		НоменклатураОбъект= Строка.Номенклатура.ПолучитьОбъект();
		НоменклатураОбъект.Цена=НоваяЦена;
		НоменклатураОбъект.Записать();
		
	КонецЦикла; 				

КонецПроцедуры
 

Процедура ДвиженияПоБУ(Режим, Отказ, Заголовок,СтруктураШапкиДокумента)
	
	Для Каждого Строка Из Оборудование Цикл
		
		Если Строка.РазницаПоПереоценке <> 0 Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период       = Дата;
			Проводка.Активность   = Истина;
			Проводка.Организация  = Организация;
			Проводка.Содержание   = "";
			//Проводка.НомерЖурнала = НомерЖурнала;
			Проводка.Сумма        = Строка.РазницаПоПереоценке;
			
			
			Проводка.СчетДт = Строка.СчетУчетаБУ;
			УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура", Строка.Номенклатура);
			УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Склады", Строка.Склад);
			
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("8511");
			УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Строка.Номенклатура);
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	
	
	
КонецПроцедуры
 

Функция ПечатьДокумента(ИмяМакета)
	
	Запрос=новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПереоценкаНеустановленногоОборудованияОборудование.Номенклатура,
	|	ПереоценкаНеустановленногоОборудованияОборудование.Склад КАК Склад,
	|	ПереоценкаНеустановленногоОборудованияОборудование.Стоимость КАК Стоимость,
	|	ПереоценкаНеустановленногоОборудованияОборудование.СтоимостьНовая КАК СтоимостьНовая,
	|	ПереоценкаНеустановленногоОборудованияОборудование.КоэффициентПереоценки,
	|	ПереоценкаНеустановленногоОборудованияОборудование.РазницаПоПереоценке КАК РазницаПоПереоценке,
	|	ПереоценкаНеустановленногоОборудованияОборудование.ДатаПоступления,
	|	ПереоценкаНеустановленногоОборудованияОборудование.СчетУчетаБУ КАК СчетУчетаБУ,
	|	ПереоценкаНеустановленногоОборудованияОборудование.Количество,
	|	ПереоценкаНеустановленногоОборудованияОборудование.Номенклатура.Код КАК КодНоменклатуры,
	|	ВЫБОР
	|		КОГДА ПереоценкаНеустановленногоОборудованияОборудование.Количество <> 0
	|			ТОГДА ПереоценкаНеустановленногоОборудованияОборудование.СтоимостьНовая / ПереоценкаНеустановленногоОборудованияОборудование.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	Документ.ПереоценкаНеустановленногоОборудования.Оборудование КАК ПереоценкаНеустановленногоОборудованияОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПереоценкаНеустановленногоОборудования КАК ПереоценкаНеустановленногоОборудования
	|		ПО ПереоценкаНеустановленногоОборудованияОборудование.Ссылка = ПереоценкаНеустановленногоОборудования.Ссылка
	|ГДЕ
	|	ПереоценкаНеустановленногоОборудования.Ссылка = &Ссылка
	|ИТОГИ
	|	СУММА(Стоимость),
	|	СУММА(СтоимостьНовая),
	|	СУММА(РазницаПоПереоценке)
	|ПО
	|	ОБЩИЕ,
	|	Склад,
	|	СчетУчетаБУ";
	
	Запрос.УстановитьПараметр("Ссылка",ЭтотОбъект.Ссылка);
	результат=Запрос.Выполнить();
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Макет");
	
	ОбластьИтог = Макет.ПолучитьОбласть("Итог");
	ОбластьСклад = Макет.ПолучитьОбласть("Склад");
	ОбластьСчетУчета = Макет.ПолучитьОбласть("СчетУчета");
	
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.Номер=ПолучитьНомерНаПечать(ЭтотОбъект);
	Область.Параметры.Дата=Формат(Дата,"ДФ=dd.MM.yyyy");
	Область.Параметры.Период=Формат(ПериодПереоценки,"ДФ=yyyy")+" г.";
	ТабДок.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Строка");
	
	ВыборкаОбщие=результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщие.Следующий();
	ОбластьИтог.Параметры.Заполнить(ВыборкаОбщие);	
	
	ТабДок.НачатьАвтогруппировкуСтрок();	
	
	ВыборкаСклад=ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСклад.Следующий() Цикл	
		
		ОбластьСклад.Параметры.Заполнить(ВыборкаСклад);
		ТабДок.Вывести(ОбластьСклад,ВыборкаСклад.Уровень());
		
		ВыборкаСчет=ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСчет.Следующий() Цикл	
			
			ОбластьСчетУчета.Параметры.Заполнить(ВыборкаСчет);
			ТабДок.Вывести(ОбластьСчетУчета,ВыборкаСчет.Уровень());
			
			ВыборкаДетали=ВыборкаСчет.Выбрать();
			Пока ВыборкаДетали.Следующий() Цикл	
				Область.Параметры.Заполнить(ВыборкаДетали);
				ТабДок.Вывести(Область,ВыборкаДетали.Уровень());
			КонецЦикла; 
		КонецЦикла; 
	КонецЦикла; 
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();	
	ТабДок.Вывести(ОбластьИтог);
	
	ТабДок.ПовторятьПриПечатиСтроки=ТабДок.Область(3,0,3);
	ТабДок.Автомасштаб=истина;
	ТабДок.ФиксацияСверху=3;
	ТабДок.ОриентацияСтраницы=ОриентацияСтраницы.Портрет;
	ТабДок.ТолькоПросмотр = Истина;
	
	Возврат ТабДок;

КонецФункции

Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	
	ТабДокумент = ПечатьДокумента(ИмяМакета);
		
	НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "");
	
КонецПроцедуры // Печать()

