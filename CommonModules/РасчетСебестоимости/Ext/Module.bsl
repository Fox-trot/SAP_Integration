	
Функция ПолучитьРасходыПоСчету(Организация,Счет,ДатаОстатка) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Субконто2,
	|	ХозрасчетныйОстатки.Субконто3,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК Сумма,
	|	ХозрасчетныйОстатки.КоличествоСЗОстатокДт КАК КоличествоСЗ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатка, Счет В ИЕРАРХИИ (&Счет), , Организация = &Организация) КАК ХозрасчетныйОстатки";
	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("Счет",Счет);
	Запрос.УстановитьПараметр("ДатаОстатка",ДатаОстатка);
	
	Возврат Запрос.Выполнить();
	
КонецФункции	

Функция ПолучитьБазуРаспределенияРасходов(Организация,ВидБазыРаспределения,НачалоПериода,КонецПериода,Подразделение=Неопределено) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Подразделение",Подразделение);
	
	Если ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.ЗаработнаяПлата 
		или ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.МатериальныеРасходы Тогда
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК Подразделение,
		|	ХозрасчетныйОбороты.Субконто2 КАК Номенклатура,
		|	ХозрасчетныйОбороты.СуммаОборотДт - ХозрасчетныйОбороты.СуммаОборотКт КАК Доля
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2010),
		|			,
		|			Организация = &Организация "+?(Подразделение=Неопределено,""," И Субконто1=&Подразделение ")+",
		|			КорСчет в иерархии (&КорСчета) ,
		|			) КАК ХозрасчетныйОбороты";
		                                                                                                      

		КорСчета=Новый СписокЗначений;
		Если ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.ЗаработнаяПлата Тогда 
			КорСчета.Добавить(ПланыСчетов.Хозрасчетный.А6710);
		ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.МатериальныеРасходы Тогда
			КорСчета.Добавить(ПланыСчетов.Хозрасчетный.А1000);
		КонецЕсли;
		Запрос.УстановитьПараметр("КорСчета",КорСчета);
		
	ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.ОбъемВыпуска Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК Подразделение,
		|	ХозрасчетныйОбороты.Субконто2 КАК Номенклатура,
		|	ХозрасчетныйОбороты.КоличествоОборотДт КАК Доля
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2010), , 
		|	Организация = &Организация "+?(Подразделение=Неопределено,""," И Субконто1=&Подразделение ")+",, ) КАК ХозрасчетныйОбороты";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции	

Функция ПолучитьБазуРаспределенияРасходовДляВспомогательныхЦехов(Организация,ВидБазыРаспределения,НачалоПериода,КонецПериода) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(КонецПериода));
	
	Если ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.ЗаработнаяПлата 
		или ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.МатериальныеРасходы Тогда
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1.ЦентрОтветственности КАК ЦентрОтветственности,
		|	ХозрасчетныйОбороты.Субконто1 КАК Подразделение,
		|	ХозрасчетныйОбороты.СуммаОборотДт - ХозрасчетныйОбороты.СуммаОборотКт КАК Доля
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2310),
		|			,
		|			Организация = &Организация
		|				И Субконто2 В ИЕРАРХИИ (&Родитель),
		|			,
		|			) КАК ХозрасчетныйОбороты";
		
		Если ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.ЗаработнаяПлата Тогда 
			Запрос.УстановитьПараметр("Родитель",Справочники.СтатьиЗатрат.ЗаработнаяПлата);
		ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.МатериальныеРасходы Тогда
			Запрос.УстановитьПараметр("Родитель",Справочники.СтатьиЗатрат.СырьеИМатериалы);
		КонецЕсли;
		
	ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределения.ОбъемВыпуска Тогда
		
		Сообщить("Модуль расчета базы по объему не сделан",СтатусСообщения.Важное);
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции	

Функция ПолучитьБазуРаспределенияРасходовПериода(Организация,ВидБазыРаспределения,НачалоПериода,КонецПериода,Подразделение=Неопределено) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Подразделение",Подразделение);
	
	Если ВидБазыРаспределения=Перечисления.ВидыБазРаспределенияРасходовПериода.ЗаработнаяПлата 
		или ВидБазыРаспределения=Перечисления.ВидыБазРаспределенияРасходовПериода.МатериальныеРасходы Тогда
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК Подразделение,
		|	ХозрасчетныйОбороты.Субконто2 КАК Номенклатура,
		|	ХозрасчетныйОбороты.СуммаОборотКт КАК Доля
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2010),
		|			,
		|			Организация = &Организация  "+?(Подразделение=Неопределено,""," И Субконто1=&Подразделение ")+"
		|				И Субконто3 В ИЕРАРХИИ (&Родитель),
		|			КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2810),
		|			) КАК ХозрасчетныйОбороты";
		
		Если ВидБазыРаспределения=Перечисления.ВидыБазРаспределенияРасходовПериода.ЗаработнаяПлата Тогда 
			Запрос.УстановитьПараметр("Родитель",Справочники.СтатьиЗатрат.ЗарплатаИОтчисления);
		ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределенияРасходовПериода.МатериальныеРасходы Тогда
			Запрос.УстановитьПараметр("Родитель",Справочники.СтатьиЗатрат.СырьеИМатериалы);
		КонецЕсли;
		
	ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределенияРасходовПериода.СебестоимостьТоварногоВыпуска Тогда
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК Подразделение,
		|	ХозрасчетныйОбороты.Субконто2 КАК Номенклатура,
		|	ХозрасчетныйОбороты.СуммаОборотКт КАК Доля
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2010),
		|			,
		|			Организация = &Организация  "+?(Подразделение=Неопределено,""," И Субконто1=&Подразделение ")+",
		|			КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2810),
		|			) КАК ХозрасчетныйОбороты";
		
		
	ИначеЕсли ВидБазыРаспределения=Перечисления.ВидыБазРаспределенияРасходовПериода.СтоимостьТоварногоВыпуска Тогда
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ОтпускныеЦеныНоменклатурыПродажСрезПоследних.Номенклатура,
		|	ОтпускныеЦеныНоменклатурыПродажСрезПоследних.Цена
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ОтпускныеЦеныНоменклатурыПродаж.СрезПоследних(&КонецПериода, Организация = &Организация) КАК ОтпускныеЦеныНоменклатурыПродажСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК Подразделение,
		|	ХозрасчетныйОбороты.Субконто2 КАК Номенклатура,
		|	ХозрасчетныйОбороты.КоличествоОборотКт * ЦеныНоменклатуры.Цена КАК Доля
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2010), , 
		|	Организация = &Организация  "+?(Подразделение=Неопределено,""," И Субконто1=&Подразделение ")+", 
		|	КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.А2810), ) КАК ХозрасчетныйОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ХозрасчетныйОбороты.Субконто2 = ЦеныНоменклатуры.Номенклатура";
		
		
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции	

Процедура РаспределитьПоБазе(Дата,Организация,ТаблицаДвижений,Выборка,БазаРаспределения,РаспределятьПоПодразделениям) Экспорт
	
	ОбщаяБаза=0;
	
	Если РаспределятьПоПодразделениям Тогда
		
		СтрокиБазы=БазаРаспределения.НайтиСтроки(Новый Структура("Подразделение",Выборка.Субконто1));	
		
		Для н=1 по СтрокиБазы.Количество() Цикл
			ОбщаяБаза=ОбщаяБаза+СтрокиБазы[н-1].Доля;
		КонецЦикла;
		
	Иначе
		
		ОбщаяБаза=БазаРаспределения.Итог("Доля");
		
		СтрокиБазы=Новый Массив;
		
		Для Каждого Строка Из БазаРаспределения Цикл
			
			СтрокиБазы.Добавить(Строка);	
			
		КонецЦикла;	
		
	КонецЕсли;
	
	Если ОбщаяБаза=0 Тогда
		Возврат;
	КонецЕсли;	

	СуммаОстаток=Выборка.Сумма;
	КоличествоСЗОстаток=Выборка.КоличествоСЗ;
	
	Для н=1 по СтрокиБазы.Количество() Цикл
		
		СтрокаБазы=СтрокиБазы[н-1];
		
		СуммаВПроводку=0;
		КоличествоВПроводку=0;
		
		Если н=СтрокиБазы.Количество() Тогда
			СуммаВПроводку=СуммаОстаток;
			КоличествоСЗВПроводку=КоличествоСЗОстаток;
		Иначе
			СуммаВПроводку=Окр(Выборка.Сумма/ОбщаяБаза*СтрокаБазы.Доля,2);
			СуммаОстаток=СуммаОстаток-СуммаВПроводку;
			
			КоличествоСЗВПроводку=Окр(Выборка.КоличествоСЗ/ОбщаяБаза*СтрокаБазы.Доля,5);
			КоличествоСЗОстаток=КоличествоСЗОстаток-КоличествоСЗВПроводку;
		КонецЕсли;	
		
		
		Проводка = ТаблицаДвижений.Добавить();
		Проводка.Период      = Дата;
		Проводка.Организация = Организация;
		Проводка.Содержание = "";
		
		Проводка.СчетДт = ПланыСчетов.Хозрасчетный.А2010;
		//Проводка.СубконтоДт1=Выборка.Субконто1;
		Проводка.СубконтоДт1=СтрокаБазы.Подразделение;
		Проводка.СубконтоДт2=СтрокаБазы.Номенклатура;
		Проводка.СубконтоДт3=Выборка.Субконто2;
		Проводка.СубконтоДт4=Выборка.Субконто3;
		
		Проводка.СчетКт = Выборка.Счет;
		Проводка.СубконтоКт1=Выборка.Субконто1;
		Проводка.СубконтоКт2=Выборка.Субконто2;
		Проводка.СубконтоКт3=Выборка.Субконто3;
		
		Проводка.Сумма = СуммаВПроводку;
		
		Проводка.КоличествоСЗДт = КоличествоСЗВПроводку;
		Проводка.КоличествоСЗКт = КоличествоСЗВПроводку;
		
	КонецЦикла;	

КонецПроцедуры	

