
// получить данные для расчета НДФЛ по ставке 13 процентов
// Параметры: 
//  ПоследнийМесяцНалоговогоПериода - месяц налогового периода, по который (включительно) считается налог. 
//  						Это текущий или будущий месяц относительно периода регистрации
//							Если Неопределено - нужно определить максимальный месяц налогового периода и 
//							минимальный месяц налоговго периода 
//  ПериодРегистрации - дата, по которую учитываются зарегистрированные доходы и начисленные налоги 
//  Организация - ссылка на организацию
//  Регистратор - ссылка на регистратор записей НДФЛ
//  СписокФизЛицТекст - текст запроса выборки списка физлиц, по которым необходимо выполнить расчет налога
//	ДополнительныеПараметрыЗапроса - структура значений параметров запроса, необходимых для выполнения текста запроса "СписокФизЛицТекст"
//
// Возвращаемое значение:
//  Нет.
//
Функция ПолучитьДанныеНДФЛПоРегистратору(Знач ПоследнийМесяцНалоговогоПериода, ПериодРегистрации, Организация, Регистратор, СписокФизЛицТекст, ДополнительныеПараметрыЗапроса = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамОрганизация",       Организация);
	Запрос.УстановитьПараметр("парамРегистратор",       Регистратор);
	Запрос.УстановитьПараметр("парамПериодРегистрации", НачалоМесяца(ПериодРегистрации));

	Отказ = Ложь;
	УчетнаяПолитикаНУ = ПолучитьПараметрыУчетнойПолитики(КонецГода(ПериодРегистрации), Отказ, Организация);
	Если Отказ Тогда
		НалоговаяПолитикаНДФЛ = Перечисления.ОсобенностиИсчисленияНДФЛ.СтандартныеВычетыНарастающимИтогом;
	Иначе
		НалоговаяПолитикаНДФЛ = УчетнаяПолитикаНУ.ОсобенностиИсчисленияНДФЛ;
	КонецЕсли;
	
	Если ПоследнийМесяцНалоговогоПериода = Неопределено Тогда

		// получим ПоследнийМесяцНалоговогоПериода
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НДФЛСведенияОДоходах.Период КАК Период
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + СписокФизЛицТекст + ") КАК Работники
		|		ПО Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
		|
		|ГДЕ
		|	НДФЛСведенияОДоходах.Организация = &парамОрганизация
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
		Результат = Запрос.Выполнить();

		Если Результат.Пустой() Тогда
			ПоследнийМесяцНалоговогоПериода = КонецМесяца(ПериодРегистрации);

		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ПоследнийМесяцНалоговогоПериода = КонецМесяца(Выборка.Период);

		КонецЕсли;

	КонецЕсли;

	// получим самый ранний месяц налогового периода
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НДФЛСведенияОДоходах.Период КАК Период
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + СписокФизЛицТекст + ") КАК Работники
	|		ПО Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
	|
	|ГДЕ
	|	НДФЛСведенияОДоходах.Организация = &парамОрганизация И НДФЛСведенияОДоходах.ПериодРегистрации = &парамПериодРегистрации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	Результат = Запрос.Выполнить();

	Если Результат.Пустой() Тогда
		ПервыйМесяцНалоговогоПериода = ПериодРегистрации;

	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПервыйМесяцНалоговогоПериода = НачалоМесяца(Выборка.Период);

	КонецЕсли;

	Запрос.УстановитьПараметр("парамСтавка13",                     Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
	Запрос.УстановитьПараметр("парамСтавкаРезидента13", ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13) / 100);
	
	Запрос.УстановитьПараметр("парамКонецМесяцаНалоговогоПериода", КонецМесяца(ПоследнийМесяцНалоговогоПериода));
	Запрос.УстановитьПараметр("парамНачалоГода",                   НачалоГода(ПервыйМесяцНалоговогоПериода));
	Запрос.УстановитьПараметр("парамКонецГода",                    КонецГода(ПоследнийМесяцНалоговогоПериода));
	Запрос.УстановитьПараметр("парамПриход",                       ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("парамВидСтроки",                    Перечисления.НДФЛРасчетыСБюджетомВидСтроки.Начисление);
	
	Запрос.УстановитьПараметр("КодВычета312", Справочники.ВычетыНДФЛ.Код312);
	Запрос.УстановитьПараметр("КодВычета311", Справочники.ВычетыНДФЛ.Код311);
	
	КодыВычетовРезидентов = Новый Массив;
	КодыВычетовРезидентов.Добавить(Справочники.ВычетыНДФЛ.Код401);
	КодыВычетовРезидентов.Добавить(Справочники.ВычетыНДФЛ.Код402);
	КодыВычетовРезидентов.Добавить(Справочники.ВычетыНДФЛ.Код403);
	КодыВычетовРезидентов.Добавить(Справочники.ВычетыНДФЛ.Код404);
	КодыВычетовРезидентов.Добавить(Справочники.ВычетыНДФЛ.Код405);
	Запрос.УстановитьПараметр("КодыВычетовРезидентов", КодыВычетовРезидентов);
	
	// установим дополнительные параметры
	Если ДополнительныеПараметрыЗапроса <> Неопределено Тогда
		Для каждого Поле Из ДополнительныеПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(Поле.Ключ, Поле.Значение);
		КонецЦикла;
	КонецЕсли; 

	// Периоды
	// Таблица список периодов-физлиц по которым необходимо выполнить расчет налога
	// Поля:
	//		Период
	//		Физлицо

	ПериодыТекст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) КАК Период,
	|	НДФЛСведенияОДоходах.ФизЛицо КАК Физлицо
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + СписокФизЛицТекст + ") КАК Работники
	|		ПО Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
	|
	|ГДЕ
	|	НДФЛСведенияОДоходах.ПериодРегистрации >= &парамНачалоГода И
	|	НДФЛСведенияОДоходах.ПериодРегистрации <= &парамПериодРегистрации И
	|	НДФЛСведенияОДоходах.Организация = &парамОрганизация И
	|	НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &парамСтавка13";

	// ФизлицаНеРезиденты
	//	Поля:
	//		Физлицо
	//
	//	Описание:
	//	сисок физлиц-нерезидентов

	ФизлицаНеРезидентыТекст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГражданствоФизЛиц.ФизЛицо
	|ИЗ
	|	РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&парамКонецГода) КАК ГражданствоФизЛиц
	|
	|ГДЕ
	|	(ГражданствоФизЛиц.НеЯвляетсяНалоговымРезидентомРУз)";

	// ДоходыСКодамиВычетов
	//	Поля:
	//		ФизЛицо
	//		Период
	//		КодДохода
	//		КодВычета
	//		СуммаДоходаОборот
	//		СуммаВычетаОборот
	//
	//	Описание:
	// Выбирает облагаемые налогом доходы за весь год по физлицам из СписокФизлиц
	ДоходыСКодамиВычетовТекст = 
	"ВЫБРАТЬ
	|	НДФЛОбороты.Физлицо КАК ФизЛицо,
	|	НДФЛОбороты.Период КАК Период,
	|	НДФЛОбороты.КодДохода КАК КодДохода,
	|	НДФЛОбороты.КодВычета КАК КодВычета,
	|	СУММА(НДФЛОбороты.СуммаДоходаОборот) КАК СуммаДоходаОборот,
	|	СУММА(НДФЛОбороты.СуммаВычетаОборот) КАК СуммаВычетаОборот
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) КАК Период,
	|		НДФЛСведенияОДоходах.ФизЛицо КАК Физлицо,
	|		НДФЛСведенияОДоходах.КодДохода КАК КодДохода,
	|		НДФЛСведенияОДоходах.КодВычета КАК КодВычета,
	|		СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДоходаОборот,
	|		СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычетаОборот
	|	ИЗ
	|		РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + СписокФизЛицТекст + ") КАК Работники
	|			ПО Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
	|	
	|	ГДЕ
	|		НДФЛСведенияОДоходах.Период >= &парамНачалоГода И
	|		НДФЛСведенияОДоходах.Период <= &парамКонецМесяцаНалоговогоПериода И
	|		НДФЛСведенияОДоходах.ПериодРегистрации <= &парамПериодРегистрации И
	|		НДФЛСведенияОДоходах.Организация = &парамОрганизация И
	|		НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &парамСтавка13
	|	
	|	СГРУППИРОВАТЬ ПО
	|		НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ),
	|		НДФЛСведенияОДоходах.ФизЛицо,
	|		НДФЛСведенияОДоходах.КодДохода,
	|		НДФЛСведенияОДоходах.КодВычета) КАК НДФЛОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛОбороты.Физлицо,
	|	НДФЛОбороты.Период,
	|	НДФЛОбороты.КодДохода,
	|	НДФЛОбороты.КодВычета";

    Ставка30Текст        = "0.3";
	ТипЗначенияНДФЛТекст = "ЧИСЛО(10, 0)";

	ПериодыСКодамиТекст = 
	"ВЫБРАТЬ
	|	ПериодФизлицо.Период КАК Период,
	|	ПериодФизлицо.Физлицо КАК Физлицо,
	|	ВЫБОР КОГДА (ФизлицаНеРезиденты.ФизЛицо) ЕСТЬ NULL  ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК Резидент,
	|	КодДоходаФизлицо.КодДохода КАК КодДохода
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) КАК Период,
	|		НДФЛСведенияОДоходах.ФизЛицо КАК Физлицо
	|	ИЗ
	|		РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + СписокФизЛицТекст + ") КАК Работники
	|			ПО Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
	|	
	|	ГДЕ
	|		НДФЛСведенияОДоходах.ПериодРегистрации >= &парамНачалоГода И
	|		НДФЛСведенияОДоходах.ПериодРегистрации <= &парамПериодРегистрации И
	|		НДФЛСведенияОДоходах.Организация = &парамОрганизация И
	|		НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &парамСтавка13) КАК ПериодФизлицо
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			НДФЛСведенияОДоходах.КодДохода КАК КодДохода,
	|			НДФЛСведенияОДоходах.ФизЛицо КАК Физлицо
	|		ИЗ
	|			РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + СписокФизЛицТекст + ") КАК Работники
	|				ПО Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
	|		
	|		ГДЕ
	|			НДФЛСведенияОДоходах.ПериодРегистрации >= &парамНачалоГода И
	|			НДФЛСведенияОДоходах.ПериодРегистрации <= &парамПериодРегистрации И
	|			НДФЛСведенияОДоходах.Организация = &парамОрганизация И
	|			НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &парамСтавка13) КАК КодДоходаФизлицо
	|		ПО КодДоходаФизлицо.Физлицо = ПериодФизлицо.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ФизлицаНеРезидентыТекст + ") КАК ФизлицаНеРезиденты
	|		ПО ФизлицаНеРезиденты.ФизЛицо = КодДоходаФизлицо.Физлицо";
	
	
	// ДоходыЗаГод 
	//	Поля:
	//		ФизЛицо
	//		Период
	//		Резидент
	//		ОблагаемыйДоходЗаГод
	//
	//	Описание:
	//	Выбирает по всем периодам с начала года сумму дохода нарастающим итогом за год

	ДоходыЗаГодТекст = "
	|ВЫБРАТЬ
	|	НДФЛОбороты.Период КАК Период,
	|	НДФЛОбороты.ФизЛицо КАК ФизЛицо,
	|	НДФЛОбороты.Резидент КАК Резидент,
	|	СУММА(НДФЛОбороты.НалогНерезидента) КАК НалогНерезидента,
	|	СУММА(НДФЛОбороты.ОблагаемыйДоходЗаПериод) КАК ОблагаемыйДоходЗаПериод,
	|	СУММА(НДФЛОбороты.ОблагаемыйДоходЗаГод) КАК ОблагаемыйДоходЗаГод
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		Периоды.Физлицо КАК ФизЛицо,
	|		Периоды.Резидент КАК Резидент,
	|		ВЫБОР КОГДА НДФЛОбороты.Период = Периоды.Период ТОГДА ВЫРАЗИТЬ((НДФЛОбороты.СуммаДоходаОборот - ВЫБОР КОГДА Периоды.Резидент ТОГДА НДФЛОбороты.СуммаВычетаОборот ИНАЧЕ 0 КОНЕЦ) * " + Ставка30Текст + " КАК " + ТипЗначенияНДФЛТекст + ") ИНАЧЕ 0 КОНЕЦ КАК НалогНерезидента,
    |       ВЫБОР КОГДА НДФЛОбороты.Период = Периоды.Период ТОГДА НДФЛОбороты.СуммаДоходаОборот - ВЫБОР КОГДА Периоды.Резидент ИЛИ НДФЛОбороты.КодВычета В (&КодыВычетовРезидентов) ТОГДА НДФЛОбороты.СуммаВычетаОборот ИНАЧЕ 0 КОНЕЦ ИНАЧЕ 0 КОНЕЦ КАК ОблагаемыйДоходЗаПериод,
	|		НДФЛОбороты.СуммаДоходаОборот - ВЫБОР КОГДА Периоды.Резидент ИЛИ НДФЛОбороты.КодВычета В (&КодыВычетовРезидентов) ТОГДА НДФЛОбороты.СуммаВычетаОборот ИНАЧЕ 0 КОНЕЦ КАК ОблагаемыйДоходЗаГод
	|	ИЗ
	|		(" + ПериодыСКодамиТекст + ") КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ (" + ДоходыСКодамиВычетовТекст + ") КАК НДФЛОбороты
	|			ПО Периоды.Период >= НДФЛОбороты.Период И НДФЛОбороты.Период >= НАЧАЛОПЕРИОДА(Периоды.Период, Год) И Периоды.Физлицо = НДФЛОбороты.Физлицо И Периоды.КодДохода = НДФЛОбороты.КодДохода) КАК НДФЛОбороты
    |
	|СГРУППИРОВАТЬ ПО
	|	НДФЛОбороты.Период,
	|	НДФЛОбороты.ФизЛицо,
	|	НДФЛОбороты.Резидент";

	// НДФЛРасчетыСБюджетом
	//	Поля:
	//		ФизЛицо
	//		МесяцНалоговогоПериода
	//		ИсчисленныйНалог
	//		ПримененныеВычетыЛичные - уже примененные личные стандартные вычеты по НДФЛ
	//		ПолеПримененныеВычетыДетские - уже примененные вычеты на детей по НДФЛ
	//
	//	Описание:
	//	Выбирает исчисленные налоги (приходы по регистру НДФЛРасчетыСБюджетом)
	//  для СписокФизлиц

	НДФЛРасчетыСБюджетомТекст = 
	"ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|	СУММА(НДФЛРасчетыСБюджетом.Налог) КАК ИсчисленныйНалог,
	|	СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычетЛичный) КАК ПримененныеВычетыЛичные,
	|	СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычетНаДетей) КАК ПолеПримененныеВычетыДетские,
	|	СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычетНаДетейИнвалидов) КАК ПолеПримененныеВычетыНаДетейИнвалидов
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ("+СписокФизлицТекст+") КАК РаботникиОрганизации
	|		ПО РаботникиОрганизации.Физлицо = НДФЛРасчетыСБюджетом.ФизЛицо
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода >= &парамНачалоГода
	|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода <= &парамКонецМесяцаНалоговогоПериода
	|	И НДФЛРасчетыСБюджетом.Организация = &парамОрганизация
	|	И НДФЛРасчетыСБюджетом.Регистратор <> &парамРегистратор
	|	И НДФЛРасчетыСБюджетом.Период <= &парамКонецМесяцаНалоговогоПериода
	|	И НДФЛРасчетыСБюджетом.ВидДвижения = &парамПриход
	|	И НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = &парамСтавка13
	|	И НДФЛРасчетыСБюджетом.ВидСтроки = &парамВидСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.ФизЛицо";

	// НДФЛРасчетыСБюджетомЗаГод
	//	Поля:
	//		ФизЛицо
	//		Период
	//		ИсчисленныйНалогЗаГод
	//		ПримененныеВычетыЛичныеЗаГод
	//		ПолеПримененныеВычетыДетскиеЗаГод
	//		ПолеПримененныеВычетыНаДетейИнвалидовЗаГод
	//
	//	Описание:
	//	Выбирает исчисленные налоги нарастающим итогом за год
	
	НДФЛРасчетыСБюджетомЗаГодТекст = 
	"ВЫБРАТЬ
	|	Периоды.Период КАК Период,
	|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА ФизлицаНеРезиденты.ФизЛицо ЕСТЬ NULL 
	|			ТОГДА СУММА(НДФЛРасчетыСБюджетом.ИсчисленныйНалог)
	|		ИНАЧЕ МАКСИМУМ(ВЫБОР
	|					КОГДА Периоды.Период <> НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода
	|						ТОГДА -99999999
	|					ИНАЧЕ НДФЛРасчетыСБюджетом.ИсчисленныйНалог
	|				КОНЕЦ)
	|	КОНЕЦ КАК ИсчисленныйНалогЗаГод,
	|	СУММА(НДФЛРасчетыСБюджетом.ПримененныеВычетыЛичные) КАК ПримененныеВычетыЛичныеЗаГод,
	|	СУММА(НДФЛРасчетыСБюджетом.ПолеПримененныеВычетыДетские) КАК ПолеПримененныеВычетыДетскиеЗаГод,
	|	СУММА(НДФЛРасчетыСБюджетом.ПолеПримененныеВычетыНаДетейИнвалидов) КАК ПолеПримененныеВычетыНаДетейИнвалидовЗаГод
	|ИЗ
	|	(" + ПериодыТекст + ") КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + НДФЛРасчетыСБюджетомТекст + ") КАК НДФЛРасчетыСБюджетом
	|		ПО (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ НАЧАЛОПЕРИОДА(Периоды.Период, ГОД) И Периоды.Период)
	|			И Периоды.Физлицо = НДФЛРасчетыСБюджетом.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ГражданствоФизЛиц.ФизЛицо КАК ФизЛицо
	|		ИЗ
	|			РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&парамКонецГода, ) КАК ГражданствоФизЛиц
	|		ГДЕ
	|			ГражданствоФизЛиц.НеЯвляетсяНалоговымРезидентомРУз) КАК ФизлицаНеРезиденты
	|		ПО ФизлицаНеРезиденты.ФизЛицо = Периоды.Физлицо
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.Период,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	ФизлицаНеРезиденты.ФизЛицо";

	// ПрименениеВычетовВОрганизации
	//	Поля:
	//		Физлицо,
	//		Период
	//
	// Описание:
	//	Выбирает список физлиц + периодв в которые применяются стандартные вычеты по 
	//	заданной организации

	ПрименениеВычетовВОрганизацииТекст = 
	"ВЫБРАТЬ
	|	НДФЛПрименениеВычетовСрезПоследних.Физлицо КАК Физлицо,
	|	НДФЛПрименениеВычетовСрезПоследних.Период КАК Период
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(НДФЛПрименениеВычетов.Период) КАК ПериодСреза,
	|		Периоды.Физлицо КАК Физлицо,
	|		Периоды.Период КАК Период
	|	ИЗ
	|		("+ПериодыТекст+") КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
	|			ПО Периоды.Период >= НДФЛПрименениеВычетов.Период И НДФЛПрименениеВычетов.Физлицо = Периоды.Физлицо
	
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Физлицо,
	|		Периоды.Период) КАК НДФЛПрименениеВычетовСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
	|		ПО НДФЛПрименениеВычетов.Период = НДФЛПрименениеВычетовСрезПоследних.ПериодСреза И НДФЛПрименениеВычетов.Физлицо = НДФЛПрименениеВычетовСрезПоследних.Физлицо
	|
	|ГДЕ
	|	НДФЛПрименениеВычетов.Организация = &парамОрганизация";
	
    // НДФЛРазмерВычетов
	// Таблица "НДФЛРазмерВычетов": - таблица это список вычетов, их размер, ограничения по доходам
	// поля:
	//		Период, 
	//		КодВычета,
	//		Размер, 
	//		ОграничениеПоДоходам,
	// Описание:	
	//	Выбираем Из Список периодов (период - год)
	//	Левое соединение с НДФЛРазмерВычетов
	//	по равенству периодов
	// 
	
	// первый год
	НачалоГода = НачалоГода(ПоследнийМесяцНалоговогоПериода);	
	ПериодыТекстПоГодам = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачалоГода,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	Для Сч = Год(ПервыйМесяцНалоговогоПериода)+ 1 По Год(ПоследнийМесяцНалоговогоПериода) Цикл
		НачалоГода = НачалоГода(НачалоГода - 1);
		ПериодыТекстПоГодам = ПериодыТекстПоГодам +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачалоГода,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	
	НДФЛРазмерВычетов =
	"ВЫБРАТЬ
	|	Периоды.Период КАК Период,
	|	ЕСТЬNULL(НДФЛРазмерВычетов.КодВычета, 0) КАК КодВычета,
	|	НДФЛРазмерВычетов.Размер КАК Размер,
	|	НДФЛРазмерВычетов.ОграничениеПоДоходам КАК ОграничениеПоДоходам
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		НДФЛРазмерВычетов.КодВычета КАК КодВычета,
	|		МАКСИМУМ(НДФЛРазмерВычетов.Период) КАК ПериодРегистра
	|	ИЗ
	|		("+ПериодыТекстПоГодам+") КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов КАК НДФЛРазмерВычетов
	|			ПО Периоды.Период >= НДФЛРазмерВычетов.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Период,
	|		НДФЛРазмерВычетов.КодВычета) КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов КАК НДФЛРазмерВычетов
	|		ПО НДФЛРазмерВычетов.Период = Периоды.ПериодРегистра И Периоды.КодВычета = НДФЛРазмерВычетов.КодВычета ";

	// ПраваНаВычеты
	//	Поля:
	//		Физлицо
	//		Период
	//		СтандартныеВычетыЛичные
	//		СтандартныеВычетыДетские
	//
	//	Описание:
	//	Выбирает по всем периодам года стандартные вычеты на которые имеет право физлицо

	ПраваНаВычетыТекст = 
	"ВЫБРАТЬ
	|	НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо КАК Физлицо,
	|	НДФЛСтандартныеВычетыФизлицСрезПоследних.Период КАК Период,
	|	НДФЛРазмерВычетовЛичный.ОграничениеПоДоходам КАК ОграничениеПоДоходамЛичные,
	|	НДФЛРазмерВычетовНаДетей.ОграничениеПоДоходам КАК ОграничениеПоДоходамНаДетей,
	|	НДФЛРазмерВычетовНаДетейИнвалидов.ОграничениеПоДоходам КАК ОграничениеПоДоходамНаДетейИнвалидов,
	|	ВЫБОР КОГДА (ПрименениеВычетов.Физлицо) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ЕСТЬNULL(НДФЛРазмерВычетовЛичный.Размер, 0) КОНЕЦ КАК СтандартныеВычетыЛичные,
	|	ВЫБОР КОГДА (ПрименениеВычетов.Физлицо) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ЕСТЬNULL(НДФЛРазмерВычетовНаДетей.Размер, 0) * ЕСТЬNULL(ВычетыФизлиц.КоличествоДетей, 0) КОНЕЦ КАК СтандартныеВычетыДетские,
	|	ВЫБОР КОГДА (ПрименениеВычетов.Физлицо) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ЕСТЬNULL(НДФЛРазмерВычетовНаДетейИнвалидов.Размер, 0) * ЕСТЬNULL(ВычетыФизлиц.КоличествоДетейИнвалидов, 0) КОНЕЦ КАК СтандартныеВычетыДетскиеИнвалидов
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ВычетыФизлиц.Период) КАК ПериодСреза,
	|		Периоды.Физлицо КАК Физлицо,
	|		Периоды.Период КАК Период
	|	ИЗ
	|		(" + ПериодыТекст + ") КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
	|			ПО ВычетыФизлиц.Период <= Периоды.Период И ВычетыФизлиц.Физлицо = Периоды.Физлицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Физлицо,
	|		Периоды.Период) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
	|		ПО ВычетыФизлиц.Период = НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодСреза И ВычетыФизлиц.Физлицо = НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + НДФЛРазмерВычетов + ") КАК НДФЛРазмерВычетовЛичный
	|		ПО НДФЛРазмерВычетовЛичный.КодВычета = ВычетыФизлиц.КодВычетаЛичный И НДФЛРазмерВычетовЛичный.Период = НАЧАЛОПЕРИОДА(НДФЛСтандартныеВычетыФизлицСрезПоследних.Период, Год)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + НДФЛРазмерВычетов + ") КАК НДФЛРазмерВычетовНаДетей
	|		ПО НДФЛРазмерВычетовНаДетей.КодВычета = ВычетыФизлиц.КодВычетаНаДетей И НДФЛРазмерВычетовНаДетей.Период = НАЧАЛОПЕРИОДА(НДФЛСтандартныеВычетыФизлицСрезПоследних.Период, Год)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + НДФЛРазмерВычетов + ") КАК НДФЛРазмерВычетовНаДетейИнвалидов
	|		ПО НДФЛРазмерВычетовНаДетейИнвалидов.КодВычета = ВычетыФизлиц.КодВычетаНаДетейИнвалидов И НДФЛРазмерВычетовНаДетейИнвалидов.Период = НАЧАЛОПЕРИОДА(НДФЛСтандартныеВычетыФизлицСрезПоследних.Период, Год)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ПрименениеВычетовВОрганизацииТекст + ") КАК ПрименениеВычетов
	|		ПО ПрименениеВычетов.Физлицо = НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо И ПрименениеВычетов.Период = НДФЛСтандартныеВычетыФизлицСрезПоследних.Период";

	// ПрименяемыеВычеты
	// Поля:
	//		Физлицо
	//		Период
	//		ПрименяемыеВычетыЛичные
	//		ПрименяемыеВычетыДетские
	//
	//	Описание:
	//	Выбирает по всем периодам года применяемые стандартные вычеты
	//  с использованием ограничения по доходам

    Если НалоговаяПолитикаНДФЛ = Перечисления.ОсобенностиИсчисленияНДФЛ.СтандартныеВычетыНарастающимИтогом Тогда
    
    	ПрименяемыеВычетыТекст = 
    	"ВЫБРАТЬ
    	|	РаботникиОргПраваНаВычеты.Физлицо КАК Физлицо,
    	|	РаботникиОргПраваНаВычеты.Период КАК Период,
    	|	ВЫБОР КОГДА ВЫБОР КОГДА (ДоходыЗаГод.ОблагаемыйДоходЗаГод) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаГод КОНЕЦ + ВЫБОР КОГДА (НДФЛДоходыПредыдущегоМестаРаботы.Размер) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НДФЛДоходыПредыдущегоМестаРаботы.Размер КОНЕЦ <= РаботникиОргПраваНаВычеты.ОграничениеПоДоходамЛичные  ИЛИ РаботникиОргПраваНаВычеты.ОграничениеПоДоходамЛичные  = 0 ТОГДА РаботникиОргПраваНаВычеты.СтандартныеВычетыЛичные  ИНАЧЕ 0 КОНЕЦ КАК ПрименяемыеВычетыЛичные,
    	|	ВЫБОР КОГДА ВЫБОР КОГДА (ДоходыЗаГод.ОблагаемыйДоходЗаГод) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаГод КОНЕЦ + ВЫБОР КОГДА (НДФЛДоходыПредыдущегоМестаРаботы.Размер) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НДФЛДоходыПредыдущегоМестаРаботы.Размер КОНЕЦ <= РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетей ИЛИ РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетей = 0 ТОГДА РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские ИНАЧЕ 0 КОНЕЦ КАК ПрименяемыеВычетыДетские,
    	|	ВЫБОР КОГДА ВЫБОР КОГДА (ДоходыЗаГод.ОблагаемыйДоходЗаГод) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаГод КОНЕЦ + ВЫБОР КОГДА (НДФЛДоходыПредыдущегоМестаРаботы.Размер) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НДФЛДоходыПредыдущегоМестаРаботы.Размер КОНЕЦ <= РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетейИнвалидов ИЛИ РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетейИнвалидов = 0 ТОГДА РаботникиОргПраваНаВычеты.СтандартныеВычетыДетскиеИнвалидов ИНАЧЕ 0 КОНЕЦ КАК ПрименяемыеВычетыДетскиеИнвалидов
    	|ИЗ
    	|	(" + ПраваНаВычетыТекст + ") КАК РаботникиОргПраваНаВычеты
    	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ДоходыЗаГодТекст + ") КАК ДоходыЗаГод
    	|		ПО ДоходыЗаГод.Период = РаботникиОргПраваНаВычеты.Период И ДоходыЗаГод.ФизЛицо = РаботникиОргПраваНаВычеты.Физлицо
    	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК НДФЛДоходыПредыдущегоМестаРаботы
    	|		ПО (НДФЛДоходыПредыдущегоМестаРаботы.Организация = &парамОрганизация) И НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода = РаботникиОргПраваНаВычеты.Период И НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо = РаботникиОргПраваНаВычеты.Физлицо";
    Иначе
        
        ПрименяемыеВычетыТекст = 
    	"ВЫБРАТЬ
    	|	РаботникиОргПраваНаВычеты.Физлицо КАК Физлицо,
    	|	РаботникиОргПраваНаВычеты.Период КАК Период,
    	|	ВЫБОР КОГДА ВЫБОР КОГДА (ДоходыЗаГод.ОблагаемыйДоходЗаГод) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаГод КОНЕЦ + ВЫБОР КОГДА (НДФЛДоходыПредыдущегоМестаРаботы.Размер) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НДФЛДоходыПредыдущегоМестаРаботы.Размер КОНЕЦ <= РаботникиОргПраваНаВычеты.ОграничениеПоДоходамЛичные  ИЛИ РаботникиОргПраваНаВычеты.ОграничениеПоДоходамЛичные  = 0 ТОГДА 
        |ВЫБОР
        |    КОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод > РаботникиОргПраваНаВычеты.СтандартныеВычетыЛичные + РаботникиОргПраваНаВычеты.СтандартныеВычетыДетскиеИнвалидов + РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские
        |        ТОГДА РаботникиОргПраваНаВычеты.СтандартныеВычетыЛичные
        |    КОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод - РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские - РаботникиОргПраваНаВычеты.СтандартныеВычетыДетскиеИнвалидов > 0
        |    ТОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод - РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские - РаботникиОргПраваНаВычеты.СтандартныеВычетыДетскиеИнвалидов
        |    ИНАЧЕ 0
        |КОНЕЦ  ИНАЧЕ 0 КОНЕЦ КАК ПрименяемыеВычетыЛичные,
    	|	ВЫБОР КОГДА ВЫБОР КОГДА (ДоходыЗаГод.ОблагаемыйДоходЗаГод) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаГод КОНЕЦ + ВЫБОР КОГДА (НДФЛДоходыПредыдущегоМестаРаботы.Размер) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НДФЛДоходыПредыдущегоМестаРаботы.Размер КОНЕЦ <= РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетей ИЛИ РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетей = 0 ТОГДА 
        |ВЫБОР
        |    КОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод > РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские
        |        ТОГДА РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские
        |    ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаПериод
        |КОНЕЦ ИНАЧЕ 0 КОНЕЦ КАК ПрименяемыеВычетыДетские,
    	|	ВЫБОР КОГДА ВЫБОР КОГДА (ДоходыЗаГод.ОблагаемыйДоходЗаГод) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ ДоходыЗаГод.ОблагаемыйДоходЗаГод КОНЕЦ + ВЫБОР КОГДА (НДФЛДоходыПредыдущегоМестаРаботы.Размер) ЕСТЬ NULL  ТОГДА 0 ИНАЧЕ НДФЛДоходыПредыдущегоМестаРаботы.Размер КОНЕЦ <= РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетейИнвалидов ИЛИ РаботникиОргПраваНаВычеты.ОграничениеПоДоходамНаДетейИнвалидов = 0 ТОГДА 
        |ВЫБОР
        |    КОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод > РаботникиОргПраваНаВычеты.СтандартныеВычетыДетскиеИнвалидов + РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские
        |        ТОГДА РаботникиОргПраваНаВычеты.СтандартныеВычетыДетскиеИнвалидов
        |    КОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод - РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские > 0
        |    ТОГДА ДоходыЗаГод.ОблагаемыйДоходЗаПериод - РаботникиОргПраваНаВычеты.СтандартныеВычетыДетские
        |    ИНАЧЕ 0
        |КОНЕЦ ИНАЧЕ 0 КОНЕЦ КАК ПрименяемыеВычетыДетскиеИнвалидов
    	|ИЗ
    	|	(" + ПраваНаВычетыТекст + ") КАК РаботникиОргПраваНаВычеты
    	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ДоходыЗаГодТекст + ") КАК ДоходыЗаГод
    	|		ПО ДоходыЗаГод.Период = РаботникиОргПраваНаВычеты.Период И ДоходыЗаГод.ФизЛицо = РаботникиОргПраваНаВычеты.Физлицо
    	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК НДФЛДоходыПредыдущегоМестаРаботы
    	|		ПО (НДФЛДоходыПредыдущегоМестаРаботы.Организация = &парамОрганизация) И НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода = РаботникиОргПраваНаВычеты.Период И НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо = РаботникиОргПраваНаВычеты.Физлицо";
    КонецЕсли;

	// ПрименяемыеВычетыЗаГод
	//	Поля:
	//		Физлицо
	//		Период
	//		ВычетыЛичныеЗаГод
	//		ВычетыДетскиеЗаГод
	//
	//	Описание:
	//	Выбирает по всем периодам года применяемые стандартные вычеты нарастающим итогом за год
	//

	ПрименяемыеВычетыЗаГодТекст = 
	"ВЫБРАТЬ
	|	ВычетыПоПериодам.Физлицо КАК Физлицо,
	|	Периоды.Период КАК Период,
	|	СУММА(ВычетыПоПериодам.ПрименяемыеВычетыЛичные) КАК ВычетыЛичныеЗаГод,
	|	СУММА(ВычетыПоПериодам.ПрименяемыеВычетыДетские) КАК ВычетыДетскиеЗаГод,
	|	СУММА(ВычетыПоПериодам.ПрименяемыеВычетыДетскиеИнвалидов) КАК ВычетыДетскиеИнвалидовЗаГод
	|ИЗ
	|	(" + ПериодыТекст + ") КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + ПрименяемыеВычетыТекст + ") КАК ВычетыПоПериодам
	|		ПО ВычетыПоПериодам.Период <= Периоды.Период И ВычетыПоПериодам.Физлицо = Периоды.Физлицо
	|
	|СГРУППИРОВАТЬ ПО
	|	ВычетыПоПериодам.Физлицо,
	|	Периоды.Период";
	
	// Приходы вычетов помесячные  
	Вычеты312ПоМесяцамТекст = 
	"ВЫБРАТЬ
	|	Остатки.ФизЛицо КАК ФизЛицо,
	|	Остатки.Период КАК Период,
	|	Остатки.Год КАК НалоговыйПериод,
	|	Остатки.РазмерНачальныйОстаток КАК Вычет312Остаток,
	|	0 КАК ПримененныеВычеты312
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.ОстаткиИОбороты(
	|		&парамНачалоГода,
	|		&парамКонецГода,
	|		Год,
	|		,
	|		КодВычетаИмущественный = &КодВычета312
	|           И Физлицо В (" + СписокФизлицТекст + ")
	|		    И Организация = &парамОрганизация) КАК Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Обороты.ФизЛицо,
	|	Обороты.Период,
	|	Обороты.Год,
	|	Обороты.РазмерПриход,
	|	Обороты.РазмерРасход
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.ОстаткиИОбороты(
	|		&парамНачалоГода,
	|		&парамКонецГода,
	|		Месяц,
	|		,
	|		КодВычетаИмущественный = &КодВычета312
	|           И Физлицо В (" + СписокФизлицТекст + ")
	|		    И Организация = &парамОрганизация) КАК Обороты";
	
	Вычеты311ПоМесяцамТекст = 
	"ВЫБРАТЬ
	|	Остатки.ФизЛицо КАК ФизЛицо,
	|	Остатки.Период КАК Период,
	|	Остатки.Год КАК НалоговыйПериод,
	|	Остатки.РазмерНачальныйОстаток КАК Вычет311Остаток,
	|	0 КАК ПримененныеВычеты311
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.ОстаткиИОбороты(
	|		&парамНачалоГода,
	|		&парамКонецГода,
	|		Год,
	|		,
	|		КодВычетаИмущественный = &КодВычета311
	|           И Физлицо В (" + СписокФизлицТекст + ")
	|		    И Организация = &парамОрганизация) КАК Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Обороты.ФизЛицо,
	|	Обороты.Период,
	|	Обороты.Год,
	|	Обороты.РазмерПриход,
	|	Обороты.РазмерРасход
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.ОстаткиИОбороты(
	|		&парамНачалоГода,
	|		&парамКонецГода,
	|		Месяц,
	|		,
	|		КодВычетаИмущественный = &КодВычета311
	|           И Физлицо В (" + СписокФизлицТекст + ")
	|		    И Организация = &парамОрганизация) КАК Обороты";
	
	// Приходы вычетов с начала года нарастающим итогом
	Вычеты312Текст = 
	"ВЫБРАТЬ
	|	Периоды.Физлицо КАК Физлицо,
	|	Периоды.Период КАК Период,
	|	СУММА(Обороты.Вычет312Остаток) КАК Вычет312Остаток,
	|	СУММА(Обороты.ПримененныеВычеты312) КАК Вычет312ЗаГод
	|ИЗ
	|	(" + ПериодыТекст + ") КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + Вычеты312ПоМесяцамТекст + ") КАК Обороты
	|		ПО Обороты.ФизЛицо = Периоды.Физлицо И Обороты.НалоговыйПериод = ГОД(Периоды.Период) И Обороты.Период МЕЖДУ НАЧАЛОПЕРИОДА(Периоды.Период, Год) И Периоды.Период   
    |
	|СГРУППИРОВАТЬ ПО
	|	Периоды.Физлицо,
	|	Периоды.Период";
	
	Вычеты311Текст = 
	"ВЫБРАТЬ
	|	Периоды.Физлицо КАК Физлицо,
	|	Периоды.Период КАК Период,
	|	СУММА(Обороты.Вычет311Остаток) КАК Вычет311Остаток,
	|	СУММА(Обороты.ПримененныеВычеты311) КАК Вычет311ЗаГод
	|ИЗ
	|	(" + ПериодыТекст + ") КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ (" + Вычеты311ПоМесяцамТекст + ") КАК Обороты
	|		ПО Обороты.ФизЛицо = Периоды.Физлицо И Обороты.НалоговыйПериод = ГОД(Периоды.Период) И Обороты.Период МЕЖДУ НАЧАЛОПЕРИОДА(Периоды.Период, Год) И Периоды.Период 
    |
	|СГРУППИРОВАТЬ ПО
	|	Периоды.Физлицо,
	|	Периоды.Период";
	
	// облагаемый доход с начала года
	ПолеОблагаемыйДоходЗаГод = "ЕСТЬNULL(ДоходыЗаГод.ОблагаемыйДоходЗаГод, 0)";

	// право на личный вычет за год
	ПолеВычетыЛичныеЗаГод = "ЕСТЬNULL(РаботникиОргВычетыЗаГод.ВычетыЛичныеЗаГод, 0)";

	// право на вычет на детей за год
	ПолеВычетыДетскиеЗаГод = "ЕСТЬNULL(РаботникиОргВычетыЗаГод.ВычетыДетскиеЗаГод, 0)";
	ПолеВычетыДетскиеИнвалидовЗаГод = "ЕСТЬNULL(РаботникиОргВычетыЗаГод.ВычетыДетскиеИнвалидовЗаГод, 0)";
	
	ПолеВычет312Остаток = "ЕСТЬNULL(Вычеты312.Вычет312Остаток, 0)";
	
	ПолеВычет311Остаток = "ЕСТЬNULL(Вычеты311.Вычет311Остаток, 0)";
	
	// размер вычета на детей с начала года
	ПолеПримененныеВычетыДетские = 
	"ВЫБОР 
	|	КОГДА ДоходыЗаГод.Резидент 
	|	ТОГДА 
	|		ВЫБОР 
	|			КОГДА " + ПолеОблагаемыйДоходЗаГод + " > " + ПолеВычетыДетскиеЗаГод + " 
	|				ТОГДА " + ПолеВычетыДетскиеЗаГод +" 
	|			ИНАЧЕ " + ПолеОблагаемыйДоходЗаГод + "
	|		КОНЕЦ 
	|	ИНАЧЕ 0 
	|КОНЕЦ";

	ПолеПримененныеВычетыДетскиеИнвалидов = 
	"ВЫБОР 
	|	КОГДА ДоходыЗаГод.Резидент 
	|	ТОГДА 
	|		ВЫБОР 
	|			КОГДА " + ПолеОблагаемыйДоходЗаГод + " > " + ПолеВычетыДетскиеЗаГод + " + " + ПолеВычетыДетскиеИнвалидовЗаГод + " 
	|				ТОГДА " + ПолеВычетыДетскиеИнвалидовЗаГод +" 
	|			КОГДА " + ПолеОблагаемыйДоходЗаГод +" > " + ПолеВычетыДетскиеЗаГод + " 
	|				ТОГДА " + ПолеОблагаемыйДоходЗаГод + " - " + ПолеВычетыДетскиеЗаГод + " 
	|			ИНАЧЕ 0 
	|		КОНЕЦ 
	|	ИНАЧЕ 0 
	|КОНЕЦ";
	// размер примененного вычета на себя с начала года
	ПолеПримененныеВычетыЛичные = 
	"ВЫБОР 
	|	КОГДА ДоходыЗаГод.Резидент 
	|	ТОГДА 
	|		ВЫБОР 
	|			КОГДА " + ПолеОблагаемыйДоходЗаГод + " > " + ПолеВычетыЛичныеЗаГод + " + " + ПолеВычетыДетскиеЗаГод + " + " + ПолеВычетыДетскиеИнвалидовЗаГод + " 
	|				ТОГДА " + ПолеВычетыЛичныеЗаГод + " 
	|			КОГДА " + ПолеОблагаемыйДоходЗаГод +" > "  + ПолеВычетыДетскиеЗаГод + " + " + ПолеВычетыДетскиеИнвалидовЗаГод + " 
	|				ТОГДА " + ПолеОблагаемыйДоходЗаГод + " - " + ПолеВычетыДетскиеЗаГод + " - " + ПолеВычетыДетскиеИнвалидовЗаГод + " 
	|			ИНАЧЕ 0 
	|		КОНЕЦ 
	|	ИНАЧЕ 0 
	|КОНЕЦ";
	// размер вычета по коду 312
	ПолеПримененныеВычеты312 = 
	"ВЫБОР 
	|	КОГДА " + ПолеОблагаемыйДоходЗаГод + " > " + ПолеПримененныеВычетыЛичные + " + " + ПолеПримененныеВычетыДетские + " - " + ПолеПримененныеВычетыДетскиеИнвалидов + " 
	|	ТОГДА 
	|		ВЫБОР 
	|			КОГДА " + ПолеВычет312Остаток + " > " + ПолеОблагаемыйДоходЗаГод + " - " + ПолеПримененныеВычетыЛичные + " - " + ПолеПримененныеВычетыДетские + " - " + ПолеПримененныеВычетыДетскиеИнвалидов + " 
	|				ТОГДА " + ПолеОблагаемыйДоходЗаГод + " - " + ПолеПримененныеВычетыЛичные + " - " + ПолеПримененныеВычетыДетские + " - " + ПолеПримененныеВычетыДетскиеИнвалидов + " 
	|			ИНАЧЕ " + ПолеВычет312Остаток + "
	|		КОНЕЦ 
	|	ИНАЧЕ 0 
	|КОНЕЦ";
	// размер вычета по коду 311
	ПолеПримененныеВычеты311 = 
	"ВЫБОР 
	|	КОГДА " + ПолеОблагаемыйДоходЗаГод + " > " + ПолеПримененныеВычеты312 + " + " + ПолеПримененныеВычетыЛичные + " + " + ПолеПримененныеВычетыДетские + " + " + ПолеПримененныеВычетыДетскиеИнвалидов + " 
	|	ТОГДА 
	|		ВЫБОР 
	|			КОГДА " + ПолеВычет311Остаток + " > " + ПолеОблагаемыйДоходЗаГод + " - " + ПолеПримененныеВычеты312 + " - " + ПолеПримененныеВычетыЛичные + " - " + ПолеПримененныеВычетыДетские + " - " + ПолеПримененныеВычетыДетскиеИнвалидов + " 
	|				ТОГДА " + ПолеОблагаемыйДоходЗаГод + " - " + ПолеПримененныеВычеты312 + " - " + ПолеПримененныеВычетыЛичные + " - " + ПолеПримененныеВычетыДетские + " - " + ПолеПримененныеВычетыДетскиеИнвалидов + " 
	|			ИНАЧЕ " + ПолеВычет311Остаток + "
	|		КОНЕЦ 
	|	ИНАЧЕ 0 
	|КОНЕЦ";
	
	// сумма налога исчисленная
	ПолеНалогИсчисленный = 
	"(ВЫРАЗИТЬ(ВЫБОР 
	|				КОГДА ДоходыЗаГод.Резидент 
	|				ТОГДА (" + ПолеОблагаемыйДоходЗаГод +" - "+ ПолеПримененныеВычетыЛичные + " - " + ПолеПримененныеВычетыДетские + " - " + ПолеПримененныеВычетыДетскиеИнвалидов + " - " + ПолеПримененныеВычеты312 + " - " + ПолеПримененныеВычеты311+ ")  * &парамСтавкаРезидента13 
	|				ИНАЧЕ ЕСТЬNULL(ДоходыЗаГод.НалогНерезидента, 0)
	|				КОНЕЦ КАК " + ТипЗначенияНДФЛТекст + "))";
	
	// сумма ранее исчисленного налога
	ПолеРанееНалогИсчисленный = 
	"ВЫБОР 
	|	КОГДА НДФЛРасчетыСБюджетомЗаГод.ИсчисленныйНалогЗаГод = -99999999 
	|	ТОГДА 0 
	|	ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетомЗаГод.ИсчисленныйНалогЗаГод, 0) 
	|КОНЕЦ";
	
	// сумма ранее примененных вычетов личных
	ПолеРанееПримененныеВычетыЛичные = "ЕСТЬNULL(НДФЛРасчетыСБюджетомЗаГод.ПримененныеВычетыЛичныеЗаГод, 0)";

	// сумма ранее примененных вычетов на детей
	ПолеРанееПримененныеВычетыДетские = "ЕСТЬNULL(НДФЛРасчетыСБюджетомЗаГод.ПолеПримененныеВычетыДетскиеЗаГод, 0)";
	ПолеРанееПримененныеВычетыДетскиеИнвалидов = "ЕСТЬNULL(НДФЛРасчетыСБюджетомЗаГод.ПолеПримененныеВычетыНаДетейИнвалидовЗаГод, 0)";

	// сумма ранее примененных вычетов по коду 312
	ПолеРанееПримененныеВычеты312 = "ЕСТЬNULL(Вычеты312.Вычет312ЗаГод, 0)";

	// сумма ранее примененных вычетов по коду 311
	ПолеРанееПримененныеВычеты311 = "ЕСТЬNULL(Вычеты311.Вычет311ЗаГод, 0)";
	
	// ИсчисленныйНДФЛ	
	//	Поля:
	//		Физлицо
	//		Период
	//		НалогУжеИсчисленный
	//		НалогИсчисленный
	//		ПримененныеВычетыЛичные
	//		ПолеПримененныеВычетыДетские
	//		ПолеПримененныеВычетыДетскиеИнвалидов
	//	    ПримененныеВычеты312
	//	    ПримененныеВычеты311
	//	
	//	Описание:
	//	Выбирает применяемые вычеты нарастающим итогом, доход нарастающим итогом и 
	//	исчисленный налог нарастающим итогом и производит расчет налога и примененных вычетов

	ИсчисленныйНДФЛТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаботникиОргВычетыЗаГод.Физлицо,
	|	РаботникиОргВычетыЗаГод.Период,
	|	ДоходыЗаГод.Резидент,
	|	" + ПолеНалогИсчисленный + "-" + ПолеРанееНалогИсчисленный + " КАК НалогИсчисленный,
	|	" + ПолеПримененныеВычетыЛичные + " - " + ПолеРанееПримененныеВычетыЛичные + " КАК ПримененныеВычетыЛичные,
	|	" + ПолеПримененныеВычетыДетские + " - " + ПолеРанееПримененныеВычетыДетские + " КАК ПримененныеВычетыДетские,
	|	" + ПолеПримененныеВычетыДетскиеИнвалидов + " - " + ПолеРанееПримененныеВычетыДетскиеИнвалидов + " КАК ПримененныеВычетыДетскиеИнвалидов,
	|	" + ПолеПримененныеВычеты312 + " - " + ПолеРанееПримененныеВычеты312 + " КАК ПримененныеВычеты312,
	|	" + ПолеПримененныеВычеты311 + " - " + ПолеРанееПримененныеВычеты311 + " КАК ПримененныеВычеты311
	|ИЗ
	|	("+ПрименяемыеВычетыЗаГодТекст+") КАК РаботникиОргВычетыЗаГод
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+НДФЛРасчетыСБюджетомЗаГодТекст+") КАК НДФЛРасчетыСБюджетомЗаГод
	|		ПО НДФЛРасчетыСБюджетомЗаГод.Период = РаботникиОргВычетыЗаГод.Период И НДФЛРасчетыСБюджетомЗаГод.ФизЛицо = РаботникиОргВычетыЗаГод.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ДоходыЗаГодТекст+") КАК ДоходыЗаГод
	|		ПО ДоходыЗаГод.Период = РаботникиОргВычетыЗаГод.Период И ДоходыЗаГод.ФизЛицо = РаботникиОргВычетыЗаГод.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+Вычеты312Текст+") КАК Вычеты312
	|		ПО Вычеты312.Период = РаботникиОргВычетыЗаГод.Период И Вычеты312.ФизЛицо = РаботникиОргВычетыЗаГод.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+Вычеты311Текст+") КАК Вычеты311
	|		ПО Вычеты311.Период = РаботникиОргВычетыЗаГод.Период И Вычеты311.ФизЛицо = РаботникиОргВычетыЗаГод.Физлицо
	|
	|	ГДЕ
	|		(" + ПолеНалогИсчисленный + " <> " + ПолеРанееНалогИсчисленный + " ИЛИ 
	|		" + ПолеПримененныеВычеты312 + " <> " + ПолеРанееПримененныеВычеты312 + " ИЛИ 
	|		" + ПолеПримененныеВычеты311 + " <> " + ПолеРанееПримененныеВычеты311 + " ИЛИ 
	|		" + ПолеПримененныеВычетыЛичные + " <> " + ПолеРанееПримененныеВычетыЛичные + " ИЛИ 
	|		" + ПолеПримененныеВычетыДетские + " <> " + ПолеРанееПримененныеВычетыДетские + " ИЛИ
	|		" + ПолеПримененныеВычетыДетскиеИнвалидов + " <> " + ПолеРанееПримененныеВычетыДетскиеИнвалидов +")
	|	УПОРЯДОЧИТЬ ПО РаботникиОргВычетыЗаГод.Физлицо.Наименование, РаботникиОргВычетыЗаГод.Период";

	Запрос.Текст = ИсчисленныйНДФЛТекст;

	Возврат Запрос.Выполнить().Выбрать();

КонецФункции  // ПолучитьДанныеНДФЛПоРегистратору

// определеяет ставку налогообложения по зкоду дохода
// Параметры:
//  КодДохода - ссылка на код дохода НДФЛ
// Возвращаемое значение:
//  число - ставка налогообложения в процентах
Функция СтавкаНалогообложенияДохода(КодДохода) Экспорт

	Если      КодДохода.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
		Возврат 13;

	ИначеЕсли КодДохода.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09 Тогда
		Возврат 9;

	ИначеЕсли КодДохода.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35 Тогда
		Возврат 35;

	КонецЕсли;

	Возврат 0;

КонецФункции

// Определяет значение ставки налога для СтавкиНалоообложенияРезидента
// Параметры:
//  СтавкаНалогообложенияРезидента - ссылка на перечисление "НДФЛСтавкиНалогообложенияРезидента"
// Возвращаемое значение:
//  число - ставка налога в процентах
Функция ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(СтавкаНалогообложенияРезидента) Экспорт

	Если      СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
		Возврат 13;

	ИначеЕсли СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09 Тогда
		Возврат 9;

	ИначеЕсли СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35 Тогда
		Возврат 35;

	КонецЕсли;

	Возврат 0;

КонецФункции

// Проверяет правильность заполнения реквизитов вида расчета 
// для некуоторых случаев выдает сообщение об бошибке
// для некоторых - возвращает текст сообщения
// 	Параметры:
//		ВидРасчета - объект Вид расчета
//		Отказ - признак отказа (проверка не прошла)
//	Возвращаемое значение:
//		ТекстСообщения - текст сообщения о результате проверки
Функция ПроверитьНастройкуВидаРасчета(ВидРасчета, Отказ) Экспорт

	МетаданныеВидаРасчета = ВидРасчета.Метаданные();
	
	Если ЗначениеНеЗаполнено(ВидРасчета.Наименование) Тогда
		СообщитьОбОшибке("Необходимо задать имя расчета!", Отказ);
	КонецЕсли; 

	Если ЗначениеНеЗаполнено(ВидРасчета.Код) Тогда
		СообщитьОбОшибке("Необходимо задать код расчета!", Отказ);
	КонецЕсли; 
	
	Если ЗначениеНеЗаполнено(ВидРасчета.СпособОтраженияВБухучете) Тогда
		СообщитьОбОшибке("Не указан способ отражения в бухучете!", Отказ);
	КонецЕсли;
	
	Если ЗначениеНеЗаполнено(ВидРасчета.КодДоходаЕСН) Тогда
		СообщитьОбОшибке("Не указан код дохода ЕСН!", Отказ);
	КонецЕсли;
	

	Если Отказ Тогда
	    Возврат "";
	КонецЕсли; 
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОПИСЫВАЮТ СВЯЗИ ПРЕДОПРЕДЕЛЕННЫХ ВИДОВ РАСЧЕТА И СПОСОБОВ ИХ РАСЧЕТА

// Сворачивает таблицу значений в которой расположены движения НДФЛСведенияОДоходах
// опирается на структуру метаданных - сворачивает все ресурсы по измерениям+реквизитам 
// Парамтеры:
//	ДвиженияНДФЛСведенияОДоходах - таблица значений
// Возвращаемое значение:
//	нет
Процедура СвернутьДвиженияНДФЛСведенияОДоходах(ДвиженияНДФЛСведенияОДоходах) Экспорт

	СтрокаСуммирования = "";

	Для Каждого Изм Из Метаданные.РегистрыНакопления["НДФЛСведенияОДоходах"].Ресурсы Цикл
		СтрокаСуммирования = СтрокаСуммирования + Изм.Имя +",";
	КонецЦикла;

	СтрокаСуммирования = Лев(СтрокаСуммирования, СтрДлина(СтрокаСуммирования)-1);// удалим последнюю запятую

	СтрокаГруппировки = "Регистратор,Период,";

	Для Каждого Изм Из Метаданные.РегистрыНакопления["НДФЛСведенияОДоходах"].Измерения Цикл
		СтрокаГруппировки = СтрокаГруппировки + Изм.Имя +",";
	КонецЦикла;

	Для Каждого Изм Из Метаданные.РегистрыНакопления["НДФЛСведенияОДоходах"].Реквизиты Цикл
		СтрокаГруппировки = СтрокаГруппировки + Изм.Имя +",";
	КонецЦикла;

	СтрокаГруппировки = Лев(СтрокаГруппировки, СтрДлина(СтрокаГруппировки)-1);// удалим последнюю запятую

	ДвиженияНДФЛСведенияОДоходах.Свернуть(СтрокаГруппировки,СтрокаСуммирования);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ВИДАМИ РАСЧЕТА

// Эта функция возвращает информацию о виде расчета из переданного соответствия
// Если в соответствии не найдена информация о виде расчета - она подготавливается и 
// помещается в соответствие
// Применяется для работы с формами, в которых активно требуется получать 
// информащцию о видах расчета, например, при выводе строки табличного поля
//
// Параметры:      
//	СведенияОВидахРасчета - соответствие, у котрого в качестве ключа - ПланВидовРасчетаСсылка, а 
//  				  в качестве значения - структура из элементов
//					  РазмерТребуется - булево - если да, то при вводе такого 
//										вида расчета требуется проставлять значение "размер" 
//										(суммы, проценты и проч. показатели, используемые при расчете)
//
//	ВидРасчета - ПланВидовРасчетаСсылка
//
// Возвращаемое значение:
//  Описанная выше структура
//
Функция ПолучитьСведенияОВидеРасчета(СведенияОВидахРасчета, ВидРасчета) Экспорт
	СведенияОВидеРасчета = СведенияОВидахРасчета[ВидРасчета];
	Если СведенияОВидеРасчета = Неопределено Тогда
		СведенияОВидеРасчета = Новый Структура("КодДоходаНДФЛ");
		ТипВР = ТипЗнч(ВидРасчета);
		Если ТипВР = Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций") Тогда
			СведенияОВидеРасчета.КодДоходаНДФЛ = ВидРасчета.КодДоходаНДФЛ;
		Иначе
			СведенияОВидеРасчета.КодДоходаНДФЛ = Справочники.ДоходыНДФЛ.КодДоходаПоУмолчанию;
		КонецЕсли;
		СведенияОВидахРасчета[ВидРасчета] = СведенияОВидеРасчета;
	КонецЕсли;
	Возврат СведенияОВидеРасчета;
	
КонецФункции  // ПолучитьСведенияОВидеРасчета

