
Функция ПолучитьНовыйСписокМПЗ() Экспорт

	СписокМПЗ = Новый ТаблицаЗначений;
	СписокМПЗ.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СписокМПЗ.Колонки.Добавить("Склад"		 , Новый ОписаниеТипов("СправочникСсылка.Склады"));
	СписокМПЗ.Колонки.Добавить("СчетУчета"	 , Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СписокМПЗ.Колонки.Добавить("Количество"	 , ПолучитьОписаниеТиповЧисла(18,3));

	Возврат СписокМПЗ;
	                                                                            
КонецФункции  

Функция ПолучитьТаблицуПартийПоСпискуМПЗ(СписокМПЗ, Период, Организация) Экспорт

	СписокМПЗ.Свернуть("Номенклатура,Склад,СчетУчета","Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокМПЗ.Номенклатура,
	|	СписокМПЗ.Склад,
	|	СписокМПЗ.СчетУчета,
	|	СписокМПЗ.Количество
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&СписокМПЗ КАК СписокМПЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК СчетУчета,
	|	ХозрасчетныйОстатки.Субконто1 КАК Номенклатура,
	|	ХозрасчетныйОстатки.Субконто2 КАК Склад,
	|	ХозрасчетныйОстатки.Субконто3 КАК Партия,
	|	ХозрасчетныйОстатки.Субконто3.Дата КАК ПартияДата,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, , &Субконто, Организация = &Организация) КАК ХозрасчетныйОстатки
	|		ПО СписокНоменклатуры.Номенклатура = ХозрасчетныйОстатки.Субконто1
	|			И СписокНоменклатуры.Склад = ХозрасчетныйОстатки.Субконто2
	|			И СписокНоменклатуры.СчетУчета = ХозрасчетныйОстатки.Счет
	|ГДЕ
	|	ХозрасчетныйОстатки.КоличествоОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПартияДата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СписокНоменклатуры";

	Субконто = Новый Массив;
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партия);
	
	Запрос.УстановитьПараметр("Период"		, Период);
//	Запрос.УстановитьПараметр("Период"		, КонецДня(Период));
	Запрос.УстановитьПараметр("Организация"	, Организация);
	Запрос.УстановитьПараметр("Субконто"	, Субконто);
	Запрос.УстановитьПараметр("СписокМПЗ"	, СписокМПЗ);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ТПартии = Новый ТаблицаЗначений;
	ТПартии.Колонки.Добавить("Номенклатура");
	ТПартии.Колонки.Добавить("Склад");
	ТПартии.Колонки.Добавить("СчетУчета");
	ТПартии.Колонки.Добавить("Партия");
	ТПартии.Колонки.Добавить("Количество");
	ТПартии.Колонки.Добавить("Сумма");
	
	СтруктураПоиска = Новый Структура("Номенклатура,Склад,СчетУчета");
	
	Для каждого СтрокаСписка Из СписокМПЗ Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаСписка);
		
		КоличествоСписать = СтрокаСписка.Количество;
		Выборка.Сбросить();
		Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
			НоваяСтрокаПартий = ТПартии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПартий, Выборка);
			Если Выборка.Количество > КоличествоСписать Тогда
				НоваяСтрокаПартий.Количество = КоличествоСписать;
				НоваяСтрокаПартий.Сумма = Выборка.Сумма/Выборка.Количество*КоличествоСписать;
				Прервать;		
			КонецЕсли; 
			КоличествоСписать = КоличествоСписать - Выборка.Количество;
		КонецЦикла;
	
	КонецЦикла; 
	
	Возврат ТПартии;
	
КонецФункции 
 