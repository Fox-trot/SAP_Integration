
// ПРОЦЕДУРЫ ПО ЗАЧЕТУ АВАНСА ////////////////////////////////////////////////////////
//
Процедура Закрыть(Документ, ПроводкиБУ, Выборка, ПараСчетов, КоличествоСубконто)
	
	СЧД=ПараСчетов.АктивныйСчет;
	СЧК=ПараСчетов.ПассивныйСчет;
	
	СД=?(ЗначениеЗаполнено(Выборка.СуммаОстатокДТ),Выборка.СуммаОстатокДТ,0);
	СК=?(ЗначениеЗаполнено(Выборка.СуммаОстатокКТ),Выборка.СуммаОстатокКТ,0);
	
	СДВ=0;
	СКВ=0;
	
	ЗакрыватьВВалюте=ложь;
	
	Если СЧД.Валютный Тогда
		ЗакрыватьВВалюте=Истина;
		СДВ=?(ЗначениеЗаполнено(Выборка.ВалютнаяСуммаОстатокДТ),Выборка.ВалютнаяСуммаОстатокДТ,0);
		СКВ=?(ЗначениеЗаполнено(Выборка.ВалютнаяСуммаОстатокКТ),Выборка.ВалютнаяСуммаОстатокКТ,0);
	КонецЕсли;	
	
	
	Сумма=0;
	СуммаВ=0;
	
	Если мин(СД,СК)<>0 Тогда
		
		Если мин(сд,ск)<0 Тогда
			
			СчетПоДебету=СЧД;
			СчетПоКредиту=СЧК;
			Сумма=-1*мин(СД,СК);
			СуммаВ=-1*мин(СДВ,СКВ);
			
		Иначе
			
			СчетПоДебету=СЧК;
			СчетПоКредиту=СЧД;
			Сумма=мин(СД,СК);
			СуммаВ=мин(СДВ,СКВ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Сумма<>0 или СуммаВ<>0 Тогда
		
		Проводка = ПроводкиБУ.Добавить();
		Проводка.Период = Документ.Дата;
		Проводка.Организация = Документ.Организация;
		Проводка.Содержание = "Зачет аванса";
		Проводка.Активность = Истина;
		Проводка.СчетДт = СчетПоДебету;
		Проводка.СчетКт = СчетПоКредиту;
		Проводка.Сумма = Сумма;
		
		Для н=1 По КоличествоСубконто Цикл
			УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, н, Выборка["Субконто"+Строка(н)]);
			УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, н, Выборка["Субконто"+Строка(н)]);
		КонецЦикла; 
		
		Если ЗакрыватьВВалюте Тогда
			
			Проводка.ВалютаДТ = Выборка.Валюта;
			Проводка.ВалютаКТ = Выборка.Валюта;
			Проводка.ВалютнаяСуммаДТ = СуммаВ;
			Проводка.ВалютнаяСуммаКТ = СуммаВ;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	
КонецПроцедуры	

Процедура СформироватьДвижения(Документ, АктивныйСчет, ПассивныйСчет, Контрагент = Неопределено,Договор = Неопределено, НеЗакрыватьПоДоговорам = Ложь) Экспорт
	
	ПроводкиБУ = Документ.Движения.Хозрасчетный;
	
	ПараСчетов = Новый Структура;
	ПараСчетов.Вставить("АктивныйСчет", АктивныйСчет);
	ПараСчетов.Вставить("ПассивныйСчет", ПассивныйСчет);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ";
	
	КоличествоСубконто = ПараСчетов.АктивныйСчет.ВидыСубконто.Количество();
	
	Если КоличествоСубконто>1 Тогда
		Если НеЗакрыватьПоДоговорам Тогда
			Если ПараСчетов.АктивныйСчет.ВидыСубконто[0].ВидСубконто=ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты Тогда
				КоличествоСубконто=1;	
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Для н=1 По КоличествоСубконто Цикл
		Запрос.Текст = Запрос.Текст + "ХозрасчетныйОстатки.Субконто"+строка(н)+" КАК Субконто"+строка(н)+",";
	КонецЦикла; 
	
	Запрос.Текст = Запрос.Текст +
	"
	|	СУММА(естьnull(ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет = &АктивныйСчет
	|			ТОГДА ХозрасчетныйОстатки.СуммаОстатокДт - ХозрасчетныйОстатки.СуммаОстатокКт
	|		КОНЕЦ,0)) КАК СуммаОстатокДт,
	|	СУММА(естьnull(ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет = &ПассивныйСчет
	|			ТОГДА ХозрасчетныйОстатки.СуммаОстатокКт - ХозрасчетныйОстатки.СуммаОстатокДт
	|		КОНЕЦ,0)) КАК СуммаОстатокКт";
	
	Если ПараСчетов.АктивныйСчет.Валютный Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|	,ХозрасчетныйОстатки.Валюта КАК Валюта,
		|	СУММА(естьnull(ВЫБОР
		|		КОГДА ХозрасчетныйОстатки.Счет = &АктивныйСчет
		|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт - ХозрасчетныйОстатки.ВалютнаяСуммаОстатокКт
		|		КОНЕЦ,0)) КАК ВалютнаяСуммаОстатокДт,
		|	СУММА(естьnull(ВЫБОР
		|		КОГДА ХозрасчетныйОстатки.Счет = &ПассивныйСчет
		|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстатокКт - ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт
		|		КОНЕЦ,0)) КАК ВалютнаяСуммаОстатокКт";
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст +
	"
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата,Счет=&АктивныйСчет или Счет=&ПассивныйСчет,,Организация=&Организация "+?(Контрагент = Неопределено,""," И Субконто1=&Контрагент ")+?(Договор = Неопределено,""," И Субконто2=&Договор ")+") КАК ХозрасчетныйОстатки
	|";
	
	
	Если КоличествоСубконто>0 или ПараСчетов.АктивныйСчет.Валютный Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|
		|СГРУППИРОВАТЬ ПО ";
	КонецЕсли;
	
	Для н=1 По КоличествоСубконто Цикл
		Запрос.Текст = Запрос.Текст +?(н>1,",","")+ "ХозрасчетныйОстатки.Субконто"+строка(н);
	КонецЦикла; 
	
	Если ПараСчетов.АктивныйСчет.Валютный Тогда
		Запрос.Текст = Запрос.Текст	+ ?(КоличествоСубконто>0,",","")+ "ХозрасчетныйОстатки.Валюта";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата",КонецДня(Документ.Дата)+1);
	Запрос.УстановитьПараметр("АктивныйСчет",ПараСчетов.АктивныйСчет);
	Запрос.УстановитьПараметр("ПассивныйСчет",ПараСчетов.ПассивныйСчет);
	Запрос.УстановитьПараметр("Контрагент",Контрагент);
	Запрос.УстановитьПараметр("Договор",Договор);
	Запрос.УстановитьПараметр("Организация",Документ.Организация);
	Результат = Запрос.Выполнить();
	
	ВыборкаСубконто = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСубконто.Следующий() Цикл
		Закрыть(Документ, ПроводкиБУ, ВыборкаСубконто, ПараСчетов, КоличествоСубконто);
	КонецЦикла;
	
КонецПроцедуры	

// Процедура вызов, предпологается вызов в событии "ПослеЗаписи" документа
//
Процедура ВыполнитьЗакрытиеАвансов(Документ, Счет, Контрагент = Неопределено, Договор = Неопределено,НеЗакрыватьПоДоговорам = Ложь, ПереоцениватьВалютныеСчета = Истина) Экспорт
	
	Если Не Документ.Проведен Тогда
		Возврат;
	КонецЕсли; 
	
	Если (Счет.Пустая())ИЛИ(Счет = Неопределено) Тогда
		Возврат;
	КонецЕсли; 
	
	АктивныйСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	ПассивныйСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	
	НайтиСоответствие(Счет, АктивныйСчет, ПассивныйСчет);
	
	Если (АктивныйСчет.Пустая())или(ПассивныйСчет.Пустая()) Тогда
		Возврат;
	КонецЕсли; 
	
	СформироватьДвижения(Документ, АктивныйСчет, ПассивныйСчет, Контрагент,Договор, НеЗакрыватьПоДоговорам);	
	
	Если ПереоцениватьВалютныеСчета Тогда
		Если АктивныйСчет.Валютный Тогда
			ПереоценитьСчетБУ(Документ, АктивныйСчет, Контрагент, Договор);
		КонецЕсли; 
		Если ПассивныйСчет.Валютный Тогда
			ПереоценитьСчетБУ(Документ, ПассивныйСчет, Контрагент, Договор);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры                                    
//////////////////////////////////////////////////////////////////////////////////////


// ПРОЦЕДУРЫ ПО ПЕРЕОЦЕНКЕ ВАЛЮТНЫХ СЧЕТОВ ///////////////////////////////////////////
//
Процедура СформироватьПроводкиПоПереоценкеБУ(Счет, ПроводкиБУ, Выборка, СтруктураКурса, НаборЗаписей,Документ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СчетаСОсобымПорядкомПереоценкиБУ.Счет
	|ИЗ
	|	РегистрСведений.СчетаСОсобымПорядкомПереоценкиБУ КАК СчетаСОсобымПорядкомПереоценкиБУ
	|ГДЕ
	|	СчетаСОсобымПорядкомПереоценкиБУ.Счет = &Счет";
	
	Запрос.УстановитьПараметр("Счет", Счет);
	
	РезультатПоиска = Запрос.Выполнить().Выбрать();
	
	Если Константы.МетодПереоценкиВалютныхДенежныхСредств.Получить() = Перечисления.МетодыПереоценкиВалютныхДенежныхСредств.МетодОтсроченногоСписания Тогда
		Если РезультатПоиска.Количество() = 0 Тогда
			СчетПрибыли=ПланыСчетов.Хозрасчетный.А9540;
			СчетУбытка=ПланыСчетов.Хозрасчетный.А9620;
		Иначе	
			СчетПрибыли=ПланыСчетов.Хозрасчетный.А6291;
			СчетУбытка=ПланыСчетов.Хозрасчетный.А3291;
		КонецЕсли; 
	Иначе
		СчетПрибыли=ПланыСчетов.Хозрасчетный.А9540;
		СчетУбытка=ПланыСчетов.Хозрасчетный.А9620;
	КонецЕсли;	
	
	Курс=СтруктураКурса.Курс;
	Кратность=СтруктураКурса.Кратность;
	
	Если Кратность=0 Тогда
		Кратность=1;
	КонецЕсли; 
	
	ОстатокСумовой=Выборка.СуммаОстатокДТ-Выборка.СуммаОстатокКТ;
	ОстатокВалютный=Выборка.ВалютнаяСуммаОстатокДТ-Выборка.ВалютнаяСуммаОстатокКТ;
	
	ОстатокСумовойНовый=Окр(Выборка.ВалютнаяСуммаОстатокДТ*Курс/Кратность-Выборка.ВалютнаяСуммаОстатокКТ*Курс/Кратность, 2);
	
	КоличествоСубконто=Счет.ВидыСубконто.Количество();
	
	Если ОстатокСумовой<>ОстатокСумовойНовый Тогда
		
		Проводка = НаборЗаписей.Добавить();
		Проводка.Период = Документ.Дата;
		Проводка.Организация = Документ.Организация;
		Проводка.Содержание = "Переоценка валюты";
		Проводка.Активность = Истина;
		Если ОстатокСумовойНовый-ОстатокСумовой>0 Тогда
			Проводка.СчетДт = Счет;
			Для н=1 По КоличествоСубконто Цикл
				УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, н, Выборка.Получить(н));
			КонецЦикла; 
			Проводка.ВалютаДТ = Выборка.Валюта;
			Проводка.СчетКт = СчетПрибыли;
			Для н=1 По КоличествоСубконто Цикл
				УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, н, Выборка.Получить(н));
			КонецЦикла; 
			УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, Счет);
			УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 4, Выборка.Валюта);

			Проводка.Сумма = ОстатокСумовойНовый - ОстатокСумовой;
		Иначе
			Проводка.СчетДт = СчетУбытка;
			Для н=1 По КоличествоСубконто Цикл
				УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, н, Выборка.Получить(н));
			КонецЦикла; 
			УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, Счет);
			УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 4, Выборка.Валюта);

			Проводка.СчетКт = Счет;
			Для н=1 По КоличествоСубконто Цикл
				УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, н, Выборка.Получить(н));
			КонецЦикла; 
			Проводка.ВалютаКТ = Выборка.Валюта;
			Проводка.Сумма =ОстатокСумовой-ОстатокСумовойНовый;
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры	

Процедура ПереоценитьСчетБУ(Документ, Счет, Контрагент = Неопределено, Договор = Неопределено) Экспорт

	Если Не Документ.Проведен Тогда
		Возврат;
	КонецЕсли; 
	
	ПроводкиБУ = Документ.Движения.Хозрасчетный;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК СуммаОстатокДт,
	|	ХозрасчетныйОстатки.СуммаОстатокКт КАК СуммаОстатокКт,
	|	ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт КАК ВалютнаяСуммаОстатокДт,
	|	ХозрасчетныйОстатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСуммаОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет = &Счет,
	|			,
	|			Организация = &Организация ";
	ТекстЗапроса = ТекстЗапроса + ?(Контрагент	= Неопределено, "", "И Субконто1 = &Контрагент ");
	ТекстЗапроса = ТекстЗапроса + ?(Договор		= Неопределено, "", "И Субконто2 = &Договор ");
	ТекстЗапроса = ТекстЗапроса +
	") КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.Счет.Валютный = ИСТИНА
	|ИТОГИ
	|	СУММА(СуммаОстатокДт),
	|	СУММА(СуммаОстатокКт),
	|	СУММА(ВалютнаяСуммаОстатокДт),
	|	СУММА(ВалютнаяСуммаОстатокКт)
	|ПО
	|	Валюта,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Дата"		,КонецДня(Документ.Дата)+1);
	Запрос.УстановитьПараметр("Счет"		,Счет);
	Запрос.УстановитьПараметр("Контрагент"	,Контрагент);
	Запрос.УстановитьПараметр("Договор"		,Договор);
	Запрос.УстановитьПараметр("Организация"	,Документ.Организация);
	Результат = Запрос.Выполнить();
	
	//Состояние("Переоценка счета "+строка(Счет.Код)+"  """+Счет.Наименование+"""");
	
	КоличествоСубконто=0;
	
	Для н=1 По Счет.ВидыСубконто.Количество() Цикл
		Если Счет.ВидыСубконто[н-1].ТолькоОбороты Тогда
			Продолжить;
		КонецЕсли; 
		КоличествоСубконто=КоличествоСубконто+1;
	КонецЦикла;
	
	НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Документ.Ссылка);
	НаборЗаписей.Прочитать();
	
	ВыборкаВалюта = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВалюта.Следующий() Цикл
		
		Если ЗначениеНеЗаполнено(ВыборкаВалюта.Валюта) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураКурса = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Документ.Дата, Новый Структура("Валюта", ВыборкаВалюта.Валюта));
		
		
		Если КоличествоСубконто>0 Тогда
			ВыборкаСубконто1 = ВыборкаВалюта.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСубконто1.Следующий() Цикл
				
				//ОбработкаПрерыванияПользователя();
				
				Если КоличествоСубконто>1 Тогда
					ВыборкаСубконто2 = ВыборкаСубконто1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаСубконто2.Следующий() Цикл
						Если КоличествоСубконто>2 Тогда
							ВыборкаСубконто3 = ВыборкаСубконто2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаСубконто3.Следующий() Цикл
								СформироватьПроводкиПоПереоценкеБУ(Счет,ПроводкиБУ,ВыборкаСубконто3,СтруктураКурса,НаборЗаписей,Документ);
							КонецЦикла;
						Иначе
							СформироватьПроводкиПоПереоценкеБУ(Счет,ПроводкиБУ,ВыборкаСубконто2,СтруктураКурса,НаборЗаписей,Документ);
						КонецЕсли; 
					КонецЦикла;
				Иначе
					СформироватьПроводкиПоПереоценкеБУ(Счет,ПроводкиБУ,ВыборкаСубконто1,СтруктураКурса,НаборЗаписей,Документ);
				КонецЕсли; 
			КонецЦикла;
		Иначе
			СформироватьПроводкиПоПереоценкеБУ(Счет,ПроводкиБУ,ВыборкаВалюта,СтруктураКурса,НаборЗаписей,Документ);
		КонецЕсли; 
		

	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры
//////////////////////////////////////////////////////////////////////////////////////

Процедура ПересчитатьВалютныеОстаткиВБазовуюВалюту(Документ) Экспорт
	
	Если Не Документ.Проведен Тогда
		Возврат;
	КонецЕсли; 
	
	АктивныйСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	ПассивныйСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	
	НайтиСоответствие(Документ.СчетУчетаАванса, АктивныйСчет, ПассивныйСчет);
	
	Если Не АктивныйСчет.Пустая() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Валюта,
		|	СУММА(ХозрасчетныйОстатки.СуммаОстатокДт) КАК Сумма,
		|	СУММА(ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт) КАК ВалютнаяСумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период,
		|			Счет = &Счет,
		|			,
		|			Субконто1 = &Контрагент
		|				И Субконто2 = &Договор) КАК ХозрасчетныйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.Валюта";
		
		Запрос.УстановитьПараметр("Контрагент", Документ.Контрагент);
		Запрос.УстановитьПараметр("Договор", Документ.ДоговорКонтрагента);
		Запрос.УстановитьПараметр("Счет", АктивныйСчет);
		Запрос.УстановитьПараметр("Период", КонецДня(Документ.Дата));
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Документ.Ссылка);
		НаборЗаписей.Прочитать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Валюта = Документ.БазоваяВалюта Тогда
				Продолжить;
			КонецЕсли; 
			
			Коэффициент = Документ.Коэффициенты.Найти(Выборка.Валюта, "Валюта");
			
			Если Коэффициент = Неопределено Тогда
				Сообщить("Не указано значение коэффициента для валюты "+Строка(Выборка.Валюта));
				Продолжить;
			КонецЕсли; 
			
			СуммаВПроводку = Выборка.ВалютнаяСумма;
			
			Если Не Коэффициент.Сумма = 0 Тогда
				СуммаВПроводку = Мин(Выборка.ВалютнаяСумма, Коэффициент.Сумма);
			КонецЕсли;  			
			
			БазоваяВалютнаяСумма = СуммаВПроводку * Коэффициент.Значение;
			
			Проводка = НаборЗаписей.Добавить();
			Проводка.Период      = Документ.Дата;
			Проводка.Организация = Документ.Организация;
			Проводка.Содержание = "";     
			
			Проводка.СчетДт     =  АктивныйСчет;
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,Документ.Контрагент);
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Документ.ДоговорКонтрагента);
			
			Проводка.СчетКт      = АктивныйСчет;
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Документ.Контрагент);
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,Документ.ДоговорКонтрагента);
			
			Если Проводка.СчетДт.Валютный Тогда
				Проводка.ВалютаДт = Документ.БазоваяВалюта;
				Проводка.ВалютнаяСуммаДт = БазоваяВалютнаяСумма; 
			КонецЕсли; 
			
			Если Проводка.СчетКт.Валютный Тогда
				Проводка.ВалютаКт = Выборка.Валюта;
				Проводка.ВалютнаяСуммаКт = СуммаВПроводку; 
			КонецЕсли; 
			
			Проводка.Сумма = Выборка.Сумма/Выборка.ВалютнаяСумма*СуммаВПроводку; 
			
		КонецЦикла;  
		
		НаборЗаписей.Записать();
		
	КонецЕсли; 
	
	Если Не ПассивныйСчет.Пустая() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Валюта,
		|	СУММА(ХозрасчетныйОстатки.СуммаОстатокКт) КАК Сумма,
		|	СУММА(ХозрасчетныйОстатки.ВалютнаяСуммаОстатокКт) КАК ВалютнаяСумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период,
		|			Счет = &Счет,
		|			,
		|			Субконто1 = &Контрагент
		|				И Субконто2 = &Договор) КАК ХозрасчетныйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.Валюта";
		
		Запрос.УстановитьПараметр("Контрагент", Документ.Контрагент);
		Запрос.УстановитьПараметр("Договор", Документ.ДоговорКонтрагента);
		Запрос.УстановитьПараметр("Счет", ПассивныйСчет);
		Запрос.УстановитьПараметр("Период", КонецДня(Документ.Дата));
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Документ.Ссылка);
		НаборЗаписей.Прочитать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Валюта = Документ.БазоваяВалюта Тогда
				Продолжить;
			КонецЕсли; 
			
			Коэффициент = Документ.Коэффициенты.Найти(Выборка.Валюта, "Валюта");
			
			Если Коэффициент = Неопределено Тогда
				Сообщить("Не указано значение коэффициента для валюты "+Строка(Выборка.Валюта));
				Продолжить;
			КонецЕсли; 
			
			СуммаВПроводку = Выборка.ВалютнаяСумма;
			
			Если Не Коэффициент.Сумма = 0 Тогда
				СуммаВПроводку = Мин(Выборка.ВалютнаяСумма, Коэффициент.Сумма);
			КонецЕсли;  			
			
			БазоваяВалютнаяСумма = СуммаВПроводку * Коэффициент.Значение;
			
			Проводка = НаборЗаписей.Добавить();
			Проводка.Период      = Документ.Дата;
			Проводка.Организация = Документ.Организация;
			Проводка.Содержание = "";     
			
			Проводка.СчетДт     =  ПассивныйСчет;
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,1,Документ.Контрагент);
			УстановитьСубконто(Проводка.СчетДт,Проводка.СубконтоДт,2,Документ.ДоговорКонтрагента);
			
			Проводка.СчетКт      = ПассивныйСчет;
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,1,Документ.Контрагент);
			УстановитьСубконто(Проводка.СчетКт,Проводка.СубконтоКт,2,Документ.ДоговорКонтрагента);
			
			Если Проводка.СчетДт.Валютный Тогда
				Проводка.ВалютаДт = Выборка.Валюта;
				Проводка.ВалютнаяСуммаДт = СуммаВПроводку; 
			КонецЕсли; 
			
			Если Проводка.СчетКт.Валютный Тогда
				Проводка.ВалютаКт = Документ.БазоваяВалюта;
				Проводка.ВалютнаяСуммаКт = БазоваяВалютнаяСумма; 
			КонецЕсли; 
			
			Проводка.Сумма = Выборка.Сумма/Выборка.ВалютнаяСумма*СуммаВПроводку; 
			
		КонецЦикла;  
		
		НаборЗаписей.Записать();
		
	КонецЕсли;        	
	
КонецПроцедуры
 
Процедура НайтиСоответствие(Счет, АктивныйСчет, ПассивныйСчет)
	
	Если Счет.Вид = ВидСчета.Активный Тогда
		
		АктивныйСчет = Счет;
		
		Соответствие = Справочники.СоответствияСчетовДляЗакрытияАвансов.НайтиПоРеквизиту("АктивныйСчет", Счет);
		
		Если Не Соответствие.Пустая() Тогда
			ПассивныйСчет = Соответствие.ПассивныйСчет;
		Иначе
			Сообщить("Не найден счет учета авансов для закрытия!", СтатусСообщения.Информация);
			Возврат;
		КонецЕсли; 
		
	ИначеЕсли Счет.Вид = ВидСчета.Пассивный Тогда

		ПассивныйСчет = Счет;
		
		Соответствие = Справочники.СоответствияСчетовДляЗакрытияАвансов.НайтиПоРеквизиту("ПассивныйСчет", Счет);
		
		Если Не Соответствие.Пустая() Тогда
			АктивныйСчет = Соответствие.АктивныйСчет;
		Иначе
			Сообщить("Не найден счет учета авансов для закрытия!", СтатусСообщения.Информация);
			Возврат;
		КонецЕсли; 
		
	ИначеЕсли Счет.Вид = ВидСчета.АктивноПассивный Тогда
		
		АктивныйСчет = Счет;
		
		Соответствие = Справочники.СоответствияСчетовДляЗакрытияАвансов.НайтиПоРеквизиту("АктивныйСчет", Счет);
		
		Если Не Соответствие.Пустая() Тогда
			ПассивныйСчет = Соответствие.ПассивныйСчет;
		Иначе
			Сообщить("Не найден счет учета авансов для закрытия!", СтатусСообщения.Информация);
			Возврат;
		КонецЕсли;   	
		
	КонецЕсли; 
	
КонецПроцедуры
 