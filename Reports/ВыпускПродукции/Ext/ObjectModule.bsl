Перем СохраненнаяНастройка Экспорт;
Перем Расшифровки Экспорт;

#Если Клиент Тогда

Перем Запрос;

Процедура УстановитьТекстЗапроса()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.КорСубконто1 КАК Подразделение,
	|	ХозрасчетныйОбороты.Субконто1 КАК Номенклатура,
	|	ХозрасчетныйОбороты.Субконто1.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов КАК ЕдиницаДляОтчетов,
	|	ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков КАК ЕдиницаХраненияОстатков,
	|	СУММА(ХозрасчетныйОбороты.КоличествоОборотДт) КАК КоличествоВЕИОстатков,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов.Коэффициент <> 0
	|				ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт * ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент / ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов.Коэффициент
	|		КОНЕЦ) КАК КоличествоВЕИДляОтчетов,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент <> 0
	|				ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт * ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент
	|		КОНЕЦ) КАК КоличествоВБазовойЕИ,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотДт / ХозрасчетныйОбороты.КоличествоОборотДт КАК ЦенаВЕИХранения,
	|	ХозрасчетныйОбороты.СуммаОборотДт / ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов.Коэффициент <> 0
	|			ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт * ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент / ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов.Коэффициент
	|	КОНЕЦ КАК ЦенаВЕИОтчетов,
	|	ХозрасчетныйОбороты.СуммаОборотДт / ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент <> 0
	|			ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт * ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент
	|	КОНЕЦ КАК ЦенаВБазовойЕИ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет = &Счет, , Организация = &Организация, КорСчет = &КорСчет, ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.КоличествоОборотДт <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Субконто1.БазоваяЕдиницаИзмерения,
	|	ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов,
	|	ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков,
	|	ХозрасчетныйОбороты.СуммаОборотДт / ХозрасчетныйОбороты.КоличествоОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотДт / ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов.Коэффициент <> 0
	|			ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт * ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент / ХозрасчетныйОбороты.Субконто1.ЕдиницаДляОтчетов.Коэффициент
	|	КОНЕЦ,
	|	ХозрасчетныйОбороты.СуммаОборотДт / ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент <> 0
	|			ТОГДА ХозрасчетныйОбороты.КоличествоОборотДт * ХозрасчетныйОбороты.Субконто1.ЕдиницаХраненияОстатков.Коэффициент
	|	КОНЕЦ,
	|	ХозрасчетныйОбороты.КорСубконто1";
	
	Запрос.Текст = ТекстЗапроса;

КонецПроцедуры
	
Процедура СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина, ВнешниеНаборыДанных = Неопределено) Экспорт
	
	Результат.Очистить();
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ВыводЗаголовкаОтчета(ЭтотОбъект, Результат);
	ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных);
	КомпоновщикНастроек.Восстановить();
	НастройкаКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	СтандартныеОтчеты.ВывестиОтчет(ЭтотОбъект, Результат, ДанныеРасшифровки, ВыводВФормуОтчета, ВнешниеНаборыДанных, Ложь, НастройкаКомпоновкиДанных);
		
	// Выполним дополнительную обработку Результата отчета
	ОбработкаРезультатаОтчета(Результат);
	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных) Экспорт
	
	// Данные для компоновщика получаем запросом и передаем в СКД таблицу значений
	Запрос = Новый Запрос;
	
	УстановитьТекстЗапроса();
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Хозрасчетный.НайтиПоКоду("2810"));
	Запрос.УстановитьПараметр("КорСчет", ПланыСчетов.Хозрасчетный.НайтиПоКоду("2010"));
	
	Выборка = Запрос.Выполнить();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("Выборка", Выборка);

	//ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "Период", Период);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТиповыеОтчеты.ДобавитьОтбор(КомпоновщикНастроек, "Организация", Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыводЗаголовкаОтчета(ОтчетОбъект, Результат)
	
	МакетЗаголовок = ПолучитьОбщийМакет("ЗаголовокОтчета");
	ОбластьЗаголовок = МакетЗаголовок.ПолучитьОбласть("Заголовок");
		
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = ПолучитьТекстЗаголовка();

	Результат.Вывести(ОбластьЗаголовок);
			
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(ОрганизацияВНачале = Истина) Экспорт 
	
	ЗаголовокОтчета = "Выпуск продукции";
	
	Возврат СтандартныеОтчеты.ПолучитьТекстЗаголовка(ЭтотОбъект, ЗаголовокОтчета, ОрганизацияВНачале);

КонецФункции

Процедура ОбработкаРезультатаОтчета(Результат)
	
	СтандартныеОтчеты.ОбработкаРезультатаОтчета(ЭтотОбъект, Результат);
	
КонецПроцедуры

// Для настройки отчета (расшифровка и др.)
Процедура Настроить(Отбор, КомпоновщикНастроекОсновногоОтчета = Неопределено) Экспорт
	
	ТиповыеОтчеты.НастроитьТиповойОтчет(ЭтотОбъект, Отбор, КомпоновщикНастроекОсновногоОтчета);
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт
	
	СтандартныеОтчеты.СохранитьНастройку(ЭтотОбъект);
	
КонецПроцедуры

// Процедура заполняет параметры отчета по элементу справочника из переменной СохраненнаяНастройка.
Процедура ПрименитьНастройку() Экспорт
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	 
	СтруктураПараметров = СохраненнаяНастройка.ХранилищеНастроек.Получить();
	ТиповыеОтчеты.ПрименитьСтруктуруПараметровОтчета(ЭтотОбъект, СтруктураПараметров);
	
КонецПроцедуры

Процедура ИнициализацияОтчета() Экспорт
	
	//ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "Период",  Период);
	
КонецПроцедуры

Расшифровки = Новый СписокЗначений;

НастройкаПериода = Новый НастройкаПериода;

#КонецЕсли