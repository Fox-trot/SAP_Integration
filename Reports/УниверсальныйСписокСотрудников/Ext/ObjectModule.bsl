Перем СохраненнаяНастройка Экспорт;
Перем Расшифровки Экспорт;

#Если Клиент Тогда

Перем ТабИнтервалы;
Перем Запрос;

Процедура УстановитьТекстЗапроса()

    ТекстЗапроса =
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Объект КАК ФизическоеЛицо,
	|	КонтактнаяИнформация.Представление КАК АдресПрописки
	|ПОМЕСТИТЬ АдресаПрописки
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтактнаяИнформация.Объект КАК ФизическоеЛицо,
	|	КонтактнаяИнформация.Представление КАК АдресПроживания
	|ПОМЕСТИТЬ АдресаПроживания
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресФизЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтактнаяИнформация.Объект КАК ФизическоеЛицо,
	|	КонтактнаяИнформация.Представление КАК НомерТелефона
	|ПОМЕСТИТЬ Телефоны
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонФизЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизаций.Сотрудник,
	|	РаботникиОрганизаций.Период КАК ДатаПриема
	|ПОМЕСТИТЬ ДатыПриема
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &Организация
	|	И РаботникиОрганизаций.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизаций.Сотрудник,
	|	РаботникиОрганизаций.Период КАК ДатаУвольнения
	|ПОМЕСТИТЬ ДатыУвольнения
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &Организация
	|	И РаботникиОрганизаций.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботникиОрганизаций.Сотрудник,
	|	РаботникиОрганизаций.Организация,
	|	РаботникиОрганизаций.ПодразделениеОрганизации КАК Подразделение,
	|	РаботникиОрганизаций.Должность,
	|	РаботникиОрганизаций.ТипСотрудника,
	|	РаботникиОрганизаций.Категория,
	|	РаботникиОрганизаций.ТабельныйНомер,
	|	РаботникиОрганизаций.НеРезидент,
	|	РаботникиОрганизаций.Надомник,
	|	РаботникиОрганизаций.НеУдерживатьПрофвзносы,
	|	ДанныеДляРасчетаЗП.ГрафикРаботы,
	|	ДанныеДляРасчетаЗП.Оклад,
	|	ДанныеДляРасчетаЗП.Тариф,
	|	ДанныеДляРасчетаЗП.ФормаТруда,
	|	ДанныеДляРасчетаЗП.Ставка,
	|	ДанныеДляРасчетаЗП.Разряд,
	|	ДанныеДляРасчетаПН.СпособРасчетаПН,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ДатаРождения КАК ДатаРождения,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ИНН КАК ИНН,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.Пол КАК Пол,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.МестоРождения КАК МестоРождения,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ИНПС КАК ИНПС,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.Национальность КАК Национальность,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ВУНомерВоенногоБилета КАК НомерВоенногоБилета,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ВУВоенкомат КАК Военкомат,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ВУГодностьКВоеннойСлужбе КАК ГодностьКВоеннойСлужбе,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ВУВоинскоеЗвание КАК ВоинскоеЗвание,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ВУСостав КАК ВоинскийСостав,
	|	РаботникиОрганизаций.Сотрудник.ФизическоеЛицо.ВУСпециальность КАК ВоинскаяСпециальность,
	|	ПаспортныеДанныеФизЛиц.ДокументСерия КАК ПаспортСерия,
	|	ПаспортныеДанныеФизЛиц.ДокументНомер КАК ПаспортНомер,
	|	ПаспортныеДанныеФизЛиц.ДокументДатаВыдачи КАК ПаспортДатаВыдачи,
	|	ПаспортныеДанныеФизЛиц.ДокументКемВыдан КАК ПаспортКемВыдан,
	|	ДатыПриема.ДатаПриема,
	|	ДатыУвольнения.ДатаУвольнения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПриема.ДатаПриема, &ПустаяДата) <> &ПустаяДата
	|			ТОГДА ВЫБОР
	|					КОГДА ДатыПриема.ДатаПриема <= &Период
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(ДатыУвольнения.ДатаУвольнения, &ПустаяДата) = &ПустаяДата
	|									ТОГДА ИСТИНА
	|								ИНАЧЕ ВЫБОР
	|										КОГДА ДатыУвольнения.ДатаУвольнения > &Период
	|											ТОГДА ИСТИНА
	|										ИНАЧЕ ЛОЖЬ
	|									КОНЕЦ
	|							КОНЕЦ
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Работает,
	|	Телефоны.НомерТелефона,
	|	АдресаПрописки.АдресПрописки,
	|	АдресаПроживания.АдресПроживания
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&Период, Организация = &Организация) КАК РаботникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДляРасчетаЗП.СрезПоследних(&Период, Организация = &Организация) КАК ДанныеДляРасчетаЗП
	|		ПО РаботникиОрганизаций.Сотрудник = ДанныеДляРасчетаЗП.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДляРасчетаПН.СрезПоследних(&Период, Организация = &Организация) КАК ДанныеДляРасчетаПН
	|		ПО РаботникиОрганизаций.Сотрудник = ДанныеДляРасчетаПН.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Телефоны КАК Телефоны
	|		ПО РаботникиОрганизаций.Сотрудник.ФизическоеЛицо = Телефоны.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&Период, ) КАК ПаспортныеДанныеФизЛиц
	|		ПО РаботникиОрганизаций.Сотрудник.ФизическоеЛицо = ПаспортныеДанныеФизЛиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПриема КАК ДатыПриема
	|		ПО РаботникиОрганизаций.Сотрудник = ДатыПриема.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыУвольнения КАК ДатыУвольнения
	|		ПО РаботникиОрганизаций.Сотрудник = ДатыУвольнения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПрописки КАК АдресаПрописки
	|		ПО РаботникиОрганизаций.Сотрудник.ФизическоеЛицо = АдресаПрописки.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПроживания КАК АдресаПроживания
	|		ПО РаботникиОрганизаций.Сотрудник.ФизическоеЛицо = АдресаПроживания.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АдресаПрописки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АдресаПроживания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Телефоны
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДатыПриема
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДатыУвольнения";
	
	Запрос.Текст = ТекстЗапроса;

КонецПроцедуры
	
Процедура СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина, ВнешниеНаборыДанных = Неопределено) Экспорт
	
	Результат.Очистить();
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ВыводЗаголовкаОтчета(ЭтотОбъект, Результат);
	ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных);
	КомпоновщикНастроек.Восстановить();
	НастройкаКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	СтандартныеОтчеты.ВывестиОтчет(ЭтотОбъект, Результат, ДанныеРасшифровки, ВыводВФормуОтчета, ВнешниеНаборыДанных, Ложь, НастройкаКомпоновкиДанных);
		
	// Выполним дополнительную обработку Результата отчета
	ОбработкаРезультатаОтчета(Результат);
	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных) Экспорт
	
	Запрос = Новый Запрос;
	
	УстановитьТекстЗапроса();
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.УстановитьПараметр("Организация", Организация);

	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеПоСотрудникам", Запрос.Выполнить());
	
	ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "Период", Период);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТиповыеОтчеты.ДобавитьОтбор(КомпоновщикНастроек, "Организация", Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыводЗаголовкаОтчета(ОтчетОбъект, Результат)
	
	МакетЗаголовок = ПолучитьОбщийМакет("ЗаголовокОтчета");
	ОбластьЗаголовок = МакетЗаголовок.ПолучитьОбласть("Заголовок");
		
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = ПолучитьТекстЗаголовка();

	Результат.Вывести(ОбластьЗаголовок);
			
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(ОрганизацияВНачале = Истина) Экспорт 
	
	ЗаголовокОтчета = "Универсальный список сотрудников";
	
	Возврат СтандартныеОтчеты.ПолучитьТекстЗаголовка(ЭтотОбъект, ЗаголовокОтчета, ОрганизацияВНачале);

КонецФункции

Процедура ОбработкаРезультатаОтчета(Результат)
	
	СтандартныеОтчеты.ОбработкаРезультатаОтчета(ЭтотОбъект, Результат);
	
КонецПроцедуры

// Для настройки отчета (расшифровка и др.)
Процедура Настроить(Отбор, КомпоновщикНастроекОсновногоОтчета = Неопределено) Экспорт
	
	ТиповыеОтчеты.НастроитьТиповойОтчет(ЭтотОбъект, Отбор, КомпоновщикНастроекОсновногоОтчета);
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт
	
	СтандартныеОтчеты.СохранитьНастройку(ЭтотОбъект);
	
КонецПроцедуры

// Процедура заполняет параметры отчета по элементу справочника из переменной СохраненнаяНастройка.
Процедура ПрименитьНастройку() Экспорт
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	 
	СтруктураПараметров = СохраненнаяНастройка.ХранилищеНастроек.Получить();
	ТиповыеОтчеты.ПрименитьСтруктуруПараметровОтчета(ЭтотОбъект, СтруктураПараметров);
	
КонецПроцедуры

Процедура ИнициализацияОтчета() Экспорт
	
	ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "Период",  Период);
	
КонецПроцедуры

Расшифровки = Новый СписокЗначений;

НастройкаПериода = Новый НастройкаПериода;

#КонецЕсли