Перем СохраненнаяНастройка Экспорт;
Перем Расшифровки Экспорт;

#Если Клиент Тогда

Перем Запрос;

Процедура УстановитьТекстЗапроса()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПродажиОбороты.Регистратор,
	|	ПродажиОбороты.Контрагент,
	|	ПродажиОбороты.Договор,
	|	ПродажиОбороты.Номенклатура,
	|	ПродажиОбороты.КоличествоОборот КАК КоличествоПродажи,
	|	ПродажиОбороты.СуммаОборот КАК СуммаПродажи,
	|	NULL КАК КоличествоСебестоимость,
	|	NULL КАК СуммаСебестоимость,
	|	ПродажиОбороты.СуммаОборот КАК Прибыль,
	|	ПродажиОбороты.Организация
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И ВидОборота = ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоПродажам.Реализация)) КАК ПродажиОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СебестоимостьПродажиОбороты.ДокументРеализации,
	|	СебестоимостьПродажиОбороты.Контрагент,
	|	СебестоимостьПродажиОбороты.Договор,
	|	СебестоимостьПродажиОбороты.Номенклатура,
	|	NULL,
	|	NULL,
	|	СебестоимостьПродажиОбороты.КоличествоОборот,
	|	СебестоимостьПродажиОбороты.СуммаОборот,
	|	-СебестоимостьПродажиОбороты.СуммаОборот,
	|	СебестоимостьПродажиОбороты.Организация
	|ИЗ
	|	РегистрНакопления.СебестоимостьПродажи.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И ВидОборота = ЗНАЧЕНИЕ(Перечисление.ВидыОборотовПоПродажам.Реализация)) КАК СебестоимостьПродажиОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Регистратор,
	|	ВТ.Контрагент,
	|	ВТ.Договор,
	|	ВТ.Номенклатура,
	|	СУММА(ВТ.КоличествоПродажи) КАК КоличествоПродажи,
	|	СУММА(ВТ.СуммаПродажи) КАК СуммаПродажи,
	|	СУММА(ВТ.КоличествоСебестоимость) КАК КоличествоСебестоимость,
	|	СУММА(ВТ.СуммаСебестоимость) КАК СуммаСебестоимость,
	|	СУММА(ВТ.Прибыль) КАК Прибыль,
	|	ВТ.Организация
	|ПОМЕСТИТЬ ВТСгрупированная
	|ИЗ
	|	ВТ КАК ВТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ.Регистратор,
	|	ВТ.Контрагент,
	|	ВТ.Договор,
	|	ВТ.Номенклатура,
	|	ВТ.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСгрупированная.Регистратор КАК Ссылка,
	|	ВТСгрупированная.Контрагент,
	|	ВТСгрупированная.Договор,
	|	ВТСгрупированная.Номенклатура,
	|	ВТСгрупированная.КоличествоПродажи КАК КоличествоПродажи,
	|	ВТСгрупированная.СуммаПродажи КАК СуммаПродажи,
	|	ВТСгрупированная.КоличествоСебестоимость КАК КоличествоСебестоимость,
	|	ВТСгрупированная.СуммаСебестоимость КАК СуммаСебестоимость,
	|	ВЫБОР
	|		КОГДА ВТСгрупированная.Прибыль < 0
	|			ТОГДА -ВТСгрупированная.Прибыль
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Убыток,
	|	ВТСгрупированная.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТСгрупированная.Организация,
	|	ВЫБОР
	|		КОГДА ВТСгрупированная.Прибыль > 0
	|			ТОГДА ВТСгрупированная.Прибыль
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Прибыль,
	|	ВТСгрупированная.Регистратор.Дата
	|ИЗ
	|	ВТСгрупированная КАК ВТСгрупированная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСгрупированная";
	
	Запрос.Текст = ТекстЗапроса;

КонецПроцедуры
	
Процедура СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина, ВнешниеНаборыДанных = Неопределено) Экспорт
	
	Результат.Очистить();
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ВыводЗаголовкаОтчета(ЭтотОбъект, Результат);
	ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных);
	КомпоновщикНастроек.Восстановить();
	НастройкаКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	СтандартныеОтчеты.ВывестиОтчет(ЭтотОбъект, Результат, ДанныеРасшифровки, ВыводВФормуОтчета, ВнешниеНаборыДанных, Ложь, НастройкаКомпоновкиДанных);
		
	// Выполним дополнительную обработку Результата отчета
	ОбработкаРезультатаОтчета(Результат);
	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных) Экспорт
	
	// Данные для компоновщика получаем запросом и передаем в СКД таблицу значений
	Запрос = Новый Запрос;
	
	УстановитьТекстЗапроса();
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(КонецПериода));
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Выборка.Колонки.Добавить("Регистратор",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100)));
	
	Для Каждого Строка Из Выборка Цикл
		
		Строка.Регистратор="№ "+ПолучитьНомерНаПечать(Строка.Ссылка)+" от "+Формат(Строка.РегистраторДата,"ДФ=dd.MM.yyyy");
		
	КонецЦикла;	
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("Выборка", Выборка);

	//ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "Период", Период);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТиповыеОтчеты.ДобавитьОтбор(КомпоновщикНастроек, "Организация", Организация);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыводЗаголовкаОтчета(ОтчетОбъект, Результат)
	
	МакетЗаголовок = ПолучитьОбщийМакет("ЗаголовокОтчета");
	ОбластьЗаголовок = МакетЗаголовок.ПолучитьОбласть("Заголовок");
		
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = ПолучитьТекстЗаголовка();

	Результат.Вывести(ОбластьЗаголовок);
			
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(ОрганизацияВНачале = Истина) Экспорт 
	
	ЗаголовокОтчета = "Себестоимость продажи";
	
	Возврат СтандартныеОтчеты.ПолучитьТекстЗаголовка(ЭтотОбъект, ЗаголовокОтчета, ОрганизацияВНачале);

КонецФункции

Процедура ОбработкаРезультатаОтчета(Результат)
	
	СтандартныеОтчеты.ОбработкаРезультатаОтчета(ЭтотОбъект, Результат);
	
КонецПроцедуры

// Для настройки отчета (расшифровка и др.)
Процедура Настроить(Отбор, КомпоновщикНастроекОсновногоОтчета = Неопределено) Экспорт
	
	ТиповыеОтчеты.НастроитьТиповойОтчет(ЭтотОбъект, Отбор, КомпоновщикНастроекОсновногоОтчета);
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт
	
	СтандартныеОтчеты.СохранитьНастройку(ЭтотОбъект);
	
КонецПроцедуры

// Процедура заполняет параметры отчета по элементу справочника из переменной СохраненнаяНастройка.
Процедура ПрименитьНастройку() Экспорт
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	 
	СтруктураПараметров = СохраненнаяНастройка.ХранилищеНастроек.Получить();
	ТиповыеОтчеты.ПрименитьСтруктуруПараметровОтчета(ЭтотОбъект, СтруктураПараметров);
	
КонецПроцедуры

Процедура ИнициализацияОтчета() Экспорт
	
	//ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "Период",  Период);
	
КонецПроцедуры

Расшифровки = Новый СписокЗначений;

НастройкаПериода = Новый НастройкаПериода;

#КонецЕсли