
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	РезультатСтруктура = ОбработчикиDidoxEImzo.ПрочитатьДанныеЭЦП();
	
	Если НЕ РезультатСтруктура.Успешно Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Невозможно загрузить сертификаты ЭЦП!");
		Возврат;
	КонецЕсли;
	
	МассивКлючей = РезультатСтруктура.МассивКлючей;
	МассивСтруктурЛичныхСертификатов = Новый Массив;
	Для каждого Соответствие Из МассивКлючей Цикл
		МассивСтруктурЛичныхСертификатов.Добавить(Соответствие.Получить("СтруктураКлюча"));		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МассивСтруктурСертификатов", МассивСтруктурЛичныхСертификатов);
	ПараметрыФормы.Вставить("ЗагрузкаСертификата", Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораСертификата", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСертификата", 
		ПараметрыФормы,	
		, 
		, 
		, 
		,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСертификата(СтруктураВозврата, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		
		ДопСвойства = ОбработчикиDidoxEImzo.ПолучитьДопПараметрыСертификата(СтруктураВозврата, Истина);
		
		Если ДопСвойства = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		НазваниеСправочникаОрганизации = ИмяПрикладногоСправочника("Организации");
		Если Не ЗначениеЗаполнено(НазваниеСправочникаОрганизации) Тогда
			НазваниеСправочникаОрганизации = "Организации";
		КонецЕсли;
		
		Организация = Неопределено;
		
		ЭлектронныеДокументыСлужебныйВызовСервера.ОпределитьОрганизацию(Организация);
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			
			Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораСертификатаПослеВыбораОрганизации", ЭтотОбъект, Новый Структура("СтруктураВозврата", СтруктураВозврата));
			ПоказатьВводЗначения(
				Оповещение,
				, // пропускаем начальное значение
				"Выбирите организацию, которой принадлежит сертификат",
				"СправочникСсылка." + НазваниеСправочникаОрганизации);
				
		Иначе
			
			ОбработкаВыбораСертификатаПослеВыбораОрганизации(Организация, Новый Структура("СтруктураВозврата", СтруктураВозврата));
			
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСертификатаПослеВыбораОрганизации(Организация, ДополнительныеПараметры) Экспорт
	
	СтруктураВозврата = ДополнительныеПараметры.СтруктураВозврата;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураВозврата.Вставить("Организация", Организация);
	Иначе
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	СсылкаНаОбъект = ЭлектронныеДокументыСлужебныйВызовСервера.ЗагрузитьСертификат(СтруктураВозврата, ОписаниеОшибки);
	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		ПоказатьПредупреждение(, ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОбъект);
	ОткрытьФорму("Справочник.СертификатыЭЦП.Форма.ФормаЭлемента", ПараметрыФормы);
	ОповеститьОбИзменении(СсылкаНаОбъект);
	Оповестить("ОбновитьСписокСертификатов");

КонецПроцедуры

&НаСервере
Функция ИмяПрикладногоСправочника(Название)
	
	Возврат ЭлектронныеДокументыПовтИсп.ПолучитьИмяПрикладногоСправочника(Название);
	
КонецФункции
