
Перем мЭтоНеНовый;


Функция СуществуютСсылки(ЕдиницаИзмерения) Экспорт

	Если ЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
		Возврат Ложь;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияНоменклатурыПродажНоменклатура.Ссылка
	|ИЗ
	|	Документ.РеализацияНоменклатурыПродаж.Номенклатура КАК РеализацияНоменклатурыПродажНоменклатура
	|ГДЕ
	|	РеализацияНоменклатурыПродажНоменклатура.ЕдиницаИзмерения = &ЕдиницаИзмерения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияНоменклатурыПродажЗаВалютуНоменклатура.Ссылка
	|ИЗ
	|	Документ.РеализацияНоменклатурыПродажЗаВалюту.Номенклатура КАК РеализацияНоменклатурыПродажЗаВалютуНоменклатура
	|ГДЕ
	|	РеализацияНоменклатурыПродажЗаВалютуНоменклатура.ЕдиницаИзмерения = &ЕдиницаИзмерения";

	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);

	Возврат не Запрос.Выполнить().Пустой();

КонецФункции //  СуществуютСсылки()

// Обработчик события ПередЗаписью формы.
//
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если мЭтоНеНовый = Неопределено Тогда
		мЭтоНеНовый = Не ЭтоНовый();
	КонецЕсли;

	Если Не ОбменДанными.Загрузка И Не ЭтоГруппа Тогда
		
		Если НЕ ЗначениеЗаполнено(БазоваяЕдиницаИзмерения) Тогда
			ТекстСообщения = "Не заполнена базовая единица измерения!";
			Сообщить(ТекстСообщения, СтатусСообщения.Важное);
			Отказ = Истина;
		Иначе
			// Надо проверить владельца единицы хранения остатков.
			Если ЗначениеЗаполнено(ЕдиницаХраненияОстатков)
			   И ЕдиницаХраненияОстатков.Владелец <> Ссылка Тогда
				ТекстСообщения = "У единицы хранения остатков номенклатуры """ + СокрЛП(Ссылка) + """ неверно указан владелец!";
				Сообщить(ТекстСообщения, СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ЕдиницаДляОтчетов) Тогда
			ЕдиницаДляОтчетов = ЕдиницаХраненияОстатков;
		КонецЕсли;

		Если ЗначениеЗаполнено(ЕдиницаДляОтчетов)
			И ЕдиницаДляОтчетов.Владелец <> Ссылка Тогда
			ТекстСообщения = "У единицы для отчетов номенклатуры """ + СокрЛП(Ссылка) + """ неверно указан владелец!";
			Сообщить(ТекстСообщения, СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;
		
		Если мЭтоНеНовый Тогда

			Если Не (ВидНоменклатуры=Перечисления.ВидыНоменклатурыПродаж.Работа или ВидНоменклатуры=Перечисления.ВидыНоменклатурыПродаж.Услуга)
				И Ссылка.ЕдиницаХраненияОстатков <> ЕдиницаХраненияОстатков И СуществуютСсылки(Ссылка.ЕдиницаХраненияОстатков) Тогда
				ТекстСообщения = "Единица """ + СокрЛП(Ссылка.ЕдиницаХраненияОстатков) + """ является единицей хранения остатков для """ + Наименование + """
				|и уже участвует в документах реализации. 
				|Изменить эту единицу уже нельзя!";
				Сообщить(ТекстСообщения, СтатусСообщения.Важное);
				Отказ = Истина;
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ПередЗаписью()

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	ДобавитьПрефиксУзла(Префикс);
КонецПроцедуры

