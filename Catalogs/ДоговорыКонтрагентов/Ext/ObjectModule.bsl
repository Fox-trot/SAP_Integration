////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью элемента справочника.
//
Процедура ПередЗаписью(Отказ, Сообщать = Истина) Экспорт

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ЭтоНовый() и не ПометкаУдаления=Ссылка.ПометкаУдаления тогда
		//В случае установки или снятия пометки удаления не производть проверкую
		Возврат;
	КонецЕсли;

	// Проверим можно ли изменять реквизиты договора.
	// Проверка осуществляется только если записывается уже существующий договор
	Если НЕ ЭтоНовый() Тогда

		Если ЭтоГруппа Тогда

			// Для группы владельца менять нельзя
			Если Владелец <> Ссылка.Владелец Тогда

				Если не Сообщать=Ложь тогда
					Сообщить("Нельзя изменять контрагента для группы договоров.", СтатусСообщения.Важное);
				КонецЕсли;
				Отказ = Истина;
			КонецЕсли; 

		Иначе

			// Проверим Заполнение обязательных реквизитов "Организация"
			Если ЗначениеНеЗаполнено(Организация) Тогда
				Если не Сообщать=Ложь тогда
					Сообщить("Для договора """ + Наименование + """ не указана организация.
							 |Элемент не записан.", 
							 СтатусСообщения.Важное);
				КонецЕсли;
				Отказ = Истина;
			КонецЕсли;

			// Проверим возможность смены владельца для договора
			Если Владелец <> Ссылка.Владелец Тогда

				Если КритерииОтбора.ДокументыПоДоговоруКонтрагента.Найти(Ссылка).Количество() > 0 Тогда

					Если не Сообщать=Ложь тогда
						Сообщить("Существуют документы, оформленные по договору """ + Наименование + """.
							 |Контрагент договора не может быть изменен, элемент не записан.", 
							 СтатусСообщения.Важное);
					КонецЕсли;
					Отказ = Истина;

				КонецЕсли;

			КонецЕсли;

			// Проверим возможность смены способа ведения взаиморасчетов и валюты взаиморасчетов
			Если ВидДоговора <> Ссылка.ВидДоговора
			 ИЛИ Организация <> Ссылка.Организация  Тогда
			    МассивДокументов = КритерииОтбора.ДокументыПоДоговоруКонтрагента.Найти(Ссылка);
			    ЕстьПроведенныеДокументы = Ложь;

				Для каждого Документ из МассивДокументов Цикл

					ЕстьПроведенныеДокументы = Документ.Проведен;

					Если ЕстьПроведенныеДокументы тогда
						Прервать;
					КонецЕсли;

				КонецЦикла;

				Если ЕстьПроведенныеДокументы Тогда
                    Если не Сообщать=Ложь тогда
						Сообщить("Существуют документы, проведенные по договору """ + Наименование + """.
								 |Реквизиты ""Организация"", ""Ведение взаиморасчетов"", ""Валюта взаиморасчетов"", ""Вид договора"", ""Расчеты в условных единицах"" не могут быть изменены.
								 |Элемент не записан.", 
								 СтатусСообщения.Важное);
					КонецЕслИ;
					Отказ = Истина;

				КонецЕсли;

			КонецЕсли;

		КонецЕсли; // Если ЭтоГруппа Тогда

	КонецЕсли; // Если НЕ ЭтоНовый() Тогда

	// Проверим заполнение обязательных реквизитов.
	Если Не ЭтоГруппа Тогда

		// Проверим Заполнение обязательных реквизитов "Организация"
		Если ЗначениеНеЗаполнено(Организация) Тогда
			Если не Сообщать=Ложь тогда
				Сообщить("Не указана организация для договора.", СтатусСообщения.Важное);
			КонецЕслИ;	
			Отказ = Истина;
		КонецЕсли; 

		//// Проверим, заполнена ли валюта
		//Если ЗначениеНеЗаполнено(ВалютаВзаиморасчетов) Тогда
		//	Если не Сообщать=Ложь тогда
		//		Сообщить("Не указана валюта договора ", СтатусСообщения.Важное);
		//	КонецЕсли;
		//	Отказ = Истина;
		//КонецЕсли;

		// Проверим, заполнен ли вид договора
		Если ЗначениеНеЗаполнено(ВидДоговора) Тогда
			Если не Сообщать=Ложь тогда
				Сообщить("Не указан вид договора ", СтатусСообщения.Важное);
			КонецЕсли;
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ПередЗаписью()

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	ДобавитьПрефиксУзла(Префикс);
КонецПроцедуры
